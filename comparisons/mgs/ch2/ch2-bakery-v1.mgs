include!("header.mgs")

/* ---------- LOOK SCRIPTS ---------- */

look_ch2_bakery {
	show serial dialog spacer;
	show serial dialog {
		"You looked around the <c>BAKERY</>."
		"\tThe place smells amazing! Warm, yeasty -- the very essence of nostalgia. The %Baker% keeps this place clean, tidy, and overflowing with baked goods."
		" "
	}
}

look_ch2_baker {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"\tHe rarely spares a glance for you as he rushes about, but his expression is warm when he does. He is smooth with his motions around the kitchen; it's clear he's had a lifetime of practice."
	}
}

/* ---------- ON_LOAD ---------- */

on_load-ch2-bakery {
	// entrance text
	show serial dialog spacer;
	show serial dialog { "Entering <bold>BAKERY</>..." }
	// The part of Aunt Zippy is being played by Jackob
	if (
		variable ch2_storyflag_round is 3
		|| variable ch2_storyflag_round is 4
	) {
		if (variable ch2_storyflag_round is 3) {
			teleport entity Jackob to geometry zippy-spot1;
		} else if (variable ch2_storyflag_round is 4) {
			teleport entity Jackob to geometry zippy-spot2;
		}
		set entity Jackob name to "Aunt Zippy";
		set entity Jackob on_look to look_ch2_zippy;
		set entity Jackob on_tick to null_script;
		set entity Jackob on_interact to interact_ch2_zippy;
		set entity Jackob type to lightning_boy;
		turn entity Jackob north;
	} else if (variable ch2_storyflag_round is not 2) {
		set entity Jackob on_tick to yeet;
	}
	// All the rest
	copy ch2_map_init;
}

/* ---------- ON_TICK ---------- */

on_tick-ch2-bakery {
	if (entity "%PLAYER%" is inside geometry south-hitbox) {
		set warp state to walk_from-bakery;
		load map ch2-town;
	}
	set map on_tick to on_tick-ch2-bakery;
}

/* ---------- EXIT STUFF ---------- */

on_go-ch2-bakery-south {
	copy warping_out_fade_out;
	load map ch2-town;
}

/* ---------- ENTITIES ---------- */

interact_ch2_baker {
	copy interrupt-walk;
	if (variable ch2_storyflag_round is >= 5) {
		show dialog {
			SELF "Good job building that computer. That's really impressive."
		}
	} else {
		show dialog {
			SELF "Sorry, can't talk now."
			"But good luck conquering the serial castle."
		}
	}
	copy resume-walk;
}

interact_ch2_jackob_bakery {
	copy face-player;
	show dialog {
		SELF "I'm just taking a little shopping break. Sometimes you just really want some fresh bread, you know?"
		"Mmmm, fresh bread!"
	}
	turn entity "%SELF%" south;
}
