include!("header.mgs")
/* ---------- DEBUG: Storyflag rounds ---------- */

// debug: "win" that storyflag round

debug-get-items-for-round1 {
	copy ch2_pickup_monitor;
	copy ch2_pickup_heatsink;
	copy ch2_pickup_powersupply;
}
debug-get-items-for-round2 {
	copy ch2_pickup_keyboard;
	copy ch2_pickup_mouse;
	copy ch2_pickup_needle;
	copy ch2_pickup_plate;
	copy ch2-solder-harddrive;
}
debug-get-items-for-round3 {
	copy ch2_pickup_ramchips;
	copy ch2_pickup_clock;
	copy ch2_pickup_goldfish;
	copy ch2_pickup_abacus;
	copy ch2-solder-cpu;
}
debug-set-flags-to-round0 {
	turn serial control on;
	set flag ch2-has-artifact to true;
	turn hex control on;
	mutate current-chapter = 2;
	set flag ch2_cutseen_birthday to true;
	// debug!("<y>debug-set-flags-to-round0")
}
debug-jump-to-round1 {
	copy debug-set-flags-to-round1;
	copy ch2_load_castle_1_special;
}
debug-jump-to-round2 {
	copy debug-set-flags-to-round2;
	copy ch2_load_castle_1_special;
}
debug-jump-to-round3 {
	copy debug-set-flags-to-round3;
	copy ch2_load_castle_1_special;
}
debug-jump-to-round4 {
	copy debug-set-flags-to-round4;
	copy ch2_load_castle_1_special;
}

debug-set-flags-to-round1 {
	copy debug-set-flags-to-round0;
	copy debug-finish-toot;
	copy debug-map-flags-for-round1;
}
debug-set-flags-to-round2 {
	copy debug-set-flags-to-round1;
	copy debug-get-items-for-round1;
	copy ch2-install-heatsink;
	copy ch2-install-powersupply;
	copy ch2-install-monitor;
	set flag ch2_cutseen_bert_secret to true;
	set flag ch2-lambda-bert-backstory to true;
	set flag ch2-parts-round-2-backstory to true;
	copy debug-map-flags-for-round2;
}
debug-set-flags-to-round3 {
	copy debug-set-flags-to-round2;
	copy debug-get-items-for-round2;
	copy ch2-install-harddrive;
	copy ch2-install-keyboard;
	copy ch2-install-mouse;
	set flag ch2_cutseen_bert_message to true;
	set flag ch2-parts-round-3-backstory to true;
	copy debug-map-flags-for-round3;
}
debug-set-flags-to-round4 {
	copy debug-set-flags-to-round3;
	copy debug-get-items-for-round3;
	copy ch2-install-ramchips;
	copy ch2-install-clock;
	copy ch2-install-cpu;
	set flag ch2_cutseen_last_parts_done to true;
	set flag ch2-warp-granted to true;
	set flag ch2-parts-round-4-backstory to true;
	copy debug-map-flags-for-round4;
}

/* ---------- PUT QUEST MARKERS ON MAP ---------- */

ch2-put_on_map-round1 {
	set flag ch2-map-monitor to true;
	set flag ch2-map-heatsink to true;
	set flag ch2-map-powersupply to true;
}
ch2-put_on_map-round2 {
	set flag ch2-map-keyboard to true;
	set flag ch2-map-mouse to true;
	set flag ch2-map-plate to true;
	set flag ch2-map-needle to true;
}
ch2-put_on_map-round3 {
	set flag ch2-map-clock to true;
	set flag ch2-map-goldfish to true;
	set flag ch2-map-abacus to true;
	set flag ch2-map-ramchips to true;
}
ch2-put_on_map-round4 {
	set flag ch2-map-mainframeos to true;
}
ch2-take_off_map-round1 {
	set flag ch2-map-monitor to false;
	set flag ch2-map-heatsink to false;
	set flag ch2-map-powersupply to false;
}
ch2-take_off_map-round2 {
	set flag ch2-map-keyboard to false;
	set flag ch2-map-mouse to false;
	set flag ch2-map-plate to false;
	set flag ch2-map-needle to false;
}
ch2-take_off_map-round3 {
	set flag ch2-map-clock to false;
	set flag ch2-map-goldfish to false;
	set flag ch2-map-abacus to false;
	set flag ch2-map-ramchips to false;
}
ch2-take_off_map-round4 {
	set flag ch2-map-mainframeos to false;
}

/* ---------- ACQUIRE ITEM ---------- */

// Also, remove quest marker from map

//ROUND 1

ch2_pickup_monitor {
	set flag ch2-map-monitor to false;
	set flag ch2_carrying_monitor to true;
	register look + monitor -> look-monitor;
}
ch2_pickup_powersupply {
	set flag ch2-map-powersupply to false;
	set flag ch2_carrying_powersupply to true;
	register look + "power supply" -> look-powersupply;
}
ch2-handoff-heatsink-to-cactuscooler {
	set flag ch2-map-heatsink to false;
	set flag ch2-map-cactuscooler-castle to true;
}
ch2-handoff-cactuscooler-to-bobaustin {
	set flag ch2-map-cactuscooler-castle to false;
	set flag ch2-map-cactuscooler-bobaustin to true;
}
ch2_pickup_cactuscooler {
	set flag ch2-map-cactuscooler-castle to false // just in case you went straight there
	set flag ch2-map-cactuscooler-bobaustin to false;
	set flag ch2-map-heatsink to true;
	set flag ch2_carrying_cactuscooler to true;
	register look + "cactus cooler" -> look-cactuscooler;
}
ch2_pickup_heatsink {
	set flag ch2-cactuscooler-delivered to true;
	set flag ch2-map-cactuscooler-castle to false // just in case you went straight there
	set flag ch2-map-heatsink to false;
	set flag ch2_carrying_heatsink to true;
	register look + "heat sink" -> look-heatsink;
}
ch2-unpickup-cactuscooler { // DEBUG
	// inverse of the above, lol, sorry for incorrect state
	set flag ch2-map-cactuscooler-bobaustin to true;
	set flag ch2-map-heatsink to false;
	set flag ch2_carrying_cactuscooler to false;
	unregister look + "cactus cooler";
}

// ROUND 2

ch2_pickup_keyboard {
	set flag ch2-map-keyboard to false;
	set flag ch2_carrying_keyboard to true;
	register look + keyboard -> look-keyboard;
}
ch2_pickup_mouse {
	set flag ch2-map-mouse to false;
	set flag ch2_carrying_mouse to true;
	register look + mouse -> look-mouse;
}
ch2_pickup_plate {
	set flag ch2-map-plate to false;
	set flag ch2_carrying_plate to true;
	if (flag ch2_carrying_needle is true) {
		set flag ch2-map-solder to true;
	}
	register look + plate -> look-plate;
}
ch2_pickup_needle {
	set flag ch2-map-needle to false;
	set flag ch2_carrying_needle to true;
	if (flag ch2_carrying_plate is true) {
		set flag ch2-map-solder to true;
	}
	register look + needle -> look-needle;
}
ch2-solder-harddrive {
	copy ch2-install-plate;
	copy ch2-install-needle;
	set flag ch2-map-solder to false;
	set flag ch2_carrying_harddrive to true;
	register look + "hard drive" -> look-harddrive;
}

// ROUND 3

ch2_pickup_clock {
	set flag ch2-map-clock to false;
	set flag ch2_carrying_clock to true;
	register look + clock -> look-clock;
}
ch2_pickup_goldfish {
	set flag ch2-map-goldfish to false;
	set flag ch2_carrying_goldfish to true;
	if (flag ch2_carrying_abacus is true) {
		set flag ch2-map-solder to true;
	}
	register look + goldfish -> look-goldfish;
}
ch2_pickup_manual {
	set flag ch2-map-manual to false;
	set flag ch2-map-abacus to true;
	set flag ch2_carrying_manual to true;
	register look + abacus -> look-abacus;
}
ch2-handoff-abacus-to-manual {
	set flag ch2-map-abacus to false;
	set flag ch2-map-manual to true;
}
ch2_pickup_abacus {
	set flag ch2-map-abacus to false;
	set flag ch2_carrying_abacus to true;
	if (flag ch2_carrying_goldfish is true) {
		set flag ch2-map-solder to true;
	}
	register look + abacus -> look-abacus;
}
ch2-solder-cpu {
	copy ch2-install-goldfish;
	copy ch2-install-abacus;
	set flag ch2-map-solder to false;
	set flag ch2_carrying_cpu to true;
	register look + cpu -> look-cpu;
}
ch2-handoff-ramchips-to-seamoss {
	set flag ch2-map-ramchips to false;
	set flag ch2-map-seamoss to true;
}
ch2_pickup_ramchips {
	set flag ch2-map-ramchips to false;
	set flag ch2-map-seamoss to false;
	set flag ch2_carrying_ramchips to true;
	register look + "ram chips" -> look-ramchips;
	register look + ram -> look-ramchips;
}

// ROUND 4

ch2_pickup_mainframeos {
	set flag ch2-map-mainframeos to false;
	set flag ch2_carrying_mainframeos to true;
	register look + mainframeos -> look-mainframeos;
}

/* ---------- ITEM "INSTALL" ---------- */

ch2-install-monitor {
	set flag ch2_carrying_monitor to false;
	set flag ch2_installed_monitor to true;
	unregister look + monitor;
}
ch2-turn_in-cactuscooler {
	set flag ch2_carrying_cactuscooler to false;
	unregister look + "cactus cooler";
	// picking up heatsink is currently a separate step
}
ch2-install-heatsink {
	set flag ch2_carrying_heatsink to false;
	set flag ch2_installed_heatsink to true;
	unregister look + "heat sink";
}
ch2-install-powersupply {
	set flag ch2_carrying_powersupply to false;
	set flag ch2_installed_powersupply to true;
	unregister look + "power supply";
}
ch2-install-keyboard {
	set flag ch2_carrying_keyboard to false;
	set flag ch2_installed_keyboard to true;
	unregister look + keyboard;
}
ch2-install-mouse {
	set flag ch2_carrying_mouse to false;
	set flag ch2_installed_mouse to true;
	unregister look + mouse;
}
ch2-install-harddrive {
	set flag ch2_carrying_harddrive to false;
	set flag ch2_installed_harddrive to true;
	unregister look + "hard drive";
}
ch2-install-plate {
	set flag ch2_carrying_plate to false;
	set flag ch2_installed_plate to true;
	unregister look + plate;
}
ch2-install-needle {
	set flag ch2_carrying_needle to false;
	set flag ch2_installed_needle to true;
	unregister look + needle;
}
ch2-install-ramchips {
	set flag ch2_carrying_ramchips to false;
	set flag ch2_installed_ramchips to true;
	unregister look + "ram chips";
	unregister look + ram;
}
ch2-install-clock {
	set flag ch2_carrying_clock to false;
	set flag ch2_installed_clock to true;
	unregister look + clock;
}
ch2-install-cpu {
	set flag ch2_carrying_cpu to false;
	set flag ch2_installed_cpu to true;
	unregister look + cpu;
}
ch2-install-goldfish {
	set flag ch2_carrying_goldfish to false;
	set flag ch2_installed_goldfish to true;
	unregister look + goldfish;
}
ch2-install-abacus {
	set flag ch2_carrying_abacus to false;
	set flag ch2_installed_abacus to true;
	unregister look + abacus;
}
ch2-install-mainframeos {
	set flag ch2_carrying_mainframeos to false;
	set flag ch2_installed_mainframeos to true;
	unregister look + mainframeos;
}

//--------------------------- SOLDER ZONE ---------------------------\\

interact_ch2_solderzone_harddrive {
	show dialog {
		PLAYER "Got the plate and the needle. All right, let's solder!"
		name "" "(You created a hard drive!)"
	}
	copy ch2-solder-harddrive;
}
interact_ch2_solderzone_cpu {
	show dialog {
		PLAYER "Got the goldfish and the abacus. All right, let's solder!"
		name "" "(You created a cpu!)"
	}
	copy ch2-solder-cpu;
}
interact_ch2_solderzone {
	if (flag ch2-parts-round-2-backstory is false) {
		show dialog {
			PLAYER "It's a metal pen resting inside a metal coil. But why is the pen plugged in? What does it do?"
		}
		return;
	}
	if (flag ch2-solder-sight is false) {
		show dialog {
			PLAYER "So this is a solder station."
		}
		set flag ch2-solder-sight to true;
	}
	// plate + needle = harddrive
	if (variable ch2_storyflag_round is 2) {
		if (
			flag ch2_installed_plate is false
			|| flag ch2_installed_needle is false
		) {
			if (flag ch2_carrying_plate is true) {
				if (flag ch2_carrying_needle is false) {
					show dialog {
						PLAYER "I have the plate. Now I just need the needle, and then I can solder them together here."
					}
				} else {
					copy interact_ch2_solderzone_harddrive;
				}
			} else if (flag ch2_carrying_needle is true) {
				if (flag ch2_carrying_plate is false) {
					show dialog {
						PLAYER "I have the needle. Now I just need the plate, and then I can solder them together here."
					}
				} else {
					copy interact_ch2_solderzone_harddrive;
				}
			} else {
				show dialog {
					PLAYER "When I have the plate and the needle, I can solder them together here."
				}
			}
		} else {
			show dialog { PLAYER "All done soldering for now." }
		}
	}
	// goldfish + abacus = cpu
	else if (variable ch2_storyflag_round is 3) {
		if (
			flag ch2_installed_goldfish is false
			|| flag ch2_installed_abacus is false
		) {
			if (flag ch2_carrying_goldfish is true) {
				if (flag ch2_carrying_abacus is false) {
					show dialog {
						PLAYER "I have the goldfish. Now I just need the abacus, and then I can solder them together here."
					}
				} else {
					copy interact_ch2_solderzone_cpu;
				}
			} else if (flag ch2_carrying_abacus is true) {
				if (flag ch2_carrying_goldfish is false) {
					show dialog {
						PLAYER "I have the abacus. Now I just need the goldfish, and then I can solder them together here."
					}
				} else {
					copy interact_ch2_solderzone_cpu;
				}
			} else {
				show dialog {
					PLAYER "When I have the goldfish and the abacus, I can solder them together here."
				}
			}
		} else {
			show dialog { PLAYER "All done soldering for now." }
		}
	} else {
		show dialog {
			PLAYER "Guess I'm all done soldering."
		}
	}
}