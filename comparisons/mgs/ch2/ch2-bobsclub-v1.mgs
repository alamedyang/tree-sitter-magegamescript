include!("header.mgs")

const! (
	$reset = 0
	$cupboard = 1
	$glasses = 2
	$fridge = 3
)

/* ---------- LOOK SCRIPTS ---------- */

look_ch2_bobsclub {
	show serial dialog spacer;
	show serial dialog {
		"You look around at the <m><bold>Bob's Club</>."
		"\tYou can hear muffled, rhythmic thumping through the floor, though there's no clear source."
		" "
	}
}

look_ch2_bobbarstool {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"\tThis lone piece of furniture feels out of place when not part of a lineup of identical pieces. It's a melancholy image, dwarfed in the open space."
	}
}

look_ch2_bobglasses {
	show serial dialog spacer;
	show serial dialog {
		"You looked at <m>%SELF%</>."
	}
	if (flag ch2_cutseen_bob_party is false) {
		show serial dialog {
			"\tFancy glass goblets for fancy parties. These pieces are definitely reserved for fancy drink orders. Not that you've ever seen a party in here."
		}
	} else {
		show serial dialog {
			"\tFancy glass goblets for fancy parties. These pieces are definitely reserved for fancy drink orders. Though from what you've seen, the Bob's Club partygoers prefer to use red plastic party cups."
		}
	}
}
look_ch2_bobsbox {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"It's still sealed. The sides are bulging, as though it was packed tightly with something."
	}
}
look_ch2_bobsbarrel {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"It's not marked, though you always assumed barrels like this would contain ale or beer or something. You suppose it could just as easily contain oyster crackers."
	}
}
look_ch2_bobcountertop {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"\tThere are hard water rings on the surface in several places. Most of it is dry, but some of the larger spots are still holding onto droplets of water. Did someone set some drinks here recently?"
	}
}

look_ch2_bobcupboard {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"\tGlass tumblers are packed together in lines on each shelf. None are missing; there are no gaps."
	}
}
look_ch2_bobsink {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"\tThere's splashes of water -- and an emaciated ice cube -- near the drain. Someone has used the sink recently."
	}
}
look_ch2_bobfridge {
	show serial dialog spacer;
	show serial dialog {
		"You looked at the <m>%SELF%</>."
		"\tIt's a larger-than-normal, industral-style refrigerator. There are a handful of small magnets on the front, but they mostly consist of the letters \"O\" and \"B\"."
	}
}

/* ---------- ON_LOAD ---------- */

// DEBUG
find_cc { set flag ch1_found_cactuscooler to true; }
unfind_cc { set flag ch1_found_cactuscooler to false; }
want_cc { set flag ch2_want_cactuscooler to true; }
unwant_cc { set flag ch2_want_cactuscooler to false; }
basement { set flag ch2_cutseen_bob_party to true; }
unbasement { set flag ch2_cutseen_bob_party to false; }
toot {
	set flag ch2_toot_done to true;
	copy register_parts;
}
untoot {
	set flag ch2_toot_done to false;
	unregister parts;
}
reset {
	set flag ch2_carrying_cactuscooler to false;
	set flag ch2_want_cactuscooler to false;
	set flag ch1_found_cactuscooler to false;
	set flag ch2_cutseen_bob_party to false;
	set flag ch2_toot_done to false;
	mutate ch2_rattle_count = 0;
	show serial dialog {
		"RESET"
	}
}

// END DEBUG

on_load_ch2_bobsclub {
	// entrance text
	show serial dialog spacer;
	show serial dialog { "Entering <bold>BOB'S CLUB</>..." }
	// do unique stuff first (but turn serial / player control off first, if you do)
	mutate ch2_rattle_count = 0;
	
	copy ch2_map_init;

	// DEBUG
	if (debug mode is on) {
		register find + cc -> find_cc;
		register unfind + cc -> unfind_cc;
		register want + cc -> want_cc;
		register unwant + cc -> unwant_cc;
		register basement -> basement;
		register unbasement -> unbasement;
		register toot -> toot;
		register untoot -> untoot;
		register carry + cc -> ch2_pickup_cactuscooler;
		register uncarry + cc -> ch2_unpickup_cactuscooler;
		register reset -> reset;
		show serial dialog {
			" "
			"<y>DEBUG></> Bob's Club</>"
			"<y>></> <c>FIND CC</>: you found CC in ch1"
			"<y>></> <c>UNFIND CC</>: you didn't find CC in ch1"
			"<y>></> <c>WANT CC</>: you know you want CC"
			"<y>></> <c>UNWANT CC</>: you don't yet know you want CC"
			"<y>></> <c>CARRY CC</>: you have CC on you"
			"<y>></> <c>UNCARRY CC</>: you don't have CC on you"
			"<y>></> <c>BASEMENT</>: you've been to the Bob's Club basement already"
			"<y>></> <c>UNBASEMENT</>: you've haven't been to the Bob's Club basement yet"
			"<y>></> <c>TOOT</>: you've finished Lambda's artifact tutorial already"
			"<y>></> <c>UNTOOT</>: you haven't finished Lambda's artifact tutorial yet"
			"<y>></> <c>RESET</>: set all above flags to false, and set the 'heard a dubstep rattle' to 0"
		}
	}
	// END DEBUG

	// Have NOT been in the basement
	if (flag ch2_cutseen_bob_party is false) {
		turn player control off;
		if (warp state is not warped) {
			walk entity "%PLAYER%" to geometry "walk-spot" over 400ms;
		}
		wait 220ms;
		// Intro words
		if (flag ch2_saw_emptybobroom is false) {
			set entity "%PLAYER%" on_tick to look_left_and_right_fast;
			show dialog {
				PLAYER "Huh? That's weird. Where did everyone go?"
				"And what's that muffled thumping noise?"
			}
			set flag ch2_saw_emptybobroom to true;
		} else {
			show dialog {
				PLAYER "Huh. Still empty... apart from that muffled thumping noise."
			}
		}
		copy player_look_down;
		// CC followup
		if (flag ch2_want_cactuscooler is true) { // You KNOW you need CC
			if (flag ch1_found_cactuscooler is true) {
				show dialog {
					PLAYER "At least I can grab some Cactus Cooler while I'm here. I know I saw some in the fridge yesterday."
				}
			}
			else {
				show dialog {
					PLAYER "There might be Cactus Cooler around somewhere, though. This place has drinks and stuff, right?"
				}
			}
		}
		else { // You DON'T know you need CC
			show dialog {
				PLAYER "Maybe I should take a look around."
			}
		}
		turn player control on;
	}
	else { // You HAVE been in the basement
		if (flag ch2_want_cactuscooler is true) { // If you know you need CC
			if ( // You've already collected the CC
				flag ch2_carrying_cactuscooler is true
				|| flag ch2_cactuscooler_delivered is true
			) {
				// DO NOTHING
			}
			else {
				// REMIND THEM TO GET IT!
				if (flag ch1_found_cactuscooler is true) {
					show dialog {
						PLAYER "Oh, yeah. I found Cactus Cooler in the fridge here yesterday. I should grab some while I'm here."
					}
				}
				else {
					show dialog {
						PLAYER "Oh, right. They had drinks and stuff at the party. I wonder if they'll have a rare soda like Cactus Cooler?"
					}
				}
			}
		}
	}
}

/* ---------- ON_TICK ---------- */

on_tick_ch2_bobsclub {
	if (entity "%PLAYER%" is inside geometry south-hitbox) {
		set warp state to walk_from-bobsclub;
		load map ch2-town;
	}
	set map on_tick to on_tick_ch2_bobsclub; // reset
}

/* ---------- EXIT STUFF ---------- */

on_go_ch2_bobsclub_stairs {
	copy warping_out_fade_out;
	load map ch2-bobsclub-basement;
}
on_go_ch2_bobsclub_south {
	copy warping_out_fade_out;
	load map ch2-town;
}

/* ---------- ENTITIES ---------- */

interact_ch2_bobbox {
	if (flag ch2_cutseen_bob_party is false) {
		show dialog {
			PLAYER "It's a box of unfilled balloons. There used to be a ton more of them though."
		}
	}
	else {
		show dialog {
			PLAYER "I guess they didn't need this box of balloons downstairs."
		}
	}
}
interact_ch2_bobbarstool {
	if (flag ch2_cutseen_bob_party is false) {
		show dialog {
			PLAYER "Only one stool for the whole bar? Where's the rest?"
		}
	}
	else {
		show dialog {
			PLAYER "They just left this stool by itself, huh? Guess there wasn't room downstairs."
		}
	}
}
interact_ch2_bobbarrel {
	show dialog {
		PLAYER "A beer keg maybe?"
	}
}
interact_ch2_bobcountertop {
	if (flag ch2_cutseen_bob_party is false) {
		show dialog {
			PLAYER "I KNOW there used to be bottles by the sink! Where did everything go?"
		}
	} // (no need to say anything once the basement is discovered)
}

// CALLBACK (ish) STUFF

interact_ch2_bobcupboard {
	if (flag ch2_touched_bobcupboard is false) {
		show dialog {
			PLAYER "Those are some hardy-looking glasses and tumblers."
		}
		set flag ch2_touched_bobcupboard to true;
	}
	else {
		show dialog {
			PLAYER "Yup, still glasses and tumblers in there."
		}
	}
	copy ch2_bobrattling;
}
interact_ch2_bobglasses {
	show dialog {
		PLAYER "These glasses look super fancy."
	}
	copy ch2_bobrattling;
}
interact_ch2_bobfridge {
	if ( // You've already collected the CC
		flag ch2_carrying_cactuscooler is true
		|| flag ch2_cactuscooler_delivered is true
	) {
		show dialog { PLAYER "Nothing in here." }
	}
	else {
		if (flag ch1_found_cactuscooler is false) {
			show dialog {
				PLAYER "I would have thought there'd be some soda in here, but nothing. Not even a box of baking soda."
			}
		}
		else {
			show dialog {
				PLAYER "I could have sworn there was Cactus Cooler in here yesterday, but it's gone now."
			}
		}
	}
	copy ch2_bobrattling;
}

player_look_down {
	set entity "%PLAYER%" on_tick to null_script;
	turn entity "%PLAYER%" south;
}

ch2_bobrattling { // Hint that there's more inside the building
	if (flag ch2_cutseen_bob_party is false) {
		set entity "%PLAYER%" on_tick to look_left_and_right_fast;
		// naive only
		if (variable ch2_rattle_count is 0) {
			show dialog {
				PLAYER "...Jeez, that weird thumping is actually rattling the shelves! Is that... music? Loud music?"
			}
			copy player_look_down;
			show dialog {
				PLAYER "Where the heck is it coming from? This is the only room in the building!"
				"...Right?"
			}
		}
		else if (variable ch2_rattle_count is 1) {
			show dialog {
				PLAYER "...Okay, that thumping is DEFINITELY music. I think the bass just dropped."
			}
			show dialog {
				PLAYER "It sounds like the speaker is nearby, like just beyond a wall. But the only exit to this room is the main entrance."
			}
		}
		else if (variable ch2_rattle_count is 2) {
			show dialog {
				PLAYER "...Wow, I think what I'm hearing is actually dubstep. It's muffled, but it's so loud it must be practically next door."
			}
			copy player_look_down;
			show dialog {
				PLAYER "Is it under the floor or something? I keeping looking but there aren't any stairs!"
			}
		}
		// you have the artifact and can get more specific hints
		else if (variable ch2_rattle_count is 3) {
			show dialog {
				PLAYER "...Okay, that music is starting to get on my nerves. But where..."
			}
			copy player_look_down;
			show dialog {
				PLAYER "...Oh, that's right! The artifact can identify the exits for each room!"
				"I wonder if it could show me the way to a secret room."
				"...With secret dubstep."
			}
		}
		else if (variable ch2_rattle_count is 4) {
			copy player_look_down;
			show dialog {
				PLAYER "...Ah, that's right, I remember now. I can use the artifact to LOOK around the room. Maybe I can find the source of the music that way."
			}
		}
		else {
			copy player_look_down;
			show dialog {
				PLAYER "...But enough distractions. I should use the LOOK command in the serial console to discover any secret doors."
			}
		}
		copy player_look_down;

		// advance the count
		mutate ch2_rattle_count + 1;
		if (flag ch2_toot_done is false) {
			mutate ch2_rattle_count % 3;
		}
	}
}
