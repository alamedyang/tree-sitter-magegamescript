include!("header.mgs")

/* --------------- on_tick --------------- */

// goose mission

on_tick_goose_mission {
	// on_tick is 'mission' -- if rake not in lake, jump
	if entity Rake is not inside geometry lake then goto on_tick_goose_mission_rake_out_of_lake;
	turn entity Goose south;
	set entity Goose current_animation to 0;
	set flag storyflag_rake to true;
	copy calculate_hint_tracking_series;
	if (variable hint_tracking_check is 10) {
		// if the hint series is the currently goose, reset hint
		copy set_hint_none;
	}
	// you win!
	goto null_script;
}
on_tick_goose_mission_rake_out_of_lake {
	// if rake is approx at start point, jump
	if entity Rake is inside geometry rake-point then goto goose_misison_rake_on_rake_point;
	turn entity Goose south;
	copy honk;
	walk entity Goose to geometry goose_midpoint over 600ms;
	walk entity Goose to geometry goose_lakepoint over 600ms;
	turn entity Goose south;
	goto on_tick_goose_mission;
}
goose_misison_rake_on_rake_point {
	// if rake has left its start point, jump
	if entity Rake is not inside geometry rake-point then goto on_tick_goose_mission_rake_out_of_lake;
	turn entity Goose south;
	copy honk;
	walk entity Goose to geometry goose_rakepoint over 1200ms;
	turn entity Goose toward entity Rake;
	// if rake has left its start point, jump
	if entity Rake is not inside geometry rake-point then goto on_tick_goose_mission_rake_out_of_lake;
	copy honk;
	walk entity Goose to geometry goose_lakepoint over 1200ms;
	turn entity Goose south;
	// if rake has left its start point, jump
	if entity Rake is not inside geometry rake-point then goto on_tick_goose_mission_rake_out_of_lake;
	goto on_tick_goose_mission;
}

honk {
	play entity Goose animation 2 thrice;
}
honk_to_make_rake_float {
	turn entity Goose toward entity Rake;
	wait 200ms; /**/ set entity Goose current_animation to 2;
	wait 600ms; /**/ set entity Goose on_tick to make_rake_float;
	wait 3000ms; /**/ set entity Goose current_animation to 0;
}

// drifting rake

make_rake_float { // on_tick
	set entity Rake on_tick to rake_drift_south;
	wait 600ms; /**/ set entity Rake on_tick to rake_drift_west;
	wait 600ms; /**/ set entity Rake on_tick to rake_drift_south;
	wait 600ms; /**/ set entity Rake on_tick to rake_drift_east;
	wait 600ms;
}
rake_drift_south {
	walk entity Rake to geometry drift-south over 60000ms;
}
rake_drift_east {
	walk entity Rake to geometry drift-east over 60000ms;
}
rake_drift_west {
	walk entity Rake to geometry drift-west over 60000ms;
}

// goose rescue protocol

goose_bounds_watch {
	if entity Goose is not inside geometry bounds then goto rescue_goose;
}
rescue_goose {
	set entity Goose on_tick to null_script;
	teleport entity Goose to geometry goose-rescue;
	walk entity Goose to geometry goose_lakepoint over 2000ms;
	set entity Goose on_tick to on_tick_goose_mission;
	set entity "%SELF%" on_tick to goose_bounds_watch;
}

/* --------------- dialog --------------- */

show_dialog_goose_generic {
	copy face_player;
	show dialog { SELF "Honk!" }
}

show_dialog_goose_mad {
	show dialog { SELF "Honk!" }
	if (flag storyflag_rake is false) {
		copy set_hint_goose;
	}
	// RESET
	set entity "%SELF%" on_interact to show_dialog_goose_mad;
}

/* --------------- debug --------------- */

debug_win_rake_q {
	copy face_player;
	show dialog {
		SELF "Position rake in lake?"
		> "Yes" : debug_win_rake_yes
		> "No" : debug_win_rake_no
	}
}
debug_win_rake_yes {
	teleport entity Rake to geometry rake-lake-point;
	set entity "%SELF%" on_interact to debug_win_rake_q;
}
debug_win_rake_no {
	set entity "%SELF%" on_interact to debug_win_rake_q;
}
