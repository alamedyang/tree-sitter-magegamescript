include "header.mgs";

"on_load-woprhouse" {
	if ("tuesdayd") {
		command "rtfm" = "command-rtfm";
	}
	if ("current-chapter" == 2) {
		// entrance text
		show serial_dialog spacer;
		show serial_dialog { "Entering <bold>WOPR ROOM</>..." };
		copy!("ch2-map-init")
	}
	if (!"wopr-backdoor-found") {
		player_control = off;
		"wopr-backdoor-found" = true;
		if (warp_state != "warped") {
			player position -> geometry "walk_from-north" length over 600ms;
		}
		wait 400ms;
		show dialog {
			PLAYER "Whoa! It looks like I found some kind of back door."
		};
		player_control = on;
	}
}

/* ---------- ON_TICK ---------- */

"on_tick-woprhouse" {
	if (player intersects geometry "north-hitbox") {
		goto "on_walk-woprhouse-north";
	}
}

/* ---------- EXIT STUFF ---------- */

"on_walk-woprhouse-north" {
	// triggered by map's on_tick
	if ("current-chapter" == 1) {
		warp_state = "exit_from-woprhouse";
		load map "main";
	} else if ("current-chapter" == 2) {
		warp_state = "walk_from-woprhouse";
		load map "ch2-town";
	} else {
		show serial_dialog spacer;
		show serial_dialog {
			"<r>ERROR:</> You're in chapter '$current-chapter$'??"
			"Back to the main menu for you!"
		};
		load map "main_menu";
	}
}

"on_go-woprhouse-north" {
	copy!("warping-out-fade-out")
	load map "ch2-town";
}

/* ---------- ENTITIES ---------- */

"show_dialog-wopr-start" {
	show dialog {
		SELF portrait wopr
		"SHALL WE PLAY A GAME?"

		SELF portrait wopr
		"PLAY?"
		> "DO NOT PLAY" = "restart-wopr"
	};
}
"restart-wopr" {
	self on_interact = "show_dialog-wopr-start";
}

"show_dialog-woprbooks" {
	show dialog {
		PLAYER "These shelves are full of Vogon poetry! What an odd sort of thing for a computer to collect."
	};
}
