include "header.mgs";

/*-----------------*/
/*     ON_LOAD     */
/*-----------------*/

// zigzag time!
"on_load-lodge" {
	if ("tuesdayd") {
		command "rtfm" = "command-rtfm";
	}
	if (!"tuesday-walkedup") {
		entity "Aunt Zippy" animation_frame = 3;
		// JUMP
		goto "cutscene-tuesday";
	}
	// otherwise, set up the room
	if ("hide-lodge-crowd-mini") {
		entity "Bookcase" current_animation = 3; // down animation
		copy!("empty-lodge-mini")
	}
	if ("hide-lodge-crowd") {
		// because there's a tiny window where you might reload map "after elders leave but before crowd does"
		entity "Bookcase" current_animation = 3; // down animation
		copy!("empty-lodge")
	}
	if ("lodge_cat-unglitched") {
		// this is not set to check 'tuesdayd' because you might want the crowd inside and otherwise leave the tuesday cutscene inert
		entity "Cat" glitched = false;
	}
	if ("timmy-unglitched") {
		entity "Timmy" glitched = false;
	}
	if (warp_state == "exit_from-secretroom") {
		player_control = off;
		player position -> geometry "enter_from-secretroom" length over 200ms;
		player_control = on;
	}
	if ("demobonus-timmy-yell" || !"demobonus") {
		goto null_script;
	}
	player_control = off;
	wait 200ms;
	player_control = on;
	show dialog {
		Timmy "Hey! Hey! My book!"
	};
	"demobonus-timmy-yell" = true;
	goto null_script;
}

"empty-lodge" {
	copy!("empty-lodge-mini")
	// just in case I didn't catch all the 'empty-lodge' scripts and need the rest to happen, too
	entity "Cleo" position = geometry "lodge-hidingplace";
	entity "Shepherd" position = geometry "lodge-hidingplace";
	entity "Beatrice" position = geometry "lodge-hidingplace";
	entity "Baker" position = geometry "lodge-hidingplace";
	entity "Trekkie" position = geometry "lodge-hidingplace";
	entity "Marta" position = geometry "lodge-hidingplace";
	entity "Max Swagger" position = geometry "lodge-hidingplace";
	entity "Smith" position = geometry "lodge-hidingplace";
	entity "Verthandi" position = geometry "lodge-hidingplace";
	entity "Uncle Zappy" position = geometry "lodge-hidingplace";
	entity "Aunt Zippy" position = geometry "lodge-hidingplace";
	entity "Goose  " position = geometry "lodge-hidingplace";
	entity "Goose " position = geometry "lodge-hidingplace";
}
"empty-lodge-mini" {
	entity "Bookcase" position = geometry "lodge-hidingplace";
	copy!("skip-artifacts")
	copy!("deaden-artifacts")
	entity "Jackob" position = geometry "lodge-hidingplace";
	entity "Alfonso" position = geometry "lodge-hidingplace";
	entity "Bert" position = geometry "lodge-hidingplace";
}

/*-----------------*/
/*     ON_TICK     */
/*-----------------*/

// doors etc
// no zigzag! (these scripts need names!)

"on_tick-lodge" {
	// if TUESDAY finished, jump to the long-term script
	if ("tuesdayd") {
		goto "on_tick-tuesdayd";
	}
	// otherwise, watch the main door and "bounce"
	if (player intersects geometry "door-lodge") {
		goto "show_dialog-lodge-cantleaveyet";
	}
}
"on_tick-tuesdayd" {
	if (player intersects geometry "door-lodge") {
		goto "leave-lodge";
	}
}
"leave-lodge" {
	warp_state = "exit_from-lodge";
	load map "main";
}
"show_dialog-lodge-cantleaveyet" {
	show dialog {
		PLAYER "Wait, I can't leave yet!"
	};
	player_control = off;
	player position -> geometry "enter-lodge" length over 200ms;
	player_control = on;
	goto "on_tick-lodge";
}

// the goose watches the door to the RTFM room (to uncomplicate the map's on_tick things)
// (it's okay if the goose's on_tick script is hacked, because there's still a functioning door)
// (also the ring doesn't work until you go into the RTFM room for the first time, so no soft locking out of the tutorial)
"on_tick-lodge-goose" {
	if ("ring-acquired") {
		goto "on_tick-lodge-goose-ring-acquired";
	}
}
"on_tick-lodge-goose-ring-acquired" {
	if (player intersects geometry "door-secretdoor") {
		goto "enter-secretroom";
	}
}
"enter-secretroom" {
	load map "secretroom";
}
"enter-secretroom-with-guaranteed-cutscene" { // for debug purposes
	tuesdayd = false;
	copy!("enter-secretroom")
}

// glitch watchers (entities' on_tick)

"lodge_cat-glitchwatch" {
	if (!self glitched) {
		goto "lodge_cat_now_unglitched";
	}
}
"lodge_cat_now_unglitched" {
	"lodge_cat-unglitched" = true;
	goto null_script;
}

"timmy-glitchwatch" {
	if (!self glitched) {
		goto "timmy_now_unglitched";
	}
}
"timmy_now_unglitched" {
	"timmy-unglitched" = true;
	goto null_script;
}

/*------------------------------*/
/*     TUESDAY CHOREOGRAPHY     */
/*------------------------------*/

"lodge-watch-player-gradually" {
	entity "Beatrice" on_tick = "face-player";
	entity "Cleo" on_tick = "face-player";
	entity "Trekkie" on_tick = "face-player";
	wait 200ms;
	entity "Max Swagger" on_tick = "face-player";
	entity "Verthandi" on_tick = "face-player";
	entity "Marta" on_tick = "face-player";
	wait 200ms;
	entity "Baker" on_tick = "face-player";
	entity "Smith" on_tick = "face-player";
	entity "Uncle Zappy" on_tick = "face-player";
	wait 200ms;
	entity "Aunt Zippy" on_tick = "face-player";
	entity "Shepherd" on_tick = "face-player";
}
"alfonso-left-right" {
	wait 200ms; /**/ entity "Alfonso" direction = west;
	wait 500ms; /**/ entity "Alfonso" direction = east;
	wait 500ms; /**/ entity "Alfonso" direction = south;
	wait 200ms;
}
"watch-marta" {
	self direction = entity "Marta";
}
"spin-around-fast" {
	wait 200ms; /**/ self direction = west;
	wait 200ms; /**/ self direction = south;
	wait 200ms; /**/ self direction = east;
	wait 200ms; /**/ self direction = north;
}
"lodge-murmur" {
	entity "Cleo" on_tick = "gossip-down-player";
	entity "Baker" on_tick = "gossip-up-player";
	entity "Beatrice" on_tick = null_script;
	entity "Beatrice" direction = player;
	entity "Shepherd" on_tick = "gossip-left-player";
	entity "Trekkie" on_tick = "gossip-right-player";
	entity "Marta" on_tick = "gossip-left-player";
	entity "Max Swagger" on_tick = "gossip-right-player";
	entity "Smith" on_tick = "gossip-left-player";
	entity "Verthandi" on_tick = "gossip-up-player";
	entity "Aunt Zippy" on_tick = "gossip-down-player";
	entity "Uncle Zappy" on_tick = null_script;
	entity "Uncle Zappy" direction = player;
}
"lodge-un-murmur" {
	entity "Cleo" on_tick = null_script;
	entity "Cleo" direction = player;
	entity "Baker" on_tick = null_script;
	entity "Baker" direction = player;
	entity "Beatrice" on_tick = null_script;
	entity "Beatrice" direction = player;
	entity "Shepherd" on_tick = null_script;
	entity "Shepherd" direction = player;
	entity "Trekkie" on_tick = null_script;
	entity "Trekkie" direction = player;
	entity "Marta" on_tick = null_script;
	entity "Marta" direction = player;
	entity "Max Swagger" on_tick = null_script;
	entity "Max Swagger" direction = player;
	entity "Smith" on_tick = null_script;
	entity "Smith" direction = player;
	entity "Verthandi" on_tick = null_script;
	entity "Verthandi" direction = player;
	entity "Aunt Zippy" on_tick = null_script;
	entity "Aunt Zippy" direction = player;
	entity "Uncle Zappy" on_tick = null_script;
	entity "Uncle Zappy" direction = player;
}
"gossip-left-player" {
	wait 200ms; /**/ self direction = west;
	wait 350ms; /**/ self direction = player;
	wait 350ms;
}
"gossip-right-player" {
	wait 300ms; /**/ self direction = east;
	wait 400ms; /**/ self direction = player;
	wait 200ms;
}
"gossip-up-player" {
	self direction = north;
	wait 300ms;
	self direction = player;
	wait 600ms;
}
"gossip-down-player" {
	wait 100ms; /**/ self direction = south;
	wait 250ms; /**/ self direction = player;
	wait 550ms;
}
"player-struggles-a-bit" {
	wait 100ms;
	player direction = west;
	player current_animation = 1;
	wait 50ms;    player current_animation = 0;
	wait 200ms;  player direction = east;
	             player current_animation = 1;
	wait 50ms;     player current_animation = 0;
	wait 200ms;
}
"elders-whisper" {
	wait 100ms; /**/ entity "Alfonso" direction = entity "Jackob";
	wait 100ms; /**/ entity "Jackob" direction = entity "Alfonso";
	wait 700ms; /**/ entity "Alfonso" direction = entity "Bert";
	wait 100ms; /**/ entity "Bert" direction = entity "Alfonso";
	wait 700ms; /**/ entity "Alfonso" direction = entity "Jackob";
	wait 500ms; /**/ entity "Bert" direction = player;
	wait 50ms; /**/ entity "Alfonso" direction = player;
	wait 100ms; /**/ entity "Jackob" direction = player;
}
"elders-to-secretdoor" {
	wait 100ms; entity "Alfonso" on_tick = "secretdoor-walk";
	wait 200ms; entity "Bert" on_tick = "secretdoor-knightsmove-walk";
	wait 400ms; entity "Jackob" on_tick = "secretdoor-knightsmove-walk";
}
"secretdoor-knightsmove-walk" {
	self position -> geometry "elder-point" origin over 500ms;
	copy!("secretdoor-walk") // don't crush this COPY_SCRIPT
}
"secretdoor-walk" {
	self position -> geometry "secretdoor-point" origin over 800ms;
	self position = geometry "lodge-hidingplace";
	goto null_script; // simultaneous behavior with on_tick; must cancel with null_script
}
"deaden-artifacts" {
	entity "Broom" on_interact = null_script;
	entity "Glasses" on_interact = null_script;
	entity "Wand" on_interact = null_script;
	entity "Book" on_interact = null_script;
	entity "Bracelet" on_interact = null_script;
}

/*------------------------*/
/*     TUESDAY PART 1     */
/*------------------------*/

"cutscene-tuesday" {
	// Player walks up to the table
	player_control = off;
	hex_control = off;
	player position -> geometry "lodge-path1" origin over 600ms;
	wait 600ms;
	show dialog {
		Alfonso "Come on in, %PLAYER%!"
	};
	wait 800ms;
	copy!("lodge-watch-player-gradually")
	wait 300ms;
	player position -> geometry "lodge-path1" length over 1900ms;
	wait 100ms;
	player direction = entity "Uncle Zappy";
	wait 100ms;
	show dialog {
		entity "Uncle Zappy" "We'll be right here."
	};
	wait 200ms;
	player position -> geometry "lodge-path2" origin over 300ms;
	wait 100ms;
	player direction = entity "Aunt Zippy";
	wait 100ms;
	show dialog {
		entity "Aunt Zippy" "Go ahead. You'll do great!"
	};
	wait 200ms;
	player position -> geometry "lodge-path2" length over 800ms;
	wait 200ms;
	player direction = entity "Alfonso";

	// Player is in the center of the hall:
	camera -> geometry "lodge-camera-point" origin over 1500ms;
	show dialog {
		Jackob alignment TL
		"The day has come!"
		"You have come of age, and what a joyous day it is."
		"You may have noticed some aches and pains, and hair growing in new places. It's also possible --"
	};
	entity "Alfonso" direction = entity "Jackob";
	show dialog {
		Alfonso alignment TL "Uh, that's the wrong speech! It's TUESDAY, not puberty education!"
	};
	entity "Bert" direction = entity "Alfonso";
	show dialog {
		Bert alignment TL "It Friday."
	};
	entity "Alfonso" direction = entity "Bert";
	show dialog {
		Alfonso alignment TL "No, not Tuesday, T.U.E.S.D.A.Y.! You know, Traditional and Unambiguous Event of Selection Directly Affecting You."
	};
	entity "Jackob" direction = entity "Alfonso";
	show dialog {
		Jackob alignment TL "Ah, yes. Right you are."
	};
	entity "Alfonso" direction = player;
	wait 100ms;
	entity "Bert" direction = player;
	wait 50ms;
	entity "Jackob" direction = player;
	wait 200ms;
	show dialog {
		Alfonso alignment TL "*Ahem*"
		"Each mage, upon the day of their 16th birthday, will then be present for their TUESDAY."
		"Upon the chosen TUESDAY, each mage is recognized by forces beyond our comprehension as ready to wield power bestowed upon them by a magical artifact."
		"Mages do not choose their own artifacts, but rather are subject to the moods and temperaments of the artifacts."
		"(And boy they are moody.)"
	};
	entity "Alfonso" position -> geometry "elder-point" origin over 400ms;
	copy!("alfonso-left-right")
	show dialog {
		Alfonso alignment TL "The mage must present themselves to the artifacts until one shows interest. Mage, step forth."
	};
	camera -> player position over 1200ms;
	player direction = south;
	show dialog {
		PLAYER "Okay. I can do this."
	};
	hex_control = off;
	player_control = on;
	"tuesday-walkedup" = true;
} // the player is now free to touch the artifacts

/*-----------------------------------*/
/*     HALF-TIME (ARTIFACTS ETC)     */
/*-----------------------------------*/

// Villagers' dialog:

"show_dialog-zippy-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "We always knew you were destined for great things!" };
	} else {
		show dialog { SELF "Go ahead, dear!" };
	}
}
"show_dialog-zappy-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "We're both so proud of you!" };
	} else {
		show dialog { SELF "Take your time!" };
	}
}
"show_dialog-verthandi-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "Oh, how exciting!" };
	} else {
		show dialog { SELF "Oh, how exciting!" };
	}
	// yeah, she says the same in both cases
	// accident? or just unimaginative Belldandy?
}
"show_dialog-smith-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "Sounds like that ring is a big deal." "Neat." };
	} else {
		show dialog { SELF "Don't look at me! I don't know how any of this stuff works!" };
	}
}
"show_dialog-max-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "Hmm. That ring looks a bit too plain to be the real Ring Zero, doesn't it?" };
	} else {
		show dialog { SELF "Hmm. I've seen better artifacts." };
	}
}
"show_dialog-marta-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "Wow. Never seen a TUESDAY like this before!" };
	} else if ("artifact-broom-touched") {
		show dialog { SELF alignment BL "I knew I recognized this broom!" };
	} else {
		show dialog { SELF "One of those artifacts seems a little familiar to me...." };
	}
}
"show_dialog-trekkie-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "Amazing!" };
	} else {
		show dialog { SELF "Oh, me hope it's a good one!" };
	}
}
"show_dialog-shepherd-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "Neat!" };
	} else {
		show dialog { SELF "Is that old junk really magic?" };
	}
}
"show_dialog-cleo-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "It looks so shiny!" };
	} else {
		show dialog { SELF "Ooh! I bet it's gonna be the one on the left! No, the right! No, the second one!" };
	}
}
"show_dialog-beatrice-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "Oh! I never thought I'd see this day! Oh, my Delmar...!" };
	} else {
		show dialog { SELF "It's been so long since we've had a new mage!" };
	}
}
"show_dialog-baker-prechoose" {
	copy!("face-player")
	if ("ring-acquired") {
		show dialog { SELF "Congratulations, %PLAYER%!" };
	} else {
		show dialog { SELF "Um, I gotta get back to work, kid. You wanna hurry it up a little?"};
	}
}
"show_dialog-jackob-prechoose" {
	copy!("face-player")
	entity "Jackob" on_tick = "look-left-and-right-fast";
	show dialog { SELF "(Wait, was this everything?)" };
	entity "Jackob" on_tick = null_script;
	self direction = south;
}
"show_dialog-alfonso-prechoose" {
	copy!("face-player")
	show dialog { SELF "*ahem*" "The mage must present themselves to the artifacts until one shows interest." };
	self direction = south;
}
"show_dialog-bert-prechoose" {
	copy!("face-player")
	show dialog { SELF "Touch each item, please." };
	self direction = south;
}

// The artifacts:

"examine-bracelet" {
	show dialog {
		PLAYER "A Power Bracelet?"
		Bert "That is a croissant!"
		"Is my lunch!"
	};
	player_control = off;
	entity "Bert" position -> geometry "bert-lunch-take-spot" origin over 400ms;
	entity "Bert" direction = south;
	wait 400ms;
	entity "Bracelet" position = geometry "lodge-hidingplace";
	wait 300ms;
	entity "Bert" position -> geometry "bert-lunch-watch-spot" origin over 400ms;
	entity "Bert" direction = south;
	player_control = on;
	"artifact-bracelet-touched" = true;
	goto "artifacts-eval";
}
"examine-glasses" {
	show dialog {
		PLAYER "This?"
		"This is clearly a temporary asset that hasn't been replaced with anything yet."
		"Though...."
		"(touch)"
		"Ahh, oh well."
	};
	"artifact-glasses-touched" = true;
	goto "artifacts-eval";
}
"examine-book" {
	show dialog {
		PLAYER "Ugh. Is that an eye on the cover?"
		"And I don't wanna know what kind of leather that is."
		"...."
		"It's not reacting to me, though."
	};
	"artifact-book-touched" = true;
	goto "artifacts-eval";
}
"examine-broom" {
	player_control = off;
	entity "Marta" on_tick = null_script;
	entity "Marta" position -> geometry "marta-broom-step-spot" origin over 500ms;
	player on_tick = "watch-marta";
	show dialog {
		Marta alignment TL "Hey, that broom is mine! How did that end up there?"
	};
	entity "Marta" position -> geometry "marta-broom-take-spot" origin over 600ms;
	wait 400ms;
	entity "Broom" position = geometry "lodge-hidingplace";
	wait 300ms;
	entity "Marta" type = "marta_broom";
	wait 300ms;
	entity "Marta" position -> geometry "marta-broom-watch-spot" origin over 700ms;
	entity "Marta" on_tick = "face-player";
	wait 300ms;
	"artifact-broom-touched" = true;
	player on_tick = null_script;
	player_control = on;
	goto "artifacts-eval";
}
"examine-wand" {
	player on_tick = "spin-around-fast";
	show dialog {
		PLAYER "Moon Prism Power...."
	};
	player_control = off;
	wait 900ms;
	player on_tick = null_script;
	player direction = south;
	show dialog {
		PLAYER "Make Up!"
	};
	wait 400ms;
	player_control = on;
	player direction = entity "Wand";
	show dialog {
		PLAYER "Erm, maybe I had that a bit wrong."
		"Still, no response."
	};
	"artifact-wand-touched" = true;
	goto "artifacts-eval";
}

"artifacts-eval" {
	if ( // if any of the artifacts are still untouched
		!"artifact-bracelet-touched"
		|| !"artifact-broom-touched"
		|| !"artifact-glasses-touched"
		|| !"artifact-wand-touched"
		|| !"artifact-book-touched"
	) {
		show dialog { PLAYER "Maybe a different artifact?" };
		// "bad" eval artifacts will get stuck here; that's fine
		// (no reset)
	} else {
		// all artifacts have been touched:
		show dialog {
			PLAYER "Maybe a different artifact?"
			"Wait. Was that the last one?"
		};
		player on_tick = "look-left-and-right-fast";
		player_control = off;
		wait 900ms;
		player on_tick = null_script;
		copy!("lodge-murmur")
		goto "player-ringzero-landing";
	}
	self on_interact = "artifacts-eval";
}

// GOOD GOD
// THE "PATHING"

"player-ringzero-landing" {
	wait 200ms;
	if (player intersects geometry "ringzero-walk-6-donut") {
		player position -> geometry "ringzero-landing-6" origin over 200ms;
		goto "cutscene-artifacts-eval-good";
	} else if (player intersects geometry "ringzero-walk-6") {
		player position -> geometry "ringzero-landing-6" origin over 300ms;
		goto "cutscene-artifacts-eval-good";
	} else if (player intersects geometry "ringzero-walk-1159") {
		player position -> geometry "ringzero-landing-10" origin over 600ms;
		copy!("player-ringzero-from-landing-10")
	} else if (player intersects geometry "ringzero-walk-1130") {
		player position -> geometry "ringzero-landing-10" origin over 300ms;
		copy!("player-ringzero-from-landing-10")
	} else if (player intersects geometry "ringzero-walk-11") {
		player position -> geometry "ringzero-landing-10" origin over 100ms;
		copy!("player-ringzero-from-landing-10")
	} else if (player intersects geometry "ringzero-walk-10") {
		player position -> geometry "ringzero-landing-8" origin over 400ms;
		copy!("player-ringzero-from-landing-8")
	} else if (player intersects geometry "ringzero-walk-9") {
		player position -> geometry "ringzero-landing-8" origin over 200ms;
		copy!("player-ringzero-from-landing-8")
	} else if (player intersects geometry "ringzero-walk-8") {
		player position -> geometry "ringzero-landing-6" origin over 500ms;
		goto "cutscene-artifacts-eval-good";
	} else if (player intersects geometry "ringzero-walk-7") {
		player position -> geometry "ringzero-landing-6" origin over 250ms;
		goto "cutscene-artifacts-eval-good";
	} else if (player intersects geometry "ringzero-walk-5") {
		player position -> geometry "ringzero-landing-6" origin over 250ms;
		goto "cutscene-artifacts-eval-good";
	} else if (player intersects geometry "ringzero-walk-4") {
		player position -> geometry "ringzero-landing-6" origin over 500ms;
		goto "cutscene-artifacts-eval-good";
	} else if (player intersects geometry "ringzero-walk-3") {
		player position -> geometry "ringzero-landing-4" origin over 200ms;
		copy!("player-ringzero-from-landing-4")
	} else if (player intersects geometry "ringzero-walk-2") {
		player position -> geometry "ringzero-landing-4" origin over 400ms;
		copy!("player-ringzero-from-landing-4")
	} else if (player intersects geometry "ringzero-walk-1") {
		player position -> geometry "ringzero-landing-1" origin over 100ms;
		copy!("player-ringzero-from-landing-1")
	} else if (player intersects geometry "ringzero-walk-1230") {
		player position -> geometry "ringzero-landing-1" origin over 300ms;
		copy!("player-ringzero-from-landing-1")
	} else if (player intersects geometry "ringzero-walk-1201") {
		player position -> geometry "ringzero-landing-1" origin over 600ms;
		copy!("player-ringzero-from-landing-1")
	}
	goto "cutscene-artifacts-eval-good";
}

"player-ringzero-from-landing-10" {
	player position -> geometry "ringzero-landing-8" origin over 600ms;
	copy!("player-ringzero-from-landing-8")
}
"player-ringzero-from-landing-8" {
	player position -> geometry "ringzero-landing-6" origin over 800ms;
	goto "cutscene-artifacts-eval-good";
}
"player-ringzero-from-landing-4" {
	player position -> geometry "ringzero-landing-6" origin over 800ms;
	goto "cutscene-artifacts-eval-good";
}
"player-ringzero-from-landing-1" {
	player position -> geometry "ringzero-landing-4" origin over 400ms;
	copy!("player-ringzero-from-landing-4")
}

/*------------------------*/
/*     TUESDAY PART 2     */
/*------------------------*/

"cutscene-artifacts-eval-good" { // TUESDAY PART 2
	player on_tick = "look-left-and-right";
	wait 300ms;
	show dialog {
		Marta alignment TR
		"Did they do it wrong?"
	};
	wait 400ms;
	show dialog {
		entity "Max Swagger" alignment TL
		"I'm pretty sure %PLAYER% touched all of them...."
	};
	wait 200ms;
	show dialog {
		Trekkie alignment TR
		"Maybe they missed one."
	};
	wait 200ms;
	player on_tick = null_script;
	player direction = north;
	show dialog {
		Alfonso
		"Well, this is certainly... unexpected. We've never had a dud quite like this one before."
	};
	copy!("alfonso-left-right")
	show dialog {
		Alfonso
		"Why don't we --"
	};
	wait 300ms;
	copy!("lodge-un-murmur")
	camera fade out -> #FFDD00 over 1000ms;
	player direction = south;
	entity "Ring Zero[0]" position = geometry "ringzero-landing[0]";
	entity "Ring Zero[1]" position = geometry "ringzero-landing[1]";
	camera fade in -> #FFDD00 over 1000ms;
	wait 500ms;
	camera -> geometry "ringzero-camera" origin over 1100ms;
	wait 400ms;
	show dialog {
		Alfonso "Oh! Oh, good heavens! Is that --"
		Bert "It is."
		Jackob "It's Ring Zero!"
		PLAYER "What?! What is that?"
	};
	copy!("player-struggles-a-bit")
	wait 200ms;
	camera -> player position over 800ms;
	wait 50ms;
	camera fade out -> #FFDD00 over 1000ms;
	player direction = south;
	entity "Ring Zero[0]" position = geometry "ring_zero_when_off_screen[0]";
	entity "Ring Zero[1]" position = geometry "ring_zero_when_off_screen[1]";
	camera fade in -> #FFDD00 over 1000ms;
	show dialog {
		PLAYER "It just slipped itself onto my finger!"
	};
	copy!("player-struggles-a-bit")
	wait 500ms;
	copy!("player-struggles-a-bit")
	wait 300ms;
	player direction = south;
	show dialog {
		PLAYER "Um, it's not coming off. Is that normal?"
	};
	player direction = entity "Alfonso";
	show dialog {
		Alfonso "Normal? Normal?!"
		"Nothing about that thing is normal!"
		"This is --\nThis is unprecedented."
		PLAYER "Is it bad?! What's wrong with it?"
		"Am I... am I gonna die?!"
		Jackob "The ring isn't bad, it's just... powerful."
		Bert "No one has been chosen by this ring before."
		Alfonso "Oh, but... but perhaps...."
	};
	entity "Alfonso" on_tick = "look-left-and-right-fast";
	show dialog {
		Alfonso "Oh, but this is good news, isn't it?"
		"This means we can finally fix things!"
		Bert "Fix the town."
		Jackob "Yes...."
		"Yes!"
	};
	entity "Alfonso" on_tick = null_script;
	entity "Alfonso" direction = south;
	show dialog {
		Jackob "Yes, you can fix things at last!"
		PLAYER "Fix the town?"
	};
	player on_tick = loiter2;
	show dialog {
		PLAYER "But the town is all glitchy and hacked! How am I supposed to fix it with this thing? Unless...."
	};
	player on_tick = null_script;
	player direction = entity "Alfonso";
	show dialog {
		Alfonso "Yes, exactly! That ring, Ring Zero --"
		"That ring gives you the power to hack the planet!"
		PLAYER "Hack the planet? You mean... all the stuff that's broken?"
		Bert "You can fix what the Big Bad broke!"
		Jackob "Everything that the Big Bad hacked so many years ago.... Everything that scoundrel ruined...."
		PLAYER "This ring gives me the Big Bad's power?"
		"But I don't know how to hack! How am I supposed to fix anything?"
		Alfonso "Yes, well, you see...."
	};
	copy!("elders-whisper")
	show dialog {
		Alfonso "Please come with us, %PLAYER%."
	};
	camera -> geometry "lodge-camera-point" origin over 1200ms;
	wait 200ms;
	// Alfonso walks to secret door
	wait 100ms;
	entity "Alfonso" position -> geometry "secretdoor-button-path1" origin over 600ms;
	entity "Alfonso" position -> geometry "secretdoor-button-path2" origin over 500ms;
	wait 100ms;
	entity "Alfonso" direction = north;
	wait 500ms;
	// The bookcase shakes
	entity "Bookcase" current_animation = 1; // shaking anim
	wait 900ms; /**/ entity "Bookcase" current_animation = 0; // tall anim
	wait 400ms; /**/ entity "Bookcase" animation -> 2 once;
	"ring-acquired" = true;
	entity "Bookcase" position = geometry "lodge-hidingplace";
	// The door is now "down"
	// Alfonso walks back
	wait 500ms;
	entity "Alfonso" position -> geometry "secretdoor-button-path1" origin over 500ms;
	entity "Alfonso" position -> geometry "elder-point" origin over 600ms;
	camera -> player position over 900ms;
	show dialog {
		Alfonso "Into the inner sanctum!"
	};
	// Elders on_tick to walk into the RTFM room
	copy!("elders-to-secretdoor")
	copy!("deaden-artifacts")
	"hide-lodge-crowd-mini" = true;
	player_control = on;
	// CUTSCENE IS OVER -- PLAYER IS FREE
}

/*-------------------*/
/*     LODGE CAT     */
/*-------------------*/

"show_dialog-lodge_cat" {
	copy!("face-player")
	if (self glitched) {
		show dialog { SELF "////////" };
		if (!"lodge_cat-unglitched") {
			copy!("set-hint-glitch")
		}
	} else if (
		"storyflag-catwork"
		|| !"white-cat"
	) {
		show dialog { SELF "...." };
	} else { // if storyflag-catwork is TRUE && flag white-cat is TRUE
		player_control = off;
		// thumbs up:
		entity "Cat" animation -> 3 once;
		entity "Cat" current_animation = 4;
		// change player:
		wait 700ms;
		player type = "pipscat";
		wait 400ms;
		// thumbs back down:
		entity "Cat" animation -> 5 once;
		player_control = on;
	}
}

/*------------------------------*/
/*     TIMMY AND SPORTSBOOK     */
/*------------------------------*/

"show_dialog-timmy-start" {
	copy!("face-player")
	if (self glitched) {
		show dialog { SELF alignment TL "Ner ubg qbtf n fnaqjvpu?" };
		self direction = west;
		if (!"timmy-unglitched") {
			copy!("set-hint-glitch")
		}
	} else if (!demobonus) { // if you haven't won ch1 yet
		if ("timmy-endstory") {
			show dialog {
				SELF alignment TL "I've been trying to find books about the olympics."
				"Gosh, it'd be cool if I could get a gold medal for the high jump!"
				"I can jump, like, SO high!"
			};
		} else {
			show dialog {
				SELF alignment TL "Oh, how lovely! It's much easier to read like this!"
			};
			"timmy-endstory" = true;
		}
	} else if (!"demobonus-sportsbook-read") { // you HAVE won, but you've NOT read the extra page
		show dialog {
			SELF "There's this weird extra page in my book!"
		};
	} else { // you've won AND read the extra page
		show dialog {
			SELF "Oh! I think I've heard of Clippy Bird! My uncle let me play it on his phone this one time, ages ago."
			"He said it was a really rare game."
		};
	}
	self direction = west;
}
"show_dialog-sportsbook" {
	show dialog "dialog-sportsbook" {
		SELF alignment TL portrait journal
		"Sports sports sports. This book is about sports!"
		"Go, home team!"
	};
	if ("demobonus") {
		show dialog {
			name "Hex Edits 2" alignment TL portrait parchment
			"-- allows us to copy and paste bytes using something called a \"Clip Bird.\""
			"Pressing the circle button will copy a single byte into the Clip Bird, but if you hold circle, left and right will change the number of bytes to be copied."
			"Pressing square will paste the contents of the Clip Bird into the selected cell and thereafter."
			"This has dramatically improved our productivity!"
		};
		"demobonus-sportsbook-read" = true;
		hex_clipboard = on;
	}
}

/*---------------*/
/*     DEBUG     */
/*---------------*/

"debug-cat-prep" { // used as an on_interact for an exa?
	entity "Cat" glitched = false;
	copy!("set-hint-max-from-cleo")
}
"bypass-tuesday" {
	copy!("set-goodmorning-flags-true")
	copy!("set-artifact-flags-true")
	copy!("set-tuesday-flags-true")
	hex_control = on;
	copy!("deaden-artifacts")
	copy!("empty-lodge")
	entity "Marta" position = geometry "lodge-hidingplace";
	// possibly you can't teleport an entity twice in one tick... though why then does this one work?
}
"debug-disable-tuesday-q" {
	show dialog {
		SELF "Disable TUESDAY?"
		> "Completely" = "debug-disable-tuesday-complete"
		> "Partially" = "debug-disable-tuesday-walkup"
		> "Un-disable" = "debug-undisable-tuesday"
		> "Never mind" = "debug-disable-tuesday-no"
	};
}
"debug-disable-tuesday-no" {
	show dialog { SELF "Nothing changed." };
	self on_interact = "debug-disable-tuesday-q";
}
"debug-disable-tuesday-walkup" {
	copy!("set-goodmorning-flags-true")
	// so you don't accidentally trigger walk-to-lodge
	// if you set all flags to false before engaging the lodge
	copy!("set-tuesday-flags-true")
	"hide-lodge-crowd" = false;
	// was set true above;
	// set to false now so the lodge crowd isn't hidden upon load,
	// so you can voluntarily start the cutscene and it'll look okay
	hex_control = on;
	show dialog { SELF "TUESDAY partially disabled." };
	self on_interact = "debug-disable-tuesday-q";
}
"debug-disable-tuesday-complete" {
	copy!("set-goodmorning-flags-true")
	copy!("set-artifact-flags-true")
	copy!("set-tuesday-flags-true")
	hex_control = on;
	show dialog { SELF "TUESDAY completely disabled." };
	self on_interact = "debug-disable-tuesday-q";
}
"debug-undisable-tuesday" {
	copy!("set-goodmorning-flags-true")
	// so you don't accidentally trigger walk-to-lodge
	// if you set all flags to false before engaging the lodge
	copy!("set-artifact-flags-false")
	copy!("set-tuesday-flags-false")
	hex_control = off;
	show dialog {
		SELF "TUESDAY re-enabled."
	};
	self on_interact = "debug-disable-tuesday-q";
}
"debug-bypass-tuesday-q" {
	copy!("face-player")
	show dialog {
		SELF "Bypass TUESDAY?"
		> "Yes" = "debug-bypass-tuesday"
		> "Yes (but enable hintman cutscene)" = "debug-bypass-tuesday-but-enable-hintman"
		> "No" = "debug-bypass-tuesday-no"
	};
}
"debug-bypass-tuesday" {
	copy!("bypass-tuesday")
	copy!("set-tuesday-flags-true")
	show dialog { SELF "Tuesday bypassed." };
	self on_interact = "debug-bypass-tuesday-q";
}
"debug-bypass-tuesday-but-enable-hintman" {
	copy!("bypass-tuesday")
	copy!("set-tuesday-flags-true")
	"hintman-explanation" = false;
	// set to true in above copied script
	show dialog { SELF "Tuesday bypassed. Prepare for hintman cutscene." };
	self on_interact = "debug-bypass-tuesday-q";
}
"debug-bypass-tuesday-no" {
	self on_interact = "debug-bypass-tuesday-q";
}
"debug-cutscene-tuesday-q" {
	copy!("face-player")
	show dialog {
		SELF "Begin TUESDAY cutscene?"
		> "Yes" = "debug-cutscene-tuesday"
		> "No" = "debug-cutscene-tuesday-no"
	};
}
"debug-cutscene-tuesday" {
	copy!("cutscene-tuesday")
	self on_interact = "debug-bypass-tuesday-q";
}
"debug-cutscene-tuesday-no" {
	self on_interact = "debug-bypass-tuesday-q";
}
"debug-touch-artifacts-q" {
	copy!("face-player")
	show dialog {
		SELF "Do what to artifacts?"
		> "Touch all artifacts" = "debug-touch-artifacts"
		> "Finish lecture and lower bookcase" = "debug-touch-artifacts-and-aftermath"
		> "Teleport to secret room (with cutscene)" = "enter-secretroom-with-guaranteed-cutscene"
		> "No" = "debug-touch-artifacts-no"
	};
}
"skip-artifacts" {
	copy!("set-artifact-flags-true")
	entity "Alfonso" position = geometry "elder-point";
	entity "Marta" position = geometry "marta-broom-watch-spot";
	entity "Broom" position = geometry "lodge-hidingplace";
	entity "Bracelet" position = geometry "lodge-hidingplace";
	entity "Marta" type = "marta_broom";
}
"debug-touch-artifacts-end" {
	self on_interact = "debug-touch-artifacts-q";
}
"debug-touch-artifacts-no" {
	self on_interact = "debug-touch-artifacts-q";
}
"debug-touch-artifacts-and-aftermath" {
	copy!("skip-artifacts")
	entity "Bookcase" position = geometry "lodge-hidingplace";
	"ring-acquired" = true;
	"hide-lodge-crowd-mini" = true;
	entity "Alfonso" position = geometry "lodge-hidingplace";
	entity "Bert" position = geometry "lodge-hidingplace";
	entity "Jackob" position = geometry "lodge-hidingplace";
	goto "debug-touch-artifacts-end";
}
"debug-touch-artifacts" {
	copy!("skip-artifacts")
	goto "debug-touch-artifacts-end";
}

dialog "dialog-artifacts-touch" { // not used (anymore)?
	SELF "All artifacts touched."
}
dialog "dialog-debug-touch-artifacts" { // not used (anymore)?
	SELF "Artifacts touched."
}
