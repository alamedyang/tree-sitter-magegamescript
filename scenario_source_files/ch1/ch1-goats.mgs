include "header.mgs";

/* ---------- on_tick ---------- */

"billy-glitchwatch" {
	if (!entity "Billy" glitched) {
		goto "billy_now_unglitched";
	}
}
"billy_now_unglitched" {
	"billy-unglitched" = true;
	goto null_script;
}
"kid-glitchwatch" {
	if (!entity "Kid" glitched) {
		goto "kid_now_unglitched";
	}
}
"kid_now_unglitched" {
	"kid-unglitched" = true;
	goto null_script;
}

/* ---------- goat handler (on_tick) ---------- */

"check_if_player_is_goat_high" {
	if (player intersects geometry "high") {
		goto "move_goats_to_low";
	}
}
"check_if_player_is_goat_low" {
	if (player intersects geometry "low") {
		goto "move_goats_to_high";
	}
}
"move_goats_to_low" {
	entity "Billy" on_tick = move_billy_to_low;
	entity "Kid" on_tick = move_kid_to_low;
	entity "Verthandi" on_tick = check_if_player_is_goat_low;
}
"move_goats_to_high" {
	entity "Billy" on_tick = move_billy_to_high;
	entity "Kid" on_tick = move_kid_to_high;
	entity "Verthandi" on_tick = check_if_player_is_goat_high;
}
"move_billy_to_low" {
	entity "Billy" position -> geometry "low1" origin over 900ms;
	entity "Billy" position -> geometry "low1" length over 3000ms forever;
}
"move_kid_to_low" {
	entity "Kid" position -> geometry "low2" origin over 1100ms;
	entity "Kid" position -> geometry "low2" length over 3400ms forever;
}
"move_billy_to_high" {
	entity "Billy" position -> geometry "high1" origin over 700ms;
	entity "Billy" position -> geometry "high1" length over 3000ms forever;
}
"move_kid_to_high" {
	entity "Kid" position -> geometry "high2" origin over 800ms;
	entity "Kid" position -> geometry "high2" length over 3400ms forever;
}

/* ---------- dialog: glitched lesson ---------- */

"show_dialog-verthandi-glitched" {
	if ("glitch-lesson") {
		goto "show_dialog-verthandi-glitched-q";
	}
	show dialog {
		SELF "Oh! It looks like you would like a lesson on glitched entities! Please allow me to explain."
	};
	goto "show_dialog-verthandi-glitched-lesson";
}
"show_dialog-verthandi-glitched-q" {
	show dialog {
		SELF "Would you like to hear the lesson again?"
		> "Yes" = "show_dialog-verthandi-glitched-lesson"
		> "No" = "show_dialog-verthandi-glitched-s"
	};
}
"show_dialog-verthandi-glitched-lesson" {
	show dialog {
		SELF "Ring Zero provides you access to our world's superficial state, but there is also a deeper state you cannot see, nor control."
		"Our physical appearance is only superficial, and our deeper soul remembers our true identities, even if our superficial apperance has changed."
		"Given enough time, any superficial changes will revert on their own --"
		"Unless you have severed the body and soul, too, like the Big Bad did when he was last here."
		"But once restored, the body and soul can reconnect again, and after that, not even Ring Zero can glitch them for long!"
		"That's why I'm not worried about being glitched! I know that my body and soul are still connected, and I will return to normal soon!"
		"I hope this explanation was enlightening!"
	};
	"glitch-lesson" = true;
	goto "show_dialog-verthandi-wrapup";
}
"show_dialog-verthandi-glitched-s" {
	show dialog { SELF "I might be glitched for now, but my soul is quite unharmed! My appearance will return to normal soon." };
	goto "show_dialog-verthandi-wrapup";
}

/* ---------- dialog: other ---------- */


"show_dialog-verthandi-start" {
	copy!("face-player")
	if (self glitched) {
		goto "show_dialog-verthandi-glitched"; // JUMP
	}
	if ("storyflag-verthandi") {
		show dialog { SELF "Thank you for everything you've done!" };
	} else {
		// find unglitch count
		"unglitch_count" = 0;
		if ("goldfish-unglitched") { "unglitch_count" += 1; }
		if ("timmy-unglitched") { "unglitch_count" += 1; }
		if ("fountain-unglitched") { "unglitch_count" += 1; }
		if ("lodge_cat-unglitched") { "unglitch_count" += 1; }
		if ("billy-unglitched") { "unglitch_count" += 1; }
		if ("kid-unglitched") { "unglitch_count" += 1; }
		if ("main_hamster-unglitched") { "unglitch_count" += 1; }
		"total_glitches" = 7;
		"total_glitches" -= "unglitch_count";
		// use unglitch count
		if ("total_glitches" == 0) {
			if (!"verthandi-backstory") {
				show dialog {
					SELF "Oh! It appears you've already restored everyone that was glitched!"
					"How wonderful! I can't thank you enough!"
				};
			} else {
				show dialog {
					SELF "You've restored everyone! Oh, how wonderful!"
					"Oh, thank you, %PLAYER%!"
				};
			}
			copy!("calculate-hint-tracking-series")
			if ("hint-tracking-check" == 11) {
				copy!("set-hint-none")
			}
			"storyflag-verthandi" = true;
			"goats-both-unglitched" = true;
		} else if ("verthandi-backstory") {
			if ("goats-both-unglitched") {
				if ("total_glitches" == 1) {
					show dialog { SELF "I hear one more glitched soul that needs help!" };
				} else {
					show dialog { SELF "I can hear the cries of $total_glitches$ glitched souls...." };
				}
				if (!"timmy-unglitched" || !"lodge_cat-unglitched") {
					show dialog { SELF "Perhaps... somewhere in the library?" };
				} else if (!"main_hamster-unglitched" || !"fountain-unglitched") {
					show dialog { SELF "Perhaps... somewhere outdoors?" };
				} else if (!"goldfish-unglitched") {
					show dialog { SELF "Perhaps... somewhere in your home?" };
				}
			} else if (entity "Billy" glitched || entity "Kid" glitched) {
				show dialog { SELF "Oh, won't you help the baby goats?" };
			} else {
				show dialog {
					SELF "You've restored the baby goats! Oh, thank you!"
					"But I still hear cries for help elsewhere in town. Please, can you help?"
				};
				"goats-both-unglitched" = true;
			}
			copy!("set-hint-glitch")
		} else {
			show dialog {
				SELF "Some time ago, the Big Bad brought chaos to this place."
				"To amuse himself, he scrambled the outward appearance of many of the people, animals, and objects around town."
			};
			if (entity "Billy" glitched || entity "Kid" glitched) {
				show dialog {
					SELF "These dear baby goats were among his many victims."
					"They are so young, and so full of life.... yet you cannot even hear their cheerful bleats!"
					"You have been blessed with the power to undo what had been done to them. Would you please help them?"
				};
			} else {
				show dialog {
					SELF "These dear baby goats are back to normal, but I still hear cries for help elsewhere in town."
					"There is more to unglitch! Please, will you set things right?"
				};
				"goats-both-unglitched" = true;
			}
			"verthandi-backstory" = true;
			copy!("set-hint-glitch")
		}
	}
	goto "show_dialog-verthandi-wrapup";
}

"show_dialog-verthandi-wrapup" {
	entity "Verthandi" direction = entity "Kid";
	// RESET
	self on_interact = "show_dialog-verthandi-start";
}

"show_dialog-goat" {
	if (self glitched) {
		show dialog { SELF "ba15456`-------++++++gf\n+++++-//==========/*8901ikg" };
	} else {
		show dialog { SELF "Baaahhh!" };
	}
	if (!"goats-both-unglitched") {
		copy!("set-hint-glitch")
	}
	self on_interact = "show_dialog-goat";
}
