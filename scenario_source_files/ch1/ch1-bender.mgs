include "header.mgs";

bender_success_backstory {
	show dialog {
		SELF "Hey, you did it! My ass is back!"
	};
	copy!(bite_my_shiny_metal_ass)
	show dialog {
		SELF emote 1 "Hee hee hee!"
		"Hey, everyone! %Bender%'s ass is back!"
		PLAYER "...."
		SELF "...."
		PLAYER "Well?"
		SELF "Well, what?"
		PLAYER "You said you'd give me a reward!"
		SELF "I did! You got a front row seat to me, %Bender%, showing off my shiny metal ass!"
		"That's the greatest reward there is!"
		PLAYER "I don't know why I assumed you'd be friendly."
		SELF "Well, you know what they say about assumptions."
		SELF emote 1 "They make a shiny ass... and they give it to me, %Bender%."
	};
}

bender_success_no_backstory {
	show dialog {
		SELF "Hey, my ass! It's back!"
	};
	copy!(bite_my_shiny_metal_ass)
	show dialog {
		SELF emote 1
		"Hee hee hee!"
		"Hey, everyone! %Bender%'s ass is back!"

		SELF
		"...."
		"Don't look so smug, kid. It had nothing to do with you."
		"I'm sure my ass came home on its own!"
	};
}

show_dialog_bender_start {
	if (self glitched) {
		show dialog {
			SELF emote 1
			"I speak binary fluently, kid! A little trick like this won't slow me down!"
		};
		return;
	}
	
	copy!(set_bender_idle_stop)
	copy!(face_player)

	if (bender_quest_done) {
		if (entity Bender type == bender) {
			copy!(bite_my_shiny_metal_ass)
			show dialog {
				SELF emote 1 "Ooh, yeah!"
				PLAYER "It's shiny. I'll give you that."
			};
		} else {
			show dialog { SELF "Yeah, yeah, you're a real comedian, kid. Now turn me back!" };
		}
		copy!(set_bender_idle_look_around_randomly)
		return;
	}

	if (entity Bender type == bender) {
		if (!bender_backstory) {
			copy!(bender_success_no_backstory)
		} else {
			copy!(bender_success_backstory)
		}
		bender_backstory = true;
		bender_quest_done = true;
		copy!(calculate_hint_tracking_series)
		if (hint_tracking_check == 5) {
			copy!(set_hint_none)
		}
		copy!(set_bender_idle_look_around_randomly)
		return;
	}

	if (bender_backstory) {
		if (entity Bender type == bender_sadbutt) {
			copy!(bite_my_shiny_metal_ass_sad)
			show dialog {
				SELF "...Oh, right. My ass. Someone bit it off."
				"So what's the holdup? I know you can fix it!"
				"I'll reward you! Just hurry it up!"
			};
		} else {
			show dialog {
				SELF "Come on! That doesn't look anything like me, half-assed or full-assed."
			};
		}
		copy!(set_hint_bender)
		copy!(set_bender_idle_glare_at_you)
		return;
	}

	if (entity Bender type == bender_sadbutt) {
		show dialog {
			PLAYER "Oh! You look friendly!"
			SELF "Well, I'm not!"
		};
		copy!(bite_my_shiny_metal_ass_sad)
		show dialog {
			SELF "Aww, man... I forgot! Someone bit my shiny metal ass!"
			"Why would someone do such a horrible thing to an innocent robot?!"
			"Life's not the same without my ass! How else am I supposed to taunt everyone stupid-looking?"
		};
		// branch briefly depending on whether the player looks like bender...
		if (player type == bender_sadbutt || player type == bender) {
			show dialog {
				SELF "Hey, you, strangely-handsome-looking kid! You're the one with the magic whatsit!"
			};
		} else {
			show dialog {
				SELF "Hey, you, kid! You're the one with the magic whatsit!"
			};
		}
		show dialog {
			SELF "Can't you fix this with your whatever powers?"
			"I'll give you a great reward! Just get my ass back!"
		};
	} else {
		show dialog {
			SELF "Hey, you! Bite my --\nAhhh!"
			"Aaaargh, this is terrible! My ass is even more missing than before!"
			"First someone bit it off, and now it's an entirely different kind of ass!"
			"I feel like this is your fault somehow!"
			"Hurry up and fix it, kid!"
		};
	}

	bender_backstory = true;
	copy!(set_hint_bender)
	copy!(set_bender_idle_glare_at_you)
}

/* ---- Bender on_tick handlers --- */

set_bender_idle_stop {
	entity "Bob Austin" on_tick = null_script;
	entity Bender on_tick = null_script;
}
set_bender_idle_glare_at_you {
	entity Bender on_tick = face_player;
	entity "Bob Austin" on_tick = handler_bender_timer;
}
set_bender_idle_look_around_randomly {
	entity "Bob Austin" on_tick = null_script;
	entity Bender on_tick = on_tick_bender_loiter;
}

handler_bender_timer {
	wait 2800ms;
	entity Bender animation -> 2 once;
	wait 3900ms;
	entity Bender animation -> 2 once;
}

/* ---- Bender choreography --- */

bite_my_shiny_metal_ass {
	player_control = off;
	wait 300ms;
	self animation -> 3 once;
	player_control = on;
}

bite_my_shiny_metal_ass_sad {
	player_control = off;
	wait 300ms;
	self animation -> 3 once;
	player_control = on;
}

on_tick_bender_loiter {
	self direction = south;
	wait 1200ms;
	self direction = east;
	wait 1400ms;
	self direction = north;
	wait 2000ms;
}
