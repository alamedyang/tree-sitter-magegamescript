include "header.mgs";

// on_load scripts

"on_load-magehouse" {
	if ("tuesdayd") {
		command "rtfm" = "command-rtfm";
	}
	"debug-mode" = true;
	// doc: this is legacy debug mode stuff -- used to hide/show exas with a save flag
	// if this flag isn't set to true, the exas will be whisked away
	if ("goldfish-unglitched") {
		entity "Mr. Tickles" glitched = false;
	}
	if (!"goodmorning") {
		player position = geometry "wakeup_spot";
		player direction = south;
		hex_control = off;
		hex_clipboard = off;
		camera fade in -> #000000 over 500ms;
	}
}

// on_tick scripts

"on_tick-magehouse" {
	if (player intersects geometry "door-magehouse") {
		goto "leave-magehouse";
	}
	if (!"goodmorning") {
		goto "bedroom-door-watch";
	}
}
"leave-magehouse" {
	warp_state = "exit_from-magehouse";
	load map "main";
}
"bedroom-door-watch" {
	if (player intersects geometry "bedroom-door") {
		goto "goodmorning-cutscene";
	}
}

"goldfish-glitchwatch" {
	if (!self glitched) {
		goto "goldfish_now_unglitched";
	}
}
"goldfish_now_unglitched" {
	"goldfish-unglitched" = true;
	goto "null_script";
}

/* ---------- CHOREOGRAPHY ---------- */

"zippy-arriving" {
	entity "Aunt Zippy" position -> geometry "zippy-arrive" length over 1700ms;
	goto "null_script";
}
"zappy-arriving" {
	entity "Uncle Zappy" position -> geometry "zappy-arrive" length over 1400ms;
	goto "null_script";
}

"freak-out" {
	self direction = south;
	wait 500ms; /**/ self direction = west; 
	wait 200ms; /**/ self direction = south; 
	wait 200ms; /**/ self direction = east; 
	wait 200ms; /**/ self direction = north; 
	wait 200ms; /**/ self direction = west; 
	wait 200ms; /**/ self direction = south; 
	wait 200ms; /**/ self direction = east; 
	wait 200ms; /**/ self direction = north; 
	wait 200ms;
}

"goodmorning-cutscene" {
	player_control = off;
	player current_animation = 0;
	player direction = west;
	show dialog {
		entity "Aunt Zippy" "Is that %PLAYER% I hear?"
		entity "Uncle Zappy" "Oh good! They're awake!"
	};
	// doc: Is that PLAYER I hear?
	entity "Uncle Zappy" on_tick = "zappy-arriving";
	entity "Aunt Zippy" on_tick = "zippy-arriving";
	wait 400ms; /**/ camera -> geometry "morning-camera" origin over 1000ms;
	wait 600ms; /**/ show dialog {
		PLAYER "Oh! Uncle Zappy! Aunt Zippy!"
		entity "Uncle Zappy" "Good morning, %PLAYER%!"
		"We were beginning to think you were going to sleep through your big day!"
		PLAYER "Big day?"
	};
	wait 200ms; /**/ player direction = south;
	wait 200ms; /**/ show dialog {
		PLAYER "(OH GOD IT'S TUESDAY!)"
	};
	wait 200ms; /**/ player on_tick = "freak-out";
	wait 200ms; /**/ show dialog {
		entity "Aunt Zippy" "Don't worry! We weren't about to let you miss it!"
	};
	wait 200ms; /**/ player on_tick = "spin_self_clockwise";
	wait 200ms; /**/ show dialog {
		PLAYER "(AAAAH!)"
	};
	player on_tick = "null_script";
	player direction = west;
	show dialog {
		entity "Uncle Zappy" "Your 16th birthday is a big day. But no matter what happens, we're proud of you and the mage you've become."
		"Remember that today, okay?"
	};
	player direction = south;
	wait 200ms; /**/ show dialog {
		PLAYER "Okay."
		"Yeah. I'll be okay."
		"It's just the most important day in my life, but I'll definitely be okay."
	};
	wait 200ms; /**/ show dialog {
		entity "Uncle Zappy"
		"Come along, now! The village elders are all waiting for you!"
	};
	camera -> player position over 399ms;
	wait 1ms;
	entity "Uncle Zappy" on_tick = "zappy-leaving";
	entity "Aunt Zippy" on_tick = "zippy-leaving";
	"goodmorning" = true;
	player_control = on;
	// RESET (jump to top)
	goto "on_tick-magehouse";
}

"zippy-leaving" {
	entity "Aunt Zippy" position -> geometry "zippy-leave" origin over 300ms;
	entity "Aunt Zippy" position -> geometry "zippy-leave" length over 1800ms;
	entity "Aunt Zippy" position = geometry "magehouse-hidingplace";
	entity "Cake" on_interact = "show_dialog-cake-nozappy";
	goto "null_script";
}
"zappy-leaving" {
	wait 200ms;
	entity "Uncle Zappy" position -> geometry "zappy-leave" origin over 300ms;
	entity "Uncle Zappy" position -> geometry "zappy-leave" length over 2100ms;
	entity "Uncle Zappy" position = geometry "magehouse-hidingplace";
	goto "null_script";
}

"mage-to-lodge" {
	player_control = off;
	player position -> geometry "morning-walk-mage" length over 10300ms;
	load map "lodge";
	player_control = on;
}
"zippy-to-lodge" {
	entity "Ram" position -> geometry "morning-walk-zippy" length over 10200ms;
	entity "Ram" position = geometry "catwalk-hide";
	goto "null_script";
}
"zappy-to-lodge" {
	entity "Helga" position -> geometry "morning-walk-zappy" length over 9800ms;
	entity "Helga" position = geometry "catwalk-hide";
	goto "null_script";
}
"walk-to-lodge" {
	player_control = off;
	entity "Verthandi" position = geometry "catwalk-hide";
	entity "Marta" position = geometry "catwalk-hide";
	entity "Cleo" on_tick = "null_script";
	entity "Cleo" position = geometry "catwalk-hide";
	entity "Shepherd" position = geometry "catwalk-hide";
	entity "Max Swagger" position = geometry "catwalk-hide";
	entity "Alfonso" position = geometry "catwalk-hide";
	entity "Bert" position = geometry "catwalk-hide";
	entity "Jackob" position = geometry "catwalk-hide";
	entity "Helga" position = geometry "morning-walk-zappy";
	entity "Ram" position = geometry "morning-walk-zippy";
	entity "Helga" type = "lightning_boy2"; // Uncle Zappy
	entity "Ram" type = "lightning_boy"; // Aunt Zippy
	entity "Helga" direction = north;
	entity "Ram" direction = north;
	player position -> geometry "enter_from-magehouse" length over 300ms;
	wait 400ms; /**/ entity "Helga" on_tick = "zappy-to-lodge";
	wait 200ms; /**/ entity "Ram" on_tick = "zippy-to-lodge";
	wait 400ms; /**/ player on_tick = "mage-to-lodge";
	"walked-to-lodge" = true;
}

/* ---------- DIALOG ---------- */
"show_dialog-zappy-start" {
	if (self glitched) {
		show dialog {
			SELF "Abj V srry rkgen mnccl!"
		};
	} else {
		copy!("face-player")
		show dialog {
			SELF "Did you remember to feed %Mr. Tickles% today?"
		};
	}
}

"show_dialog-zippy-start" {
	if (self glitched) {
		show dialog { SELF "Jbj, guvf srryf ernyyl jrveq! Vg erzvaqf zr bs guvf bar gvzr va pbyyrtr...." };
	} else {
		copy!("face-player")
		show dialog { SELF "How's your day going, %PLAYER%?" };
	}
}

"show_dialog-goldfish" {
	show dialog {
		PLAYER "Good morning, %Mr. Tickles%!"
	};
	if (!entity "Mr. Tickles" glitched) {
		show dialog {
			SELF portrait goldfish "(Glub, glub.)"
			PLAYER "Looking good!"
		};
	} else {
		show dialog {
			SELF portrait goldfish "(srrq zr guerr unzf)"
			PLAYER "Good ol' glitched-out fish."
		};
		if (!"goldfish-unglitched") {
			if ("ring-acquired") {
				copy!("set-hint-glitch")
			}
		}
	}
}

"show_dialog-magejournal" {
	show dialog {
		entity "Mage Journal" portrait journal
		"Dear Diary,"
		"I'm turning 16 soon, and I keep wondering what my magic will turn out to be."
		"Maybe I'll be able to turn invisible! Or fly!"
		"But really, my power will probably turn out to be something dumb."
		"I just hope I don't make a mess of anything. The town is already messed up enough from the Big Bad's hacks."
	};
}
"show_dialog-magefacts" {
	show dialog {
		entity "Mage Facts" portrait journal
		"Mage Facts Magazine, issue 28."
		"Did you know you can press X to run, circle to interact with something, and square to perform a short animation?"
		PLAYER "Well, what about triangle?"
		"What say you about triangle, Mage Facts Magazine?"
	};
}
"show_dialog-magebox" {
	show dialog {
		PLAYER "I can't even remember what's in that box. Comics? Amiibos? Otomatones?"
	};
}
"show_dialog-microwave-magehouse" {
	show dialog {
		PLAYER "Whoops! I think this volcano bakemeat is a bit overdone."
	};
}

"show_dialog-cake" {
	show dialog {
		PLAYER "Is that cake for me?"
		entity "Uncle Zappy" "It's for tonight! Happy birthday, %PLAYER%!"
	};
}
"show_dialog-cake-nozappy" {
	show dialog {
		PLAYER "Is that my birthday cake?"
		"Oh, it looks delicious!"
	};
}

"show_dialog-sink" {
	show dialog {
		PLAYER "Gotta keep my hands washed!"
	};
}

/* ---------- DEBUG ---------- */

"debug-walk-to-lodge-q" {
	self direction = player;
	show dialog {
		SELF "Go on a morning walk?"
		> "Yes" = "debug-walk-to-lodge"
		> "No" = "debug-walk-to-lodge-no"
	};
}

"debug-walk-to-lodge" {
	copy!("walk-to-lodge")
}

"debug-walk-to-lodge-no" {
	self on_interact = "debug-walk-to-lodge-q";
}

"debug-goodmorning-q" {
	self direction = player;
	show dialog {
		SELF "Start the beginning cutscene?"
		> "Yes (just the magehouse and exterior)" = "debug-goodmorning"
		> "Yes (also the lodge stuff)" = "debug-goodmorning-plus"
		> "No" = "debug-goodmorning-no"
	};
}

"debug-goodmorning" {
	show dialog "dialog-debug-goodmorning" { // reused below
		SELF "Walk through the doorway to begin cutscene."
	};
	hex_control = off;
	copy!("set-goodmorning-flags-false")
	map on_tick = "on_tick-magehouse";
}

"debug-goodmorning-plus" {
	show dialog "dialog-debug-goodmorning";
	hex_control = off;
	copy!("set-goodmorning-flags-false")
	"ring-acquired" = false;
	"hide-lodge-crowd" = false;
	"hide-lodge-crowd-mini" = false;
	"tuesdayd" = false;
	"hintman-explanation" = false;
	map on_tick = "on_tick-magehouse";
}

"debug-goodmorning-no" {
	self on_interact = "debug-goodmorning-q";
}

"debug-everything-q" {
	show dialog {
		SELF "DEBUG EVERYTHING"
		> "Turn debug mode on" = "debug-everything-on"
		> "Go to debug map hub" = "debug-hub-go"
		> "Never mind" = "debug-everything-off"
	};
}

"debug-everything-on" {
	"debug-mode" = true;
	hex_control = on;
	copy!("set-goodmorning-flags-true")
	"tuesday-walkedup" = true;
	"hintman-explanation" = true;
	map on_tick = "on_tick-magehouse";
	entity "Debug Exa" position = geometry "debug-exa-spot";
	show dialog {
		SELF "Debug mode on."
	};
	self on_interact = "debug-everything-q";
}

"debug-everything-off" {
	show dialog { SELF "Never mind."};
	self on_interact = "debug-everything-q";
}
