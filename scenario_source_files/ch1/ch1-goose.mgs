include "header.mgs";

/* --------------- on_tick --------------- */

// goose mission

"on_tick-goose-mission" {
	// on_tick is 'mission' -- if rake not in lake, jump
	if (!entity "Rake" intersects geometry "lake") {
		goto "on_tick-goose-mission-rake-out-of-lake";
	}
	entity "Goose" direction = south;
	entity "Goose" current_animation = 0;
	"storyflag-rake" = true;
	copy!("calculate-hint-tracking-series")
	if ("hint-tracking-check" == 10) {
		// if the hint series is the currently goose, reset hint
		copy!("set-hint-none")
	}
	// you win!
	goto null_script;
}
"on_tick-goose-mission-rake-out-of-lake" {
	// if rake is approx at start point, jump
	if (entity "Rake" intersects geometry "rake-point") {
		goto "goose-misison-rake-on-rake-point";
	}
	entity "Goose" direction = south;
	copy!(honk)
	entity "Goose" position -> geometry "goose_midpoint" origin over 600ms;
	entity "Goose" position -> geometry "goose_lakepoint" origin over 600ms;
	entity "Goose" direction = south;
	goto "on_tick-goose-mission";
}
"goose-misison-rake-on-rake-point" {
	// if rake has left its start point, jump
	if (!entity "Rake" intersects geometry "rake-point") {
		goto "on_tick-goose-mission-rake-out-of-lake";
	}
	entity "Goose" direction = south;
	copy!(honk)
	entity "Goose" position -> geometry "goose_rakepoint" origin over 1200ms;
	entity "Goose" direction = entity "Rake";
	// if rake has left its start point, jump
	if (!entity "Rake" intersects geometry "rake-point") {
		goto "on_tick-goose-mission-rake-out-of-lake";
	}
	copy!(honk)
	entity "Goose" position -> geometry "goose_lakepoint" origin over 1200ms;
	entity "Goose" direction = south;
	// if rake has left its start point, jump
	if (!entity "Rake" intersects geometry "rake-point") {
		goto "on_tick-goose-mission-rake-out-of-lake";
	}
	goto "on_tick-goose-mission";
}

"honk" {
	entity "Goose" animation -> 2 thrice;
}
"honk-to-make-rake-float" {
	entity "Goose" direction = entity "Rake";
	wait 200ms; /**/ entity "Goose" current_animation = 2;
	wait 600ms; /**/ entity "Goose" on_tick = "make-rake-float";
	wait 3000ms; /**/ entity "Goose" current_animation = 0;
}

// drifting rake

"make-rake-float" { // on_tick
	entity "Rake" on_tick = "rake-drift-south";
	wait 600ms; /**/ entity "Rake" on_tick = "rake-drift-west";
	wait 600ms; /**/ entity "Rake" on_tick = "rake-drift-south";
	wait 600ms; /**/ entity "Rake" on_tick = "rake-drift-east";
	wait 600ms;
}
"rake-drift-south" {
	entity "Rake" position -> geometry "drift-south" origin over 60000ms;
}
"rake-drift-east" {
	entity "Rake" position -> geometry "drift-east" origin over 60000ms;
}
"rake-drift-west" {
	entity "Rake" position -> geometry "drift-west" origin over 60000ms;
}

// goose rescue protocol

"goose-bounds-watch" {
	if (!entity "Goose" intersects geometry "bounds") {
		goto "rescue-goose";
	}
}
"rescue-goose" {
	entity "Goose" on_tick = null_script;
	entity "Goose" position = geometry "goose-rescue";
	entity "Goose" position -> geometry "goose_lakepoint" origin over 2000ms;
	entity "Goose" on_tick = "on_tick-goose-mission";
	self on_tick = "goose-bounds-watch";
}

/* --------------- dialog --------------- */

"show_dialog-goose-generic" {
	copy!(face_player)
	show dialog { SELF "Honk!" };
}

"show_dialog-goose-mad" {
	show dialog { SELF "Honk!" };
	if (!"storyflag-rake") {
		copy!("set-hint-goose")
	}
	// RESET
	self on_interact = "show_dialog-goose-mad";
}

/* --------------- debug --------------- */

"debug_win_rake_q" {
	copy!(face_player)
	show dialog {
		SELF "Position rake in lake?"
		> "Yes" = "debug_win_rake_yes"
		> "No" = "debug_win_rake_no"
	};
}
"debug_win_rake_yes" {
	entity "Rake" position = geometry "rake-lake-point";
	self on_interact = "debug_win_rake_q";
}
"debug_win_rake_no" {
	self on_interact = "debug_win_rake_q";
}
