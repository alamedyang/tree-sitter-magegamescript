include "header.mgs";
add dialog settings {
	entity Cat { alignment TL }
	entity Cleo { alignment TL }
}

/* ---------- on_load scripts ---------- */

rake_in_lake {
	entity Rake position = geometry "rake-lake-point";
}
sheep_in_pen {
	entity Baa position = geometry baa_point;
	entity Ram position = geometry ram_point;
	entity Ewe position = geometry ewe_point;
	entity Helga position = geometry helga_point;
}
bender_happybutt {
	entity Bender type = bender;
}

on_load_map_main {
	if (tuesdayd) {
		command rtfm = command_rtfm;
	}
	// if you haven't started ch1 yet
	if (!begunthegame) {
		begunthegame = true;
		hex_control = off;
		camera fade in -> #000000 over 1ms; 
		load map magehouse;
	}
	// assuming you have ring zero
	hex_control = on;
	if (bobmoss_quest_done) {
		entity "Bob Moss" position = geometry "catwalk-hide";
	} 
	if (rake_quest_done) {
		copy!(rake_in_lake)
		entity Goose on_interact = honk_to_make_rake_float;
	}
	if (billy_unglitched) {
		entity Billy glitched = false;
	}
	if (kid_unglitched) {
		entity Kid glitched = false;
	}
	if (fountain_unglitched) {
		entity Fountain glitched = false;
	}
	if (main_hamster_unglitched) {
		entity Hamster glitched = false;
	}
	if (shepherd_quest_done) {
		copy!(sheep_in_pen)
	}
	if (bender_quest_done) {
		copy!(bender_happybutt)
	}
	if (!walked_to_lodge) {
		goto walk_to_lodge;
	}
	player_control = off;
	if (!hintman_explanation) {
		player position -> geometry "enter_from-lodge" length over 400ms; 
		copy!(hintman_cutscene) // see script-elders.json for this
		hintman_explanation = true;
	} else if (warp_state == enter_from_east) {
		player position -> geometry enter_from_east length over 1000ms; 
	} else if (warp_state == enter_from_west) {
		player position -> geometry enter_from_west length over 1000ms; 
	} else if (warp_state == exit_from_bobsclub) {
		player position -> geometry "enter_from-bobsclub" length over 200ms; 
	} else if (warp_state == exit_from_bakery) {
		player position -> geometry "enter_from-bakery" length over 200ms; 
	} else if (warp_state == exit_from_lodge) {
		player position -> geometry "enter_from-lodge" length over 200ms; 
	} else if (warp_state == exit_from_greenhouse) {
		player position -> geometry "enter_from-greenhouse" length over 200ms; 
	} else if (warp_state == exit_from_magehouse) {
		player position -> geometry "enter_from-magehouse" length over 200ms; 
	} else if (warp_state == exit_from_oldcouplehouse) {
		player position -> geometry "enter_from-oldcouplehouse" length over 200ms; 
	} else if (warp_state == exit_from_family) {
		player position -> geometry "enter_from-family" length over 200ms; 
	} else if (warp_state == exit_from_woprhouse) {
		player position -> geometry "enter_from-woprhouse" length over 200ms; 
	} else if (warp_state == exit_from_credits) {
		player position = geometry "from-credits-spot";
		camera fade in -> #000000 over 800ms;
		player_control = on;
		if (!ch1_finished) {
			goto null_script;
		}
		if (!catwork_quest_done) {
			goto null_script;
		}
		if (!shepherd_quest_done) {
			goto null_script;
		}
		if (!bender_quest_done) {
			goto null_script;
		}
		if (!baker_quest_done) {
			goto null_script;
		}
		if (!bobmoss_quest_done) {
			goto null_script;
		}
		if (!bea1_quest_done) {
			goto null_script;
		}
		if (!trekkie_quest_done) {
			goto null_script;
		}
		if (!sportskid_quest_done) {
			goto null_script;
		}
		if (!bea2_quest_done) {
			goto null_script;
		}
		if (!rake_quest_done) {
			goto null_script;
		}
		if (!verthandi_quest_done) {
			goto null_script;
		}
		if (demobonus_jackob_informed) {
			goto null_script;
		}
		demobonus = true;
		// ^^ set now because all story flags and credits confirmed true; use this flag for same check hereafter
		show dialog {
			Jackob "While you were gone, I saw an odd flash of light coming from the library."
			"You might want to investigate."
		};
		demobonus_jackob_informed = true;
	}
	player_control = on;
}

/* ---------- on_tick scripts ---------- */

yabbo_journey {
	self position -> geometry "%ENTITY_PATH%" length over 22000ms; 
}

// watchers

fountain_glitchwatch {
	if (!self glitched) {
		goto fountain_now_unglitched;
	}
}
fountain_now_unglitched {
	fountain_unglitched = true;
	goto null_script;
}
hamster_glitchwatch {
	if (!self glitched) {
		goto hamster_now_unglitched;
	}
}
hamster_now_unglitched {
	main_hamster_unglitched = true;
	goto null_script;
}
cat_watch {
	if (!entity Cat intersects geometry "cat_watch-box") {
		goto cat_rescue_protocol;
	}
}
cat_rescue_protocol {
	entity Cat position = geometry "cat-rescue-path";
	entity Cat position -> geometry "cat-rescue-path" length over 800ms; 
	copy!(cat_and_cleo_convo_turn)
	entity Cat on_tick = cat_watch;
}
cleo_watch {
	if (!entity Cleo intersects geometry "cleo_watch-box") {
		goto cleo_rescue_protocol;
	}
	// WARNING!! if you change who is watching, change who to null_script for the walk_to_lodge cutscene!!!
}
cleo_rescue_protocol {
	entity Cleo position = geometry "cleo-rescue-path";
	entity Cleo position -> geometry "cleo-rescue-path" length over 800ms; 
	copy!(cat_and_cleo_convo_turn)
	entity Cleo on_tick = cleo_watch;
}

// map loading scripts

on_tick_main {
	if (player intersects geometry "door-bakery-entrance") {
		goto load_map_bakery;
	}
	if (player intersects geometry "door-family-entrance") {
		goto load_map_family;
	}
	if (player intersects geometry "door-greenhouse-entrance") {
		goto load_map_greenhouse;
	}
	if (player intersects geometry "door-magehouse-entrance") {
		goto load_map_magehouse;
	}
	if (player intersects geometry "door-lodge-entrance") {
		goto load_map_lodge;
	}
	if (player intersects geometry "door-oldcouplehouse-entrance") {
		goto load_map_oldcouplehouse;
	}
	if (player intersects geometry "door-woprhouse-entrance") {
		goto load_map_woprhouse;
	}
	if (player intersects geometry "door_to-town-east") {
		goto wrap_from_west;
	}
	if (player intersects geometry "door_to-town-west") {
		goto wrap_from_east;
	}
}

load_map_bakery { load map bakery; }
load_map_oldcouplehouse { load map oldcouplehouse; }
load_map_greenhouse { load map greenhouse; }
load_map_magehouse { load map magehouse; }
load_map_lodge { load map lodge; }
load_map_family { load map family; }
load_map_woprhouse { load map woprhouse; }

wrap_from_west {
	player on_tick = on_tick_wrap_into_west;
	camera fade out -> #000000 over 600ms; 
	player on_tick = on_tick_wrap_from_west;
	camera fade in -> #000000 over 600ms; 
	goto on_tick_main;
}
on_tick_wrap_into_west {
	player_control = off;
	player position -> geometry "exit_to-west" origin over 3500ms; 
	goto null_script;
}
on_tick_wrap_from_west {
	player position -> geometry "enter_from_east" length over 600ms; 
	player_control = on;
	goto null_script;
}
wrap_from_east {
	player on_tick = on_tick_wrap_into_east;
	camera fade out -> #000000 over 600ms; 
	player on_tick = on_tick_wrap_from_east;
	camera fade in -> #000000 over 600ms; 
	goto on_tick_main;
}
on_tick_wrap_into_east {
	player_control = off;
	player position -> geometry "exit_to-east" origin over 3500ms; 
	goto null_script;
}
on_tick_wrap_from_east {
	player position -> geometry "enter_from_west" length over 600ms; 
	player_control = on;
	goto null_script;
}

/* ---------- dialog ---------- */

show_dialog_yabbo {
	player on_tick = yabbo_watch;
	show dialog {
		SELF "I found infinity! Take a butcher's!"
		// Butcher's hook = look
		// (Cockney rhyming slang, which was requested by the artist for this character)
		// (Something more clever might be used instead; feel free to change)
	};
	player on_tick = null_script;
}
yabbo_watch {
	player direction = entity "Yabbo Mongo";
}
show_dialog_construction {
	show dialog {
		name Construction "UNDER CONSTRUCTION"
		PLAYER "Guess I'll come back later. Next year, perhaps."
	};
}
show_dialog_messageboard {
	show dialog {
		name MessageBoard
		portrait parchment
		"If you've been the victim of a hack, come join us at our weekly hacked citizen support group, Friday nights at 6pm in the library."
	};
}
show_dialog_safety_skyler {
	show dialog {
		PLAYER "Safety Skyler says:\n\"Remember to wear eye protection!\""
	};
}
fountain_interact {
	if (!self glitched) {
		show dialog {
			PLAYER "This is fountain is awesome! I've always wanted to see it un-glitched!"
		};	
	} else {
		show dialog {
			PLAYER "This fountain has been glitched as long as I can remember, but now I have the chance to fix it!"
			"I just need to figure out how...."
		};
	}
	self on_interact = fountain_interact;
}

/* ---------- cat and cleo ---------- */

turn_padding_start {
	player_control = off;
	wait 200ms;
}
turn_padding_end {
	wait 0ms;
	player_control = on;
}

cat_and_cleo_convo_turn {
	entity Cleo direction = entity Cat;
	entity Cat direction = entity Cleo;
}

cat_and_cleo_convo_turn_player {
	copy!(turn_padding_start)
	entity Cleo direction = player;
	entity Cat direction = player;
	copy!(turn_padding_end)
}

show_dialog_cleo1 {
	if (self glitched) {
		show dialog { Cleo "Uzz, V srry n ovg shaal." };
	} else {
		copy!(cat_and_cleo_convo_turn)
		show dialog {
			Cat "Meowrrow."
			Cleo "Oh, you don't say!"
			Cat "Meow!"
			Cleo "He didn't!"
		};
	}
	entity Cleo on_interact = show_dialog_cleo2;
}

show_dialog_cleo2 {
	if (self glitched) {
		show dialog { Cleo "Uzz, V srry n ovg shaal." };
	} else {
		copy!(cat_and_cleo_convo_turn)
		show dialog {
			Cat "Meowwowow."
			Cleo "That's what I've been saying this whole time!"
			Cat "Meowwrrrr!"
			Cleo "Oh, I know!"
		};
	}
	entity Cleo on_interact = show_dialog_cleo1;
}

show_dialog_cat {
	if (self glitched) {
		show dialog { Cat "...." };
	} else if (hint_tracking == 61 || hint_tracking == 62) {
		show dialog {
			PLAYER alignment TR
			"Excuse me. I'm looking for the cat boss. Is that you?"
		};
		copy!(turn_padding_start)
		copy!(cat_and_cleo_convo_turn_player)
		show dialog {
			Cat "...."
			Cleo "Cat boss?"
			PLAYER alignment TR "Yeah. The construction crew needs a thumbs up."
			Cleo "Well, the cat boss is very busy right now."
			PLAYER alignment TR "But the crew --"
			Cat "Meowrrow!"
			Cleo "Yeah, get some other white cat to do the thumbs up!"
		};
		if (player type == pipscat) {
			show dialog {
				PLAYER alignment TR
				"Some other white cat? Seriously?"
				"You mean me?"
			};
		} else {
			show dialog {
				PLAYER alignment TR
				"But... where else am I gonna find a white cat?"
			};
		}
		copy!(cat_and_cleo_convo_turn)
		copy!(turn_padding_end)
		copy!(set_hint_max_from_cleo)
		white_cat = true;
	} else { // normal conversation
		show dialog {
			PLAYER alignment TR "Hey there, cat!"
		};
		copy!(turn_padding_start)
		copy!(cat_and_cleo_convo_turn_player)
		show dialog {
			Cat "...."
			Cleo "Excuse me, but we were in the middle of a conversation."
			PLAYER alignment TR "Oh, sorry. Carry on."
		};
		copy!(cat_and_cleo_convo_turn)
		copy!(turn_padding_end)
	}
	self on_interact = show_dialog_cat;
}

show_dialog_hamster_start {
	copy!(face_player)
	if (self glitched) {
		show dialog {
			SELF "Mxqj'i jxyi? Q mehbtmytu iybysed ixehjqwu?!"
		};
		if (!main_hamster_unglitched) {
			copy!(set_hint_glitch)
		}
	} else if (hamster_endstory) {
		show dialog {
			SELF "Did you know that all electronics are powered by angry pixies?"
			"The trick is to keep the pixies angry. If the pixies aren't angry, then they start to get complacent, and that's when your battery dies!"
		};
		self animation -> 2 twice;
	} else {
		show dialog {
			SELF "Ah, that's better."
			"It was real rough wrangling angry pixies with discombobulated pixel hands."
		};
		hamster_endstory = true;
	}
	self on_interact = show_dialog_hamster_start;
}

show_dialog_marta_start {
	entity Marta direction = player;
	if (self glitched) {
		show dialog {
			SELF "Jpd, hp rpe te! Jzf slnv!"
		};
	} else if (entity Marta type != marta_broom) {
		show dialog {
			SELF "...." "That's cheating."
		};
	} else if (!marta_backstory) {
		show dialog {
			SELF "It's true. I can fly. It's not a trick."
			"I had my TUESDAY a few years ago, and this\nbroom chose me."
			"I'm one of the last mages to gain an\nartifact in a while. It's kind of nice\nthere's finally another."
			"Though your powers seem more useful than\nmine. All I can do is fly!"
			PLAYER "That's still pretty cool!"
			SELF "It helps me deliver newspapers, I guess.\nCan't fix much else around here."
		};
		marta_backstory = true;
	} else {
		show dialog {
			SELF "Good luck."
		};
	}
	self on_interact = show_dialog_marta_start;
}

dialog dialog_timmy {
	Timmy "I'm practicing for my triathlon!"
	"Running is my weakest event, so I'm working hard to improve my time!"
	PLAYER "Don't you need legs to run?"
	Timmy "...?"
	"No?"
}
dialog dialog_dsheep {
	SELF "...?"
}
