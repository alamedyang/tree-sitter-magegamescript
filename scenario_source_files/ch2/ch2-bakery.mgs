include "header.mgs";

/* ---------- LOOK SCRIPTS ---------- */

"look-ch2-bakery" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked around the <c>BAKERY</>."
		"\tThe place smells amazing! Warm, yeasty -- the very essence of nostalgia. The %Baker% keeps this place clean, tidy, and overflowing with baked goods."
		" "
	};
}

"look-ch2-baker" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the <m>%SELF%</>."
		"\tHe rarely spares a glance for you as he rushes about, but his expression is warm when he does. He is smooth with his motions around the kitchen; it's clear he's had a lifetime of practice."
	};
}

/* ---------- ON_LOAD ---------- */

"on_load-ch2-bakery" {
	// entrance text
	show serial_dialog spacer;
	show serial_dialog { "Entering <bold>BAKERY</>..." };
	// The part of Aunt Zippy is being played by Jackob
	if (
		"ch2-storyflag-round" == 3
		|| "ch2-storyflag-round" == 4
	) {
		if ("ch2-storyflag-round" == 3) {
			entity "Jackob" position = geometry "zippy-spot1";
		} else if ("ch2-storyflag-round" == 4) {
			entity "Jackob" position = geometry "zippy-spot2";
		}
		entity "Jackob" name = "Aunt Zippy";
		entity "Jackob" on_look = "look-ch2-zippy";
		entity "Jackob" on_tick = null_script;
		entity "Jackob" on_interact = "interact-ch2-zippy";
		entity "Jackob" type = "lightning_boy";
		entity "Jackob" direction = north;
	} else if ("ch2-storyflag-round" != 2) {
		entity "Jackob" on_tick = yeet;
	}
	// All the rest
	copy!("ch2-map-init")
}

/* ---------- ON_TICK ---------- */

"on_tick-ch2-bakery" {
	if (player intersects geometry "south-hitbox") {
		warp_state = "walk_from-bakery";
		load map "ch2-town";
	}
	map on_tick = "on_tick-ch2-bakery";
}

/* ---------- EXIT STUFF ---------- */

"on_go-ch2-bakery-south" {
	copy!("warping-out-fade-out")
	load map "ch2-town";
}

/* ---------- ENTITIES ---------- */

"interact-ch2-baker" {
	copy!("interrupt-walk")
	if ("ch2-storyflag-round" >= 5) {
		show dialog {
			SELF "Good job building that computer. That's really impressive."
		};
	} else {
		show dialog {
			SELF "Sorry, can't talk now."
			"But good luck conquering the serial castle."
		};
	}
	copy!("resume-walk")
}

"interact-ch2-jackob-bakery" {
	copy!("face-player")
	show dialog {
		SELF "I'm just taking a little shopping break. Sometimes you just really want some fresh bread, you know?"
		"Mmmm, fresh bread!"
	};
	self direction = south;
}
