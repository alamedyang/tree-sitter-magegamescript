include "header.mgs";

/* ---------- LOOK SCRIPTS ---------- */

look_ch2_lodge {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked around the <c>LIBRARY</>."
		"\tThe sight of endless books -- and the smell of books, new and old! Is there another single sight on the planet that promises such boundless knowledge, fantastic adventure, or exquisite trivia factoids?"
		" "
	};
}

look_ch2_cat_lodge {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the <m>%SELF%</>."
		"\tIts eyes are fixed on your every movement. This is probably normal for a cat."
	};
}

look_ch2_timmy {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tThe child monster-bug wears his heart on his... arms? In any case, he is clearly excited by what he's reading."
	};
}

look_ch2_sportsbook {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tThe cover of this book is so vague that it's unclear which sport it's celebrating."
	};
}

look_ch2_dejavu {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tThe magazine itself seems not to have run long, as this issue claims to be both the third issue and the final issue. The glossy cover is a badly-Photoshopped collage of improbable monsters in implausible situations."
	};
}

look_ch2_manual {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the <m>%SELF%</>."
		"\tOverall, you're left with confusion as to why a calculator would require a whole book to use it properly."
	};
}

/* ---------- ON_LOAD ---------- */

on_load_ch2_lodge {
	// entrance text
	show serial_dialog spacer;
	show serial_dialog { "Entering <bold>LIBRARY</>..." };
	// do unique stuff first (but player_control = serial / off first, if you do)
	if (ch2_storyflag_round != 1) {
		entity Alfonso on_tick = yeet;
	}
	// The part of Uncle Zappy is being played by Jackob
	if (
		ch2_storyflag_round == 3
		|| ch2_storyflag_round == 4
	) {
		entity Jackob position = geometry "zappy-spot";
		entity Jackob name = "Uncle Zappy";
		entity Jackob on_look = look_ch2_zappy;
		entity Jackob on_tick = null_script;
		entity Jackob on_interact = interact_ch2_zappy;
		entity Jackob type = lightning_boy2;
		entity Jackob direction = north;
	} else if (ch2_storyflag_round != 1) {
		entity Jackob on_tick = yeet;
	}
	// hide stuff
	if (
		ch2_installed_manual
		|| ch2_carrying_manual
	) {
		copy!(ch2_hide_manual)
	}

	if (warp_state == "walk_from-north") {
		player_control = off;
		player position = geometry "walk_from-north";
	}
	// player control comes on after this next script is over:
	copy!(ch2_map_init)
}

/* ---------- ON_TICK ---------- */

on_tick_ch2_lodge {
	if (player intersects geometry "south-hitbox") {
		warp_state = "walk_from-lodge";
		load map "ch2-town";
	}
	if (player intersects geometry "north-hitbox") {
		warp_state = "walk_from-south";
		load map "ch2-lodge-rtfm";
	}
}

look_left_sometimes {
	self direction = west;
	wait 800ms;
	self direction = north;
	wait 3800ms;
	self direction = west;
	wait 500ms;
	self direction = north;
	wait 2500ms;
}

/* ---------- EXIT STUFF ---------- */

on_go_ch2_lodge_south {
	copy!(warping_out_fade_out)
	load map "ch2-town";
}
on_go_ch2_lodge_north {
	copy!(warping_out_fade_out)
	load map "ch2-lodge-rtfm";
}

/* ---------- ENTITIES ---------- */

interact_ch2_manual {
	if (!ch2_want_manual) {
		show dialog {
			PLAYER "Looks like a book about a calculator."
		};
	} else {
		show dialog {
			PLAYER "Oh, this must be the calculator manual Frankie wanted."
			name "" "(You pick up the calculator manual!)"
		};
		copy!(ch2_pickup_manual)
		copy!(ch2_hide_manual)
	}
}
ch2_hide_manual {
	entity "HP35c Man" position = geometry "hiding-spot";
	entity "HP35c Man" name = " ";
}

interact_ch2_timmy {
	copy!(face_player)
	if (ch2_storyflag_round == 0) {
		show dialog {
			SELF "I love the library! There's so many books here it would take a hundred years to read them all!"
		};
	} else if (ch2_storyflag_round <= 2) {
		show dialog {
			SELF "If you had a race car, what color would you want?"
			"I think red cars are faster than blue cars, usually."
		};
	} else if (ch2_storyflag_round <= 4) {
		show dialog {
			PLAYER "Hey, %SELF%, do you know if Bert came in here recently?"
			SELF "Bert? Who's that?"
			PLAYER "Well, he's one of the village elders. He's the big one, the one who --"
			SELF "Oh yeah, Bert is the one that looks like a bear, right?"
			PLAYER "Bear?"
			SELF "Yeah, bear-Bert!"
			"But you see, I actually don't know if I saw him or not."
			"I was reading."
			PLAYER "Oh, I see, you were busy reading. Yeah, I guess that makes sense. Thanks anyway."
		};
	} else {
		show dialog {
			SELF "So you see... I think... bees are kind of scary."
			"I wasn't stung before, but I don't want them to sting me!"
			"Bees and bugs -- but especially bees -- really freak me out."
		};
	}
	self direction = east;
}
interact_ch2_cat_lodge {
	show dialog {
		SELF "..."
	};
}

interact_ch2_jackob_library {
	show dialog {
		SELF "I hate geese. Especially that one."
		"It keeps staring at me. I can't concentrate on what I'm doing."
	};
}

$bookcount = 4;

interact_ch2_alfonso_library {
	ch2_alfonso_library_count += 1;
	ch2_alfonso_library_count %= $bookcount;
	if (ch2_alfonso_library_count == 1) {
		show dialog {
			SELF "Oh, goodness! It's \"The Importance of Being Earnest.\" I remember this play. Twenty years ago, Jackob and I did a reading of it with --"
		};
	} else if (ch2_alfonso_library_count == 2) {
		show dialog {
			SELF "Aha, \"A Brief History of Time!\" You know, I've always wanted learn about time and space and the universe and all that. And it isn't very long."
		};
	} else if (ch2_alfonso_library_count == 3) {
		show dialog {
			SELF "Oho, \"The Complete Plays of Gilbert and Sullivan.\" I think I memorized one of their patter songs once. But was it from Pinafore or Penzance?"
		};
	} else {
		show dialog {
			SELF "Oh, look at that. \"Rendezvous with Rama,\" but what a stunning cover! I don't recognize this edition. Who publizhed this one? Gollancz?"
		};
	}
	copy!(face_player)
	show dialog {
		SELF "Yes, yes, I'm getting work done! What does it look like I'm doing?"
	};
	self direction = north;
}

ch2_goose_library {
	if (!ch2_has_artifact) {
		show dialog {
			SELF "Honk!"
		};

	} else {
		if (!ch2_goosefacts) {
			concat serial_dialog { "<bell>" };
			show dialog {
				SELF "HONK! You are now subscribed to goose facts!"
			};
			ch2_goosefacts = true;
			command stop = stop_goosefacts;
		} else {
			show dialog {
				SELF "Enjoy your free goose facts!"
			};
		}
	}
}
