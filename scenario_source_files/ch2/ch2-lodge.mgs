include "header.mgs";

/* ---------- LOOK SCRIPTS ---------- */

"look-ch2-lodge" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked around the <c>LIBRARY</>."
		"\tThe sight of endless books -- and the smell of books, new and old! Is there another single sight on the planet that promises such boundless knowledge, fantastic adventure, or exquisite trivia factoids?"
		" "
	};
}

"look-ch2-cat-lodge" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the <m>%SELF%</>."
		"\tIts eyes are fixed on your every movement. This is probably normal for a cat."
	};
}

"look-ch2-timmy" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tThe child monster-bug wears his heart on his... arms? In any case, he is clearly excited by what he's reading."
	};
}

"look-ch2-sportsbook" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tThe cover of this book is so vague that it's unclear which sport it's celebrating."
	};
}

"look-ch2-dejavu" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tThe magazine itself seems not to have run long, as this issue claims to be both the third issue and the final issue. The glossy cover is a badly-Photoshopped collage of improbable monsters in implausible situations."
	};
}

"look-ch2-manual" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the <m>%SELF%</>."
		"\tOverall, you're left with confusion as to why a calculator would require a whole book to use it properly."
	};
}

/* ---------- ON_LOAD ---------- */

"on_load-ch2-lodge" {
	// entrance text
	show serial_dialog spacer;
	show serial_dialog { "Entering <bold>LIBRARY</>..." };
	// do unique stuff first (but player_control = serial / off first, if you do)
	if ("ch2-storyflag-round" != 1) {
		entity "Alfonso" on_tick = yeet;
	}
	// The part of Uncle Zappy is being played by Jackob
	if (
		"ch2-storyflag-round" == 3
		|| "ch2-storyflag-round" == 4
	) {
		entity "Jackob" position = geometry "zappy-spot";
		entity "Jackob" name = "Uncle Zappy";
		entity "Jackob" on_look = "look-ch2-zappy";
		entity "Jackob" on_tick = null_script;
		entity "Jackob" on_interact = "interact-ch2-zappy";
		entity "Jackob" type = "lightning_boy2";
		entity "Jackob" direction = north;
	} else if ("ch2-storyflag-round" != 1) {
		entity "Jackob" on_tick = yeet;
	}
	// hide stuff
	if (
		"ch2-installed-manual"
		|| "ch2-carrying-manual"
	) {
		copy!("ch2-hide-manual")
	}

	if (warp_state == "walk_from-north") {
		player_control = off;
		player position = geometry "walk_from-north";
	}
	// player control comes on after this next script is over:
	copy!("ch2-map-init")
}

/* ---------- ON_TICK ---------- */

"on_tick-ch2-lodge" {
	if (player intersects geometry "south-hitbox") {
		warp_state = "walk_from-lodge";
		load map "ch2-town";
	}
	if (player intersects geometry "north-hitbox") {
		warp_state = "walk_from-south";
		load map "ch2-lodge-rtfm";
	}
}

"look-left-sometimes" {
	self direction = west;
	wait 800ms;
	self direction = north;
	wait 3800ms;
	self direction = west;
	wait 500ms;
	self direction = north;
	wait 2500ms;
}

/* ---------- EXIT STUFF ---------- */

"on_go-ch2-lodge-south" {
	copy!("warping-out-fade-out")
	load map "ch2-town";
}
"on_go-ch2-lodge-north" {
	copy!("warping-out-fade-out")
	load map "ch2-lodge-rtfm";
}

/* ---------- ENTITIES ---------- */

"ch2-interact-manual" {
	if (!"ch2-want-manual") {
		show dialog {
			PLAYER "Looks like a book about a calculator."
		};
	} else {
		show dialog {
			PLAYER "Oh, this must be the calculator manual Frankie wanted."
			name "" "(You pick up the calculator manual!)"
		};
		copy!("ch2-pickup-manual")
		copy!("ch2-hide-manual")
	}
}
"ch2-hide-manual" {
	entity "HP35c Man" position = geometry "hiding-spot";
	entity "HP35c Man" name = " ";
}

"interact-ch2-timmy" {
	copy!("face-player")
	if ("ch2-storyflag-round" == 0) {
		show dialog {
			SELF "I love the library! There's so many books here it would take a hundred years to read them all!"
		};
	} else if ("ch2-storyflag-round" <= 2) {
		show dialog {
			SELF "If you had a race car, what color would you want?"
			"I think red cars are faster than blue cars, usually."
		};
	} else if ("ch2-storyflag-round" <= 4) {
		show dialog {
			PLAYER "Hey, %SELF%, do you know if Bert came in here recently?"
			SELF "Bert? Who's that?"
			PLAYER "Well, he's one of the village elders. He's the big one, the one who --"
			SELF "Oh yeah, Bert is the one that looks like a bear, right?"
			PLAYER "Bear?"
			SELF "Yeah, bear-Bert!"
			"But you see, I actually don't know if I saw him or not."
			"I was reading."
			PLAYER "Oh, I see, you were busy reading. Yeah, I guess that makes sense. Thanks anyway."
		};
	} else {
		show dialog {
			SELF "So you see... I think... bees are kind of scary."
			"I wasn't stung before, but I don't want them to sting me!"
			"Bees and bugs -- but especially bees -- really freak me out."
		};
	}
	self direction = east;
}
"interact-ch2-cat-lodge" {
	show dialog {
		SELF "..."
	};
}

"interact-ch2-jackob-library" {
	show dialog {
		SELF "I hate geese. Especially that one."
		"It keeps staring at me. I can't concentrate on what I'm doing."
	};
}

$bookcount = 4;

"interact-ch2-alfonso-library" {
	"ch2-alfonso-library-count" += 1;
	"ch2-alfonso-library-count" %= $bookcount;
	if ("ch2-alfonso-library-count" == 1) {
		show dialog {
			SELF "Oh, goodness! It's \"The Importance of Being Earnest.\" I remember this play. Twenty years ago, Jackob and I did a reading of it with --"
		};
	} else if ("ch2-alfonso-library-count" == 2) {
		show dialog {
			SELF "Aha, \"A Brief History of Time!\" You know, I've always wanted learn about time and space and the universe and all that. And it isn't very long."
		};
	} else if ("ch2-alfonso-library-count" == 3) {
		show dialog {
			SELF "Oho, \"The Complete Plays of Gilbert and Sullivan.\" I think I memorized one of their patter songs once. But was it from Pinafore or Penzance?"
		};
	} else {
		show dialog {
			SELF "Oh, look at that. \"Rendezvous with Rama,\" but what a stunning cover! I don't recognize this edition. Who publizhed this one? Gollancz?"
		};
	}
	copy!("face-player")
	show dialog {
		SELF "Yes, yes, I'm getting work done! What does it look like I'm doing?"
	};
	self direction = north;
}

"ch2-goose-library" {
	if (!"ch2-has-artifact") {
		show dialog {
			SELF "Honk!"
		};

	} else {
		if (!"ch2-goosefacts") {
			concat serial_dialog { "<bell>" };
			show dialog {
				SELF "HONK! You are now subscribed to goose facts!"
			};
			"ch2-goosefacts" = true;
			command "stop" = "stop-goosefacts";
		} else {
			show dialog {
				SELF "Enjoy your free goose facts!"
			};
		}
	}
}
