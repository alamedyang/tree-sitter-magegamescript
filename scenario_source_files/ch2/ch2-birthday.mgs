include "header.mgs";

/* --- CUTSCENE: Birthday (house) --- */

// NON-DEBUG

"on_load-ch2-magehouse-birthday" {
	hex_control = on;
	player_control = off;
	if (debug_mode) {
		serial_control = on;
		command "skip" = "cutscene-ch2-birthday-exit"; // this shows up twice if put first in the script O.o
		show serial_dialog {
			" "
			"<y>DEBUG></> <c>SKIP</>: skip this cutscene"
		};
	} else {
		serial_control = off;
	}
	hex_editor = closed;
	player current_animation = 0;
	camera fade out -> #000000 over 0ms;
	player direction = east;
	camera = geometry "cake-camera-1";
	entity "Stunt Cake" current_animation = 1;
	wait 1000ms;
	camera fade in -> #000000 over 1000ms;
	wait 500ms;
	show dialog {
		name " " // blank dialog label
		border_tileset transparent
		"Happy birthday to you!\nHappy birthday to you!"
		"Happy birthday, dear %PLAYER%!\nHappy birthday to you!"
	};
	wait 200ms;
	entity "Stunt Cake" animation -> 2 once;
	entity "Stunt Cake" current_animation = 3;
	camera fade out -> #000000 over 0ms;
	camera = geometry "cake-camera-2";
	camera fade in -> #000000 over 500ms;
	show dialog {
		ZIPPY "Now you're an official mage, %PLAYER%!"
		PLAYER "Blowing out the candle didn't make me a mage, %Aunt Zippy%!"
		ZIPPY "Well, you're officially sixteen now!"
		PLAYER "I'm pretty sure I officially turned sixteen at midnight last night."
		ZAPPY "Well, you're officially our favorite birthday mage!"
		PLAYER "Aw, thanks, %Uncle Zappy%!"
		ZIPPY "All right, everyone! Dig in!"
	};
	camera fade out -> #000000 over 500ms;
	entity "Cake" current_animation = 4;
	show dialog {
		name " "
		border_tileset menu
		"*munch munch munch*"
	};
	camera fade in -> #000000 over 1000ms;
	show dialog {
		ZAPPY
		"How was your day, %PLAYER%? Are you getting used to Ring Zero?"

		PLAYER
		"Yeah, today was pretty wild. I went around town and fixed a lot of the stuff the Big Bad had ruined."
		"I changed the color of some ether nettle blossoms, teleported some sheep, and impersonated a cat!"
		"Oh, and I changed my name to Bob and drank some Cactus Cooler with a stone golem."

		ZIPPY "I love that the water feature in the center of town is unglitched again. It really feels like old times."
		ZAPPY "But then that earthquake hit. That was like old times, too."

		PLAYER
		"Yeah, that was scary."
		"I guess it's a sign that the Big Bad might be coming back."
	};
	wait 180ms;
	player direction = south;
	wait 400ms;
	show dialog {
		PLAYER "First thing tomorrow, I'm supposed to meet the village elders and visit the castle."
		ZAPPY "They're really sending you to get the artifacts, then?"
	};
	player direction = east;
	show dialog {
		PLAYER
		"I guess so. The village elders say the artifacts are the only thing that can stop the Big Bad for good."

		ZIPPY
		"It's a lot to ask a sixteen year old, but if Ring Zero chose you...."
		"Well, perhaps you're exactly the right mage for the job."

		ZAPPY
		"In which case we'd best get to bed soon. You'll need a good night's rest."

		PLAYER
		"Yeah."
		"Good night, %Aunt Zippy%, %Uncle Zappy%."
	};
	camera fade out -> #000000 over 1200ms;
	wait 200ms;
	goto "cutscene-ch2-birthday-exit";
}
"cutscene-ch2-birthday-exit" {
	"ch2-cutseen-birthday" = true;
	warp_state = "cutscene-escort";
	load map "ch2-town";
}

/* --- CUTSCENE: Birthday (elders) --- */

// helper scripts
"gossip-right-down" {
	wait 600ms;
	self direction = east;
	wait 800ms;
	self direction = south;
	wait 400ms;
}
"gossip-left-and-right-down" {
	self direction = west;
	wait 600ms;
	self direction = south;
	wait 300ms;
	self direction = east;
	wait 360ms;
}
"gossip-left-down" {
	wait 200ms;
	self direction = south;
	wait 500ms;
	self direction = west;
	wait 1100ms;
}

// cutscene
"ch2-escort-cutscene" {
	if (debug_mode) {
		serial_control = on;
		command "skip" = "cutscene-ch2-escort-exit";
		show serial_dialog {
			" "
			"<y>DEBUG></> <c>SKIP</>: skip this cutscene"
		};
	} else {
		serial_control = off;
	}
	camera fade out -> #000000 over 0ms;
	player_control = off;
	hex_editor = closed;
	entity "Alfonso" position = geometry "birthday-alfonso";
	entity "Bert" position = geometry "birthday-bert";
	entity "Jackob" position = geometry "birthday-jackob";
	entity "Alfonso" direction = north;
	entity "Bert" direction = north;
	entity "Jackob" direction = north;
	camera = geometry "escort-camera";
	camera fade in -> #000000 over 1000ms;
	player direction = south;
	wait 800ms;
	player position = geometry "walk_from-magehouse";
	player position -> geometry "walk_from-magehouse" length over 400ms;
	wait 1200ms;
	show dialog {
		PLAYER "Oh, um...."
		Alfonso "The time has come to tackle the first challenge, %PLAYER%."
		PLAYER "Yeah."
		Alfonso "Ready to go?"
		PLAYER "Yeah. I mean, no. I mean...."
	};
	wait 150ms;
	player on_tick = "look-left-and-right-fast";
	wait 300ms;
	show dialog {
		PLAYER "Wait, weren't you supposed to bring me a book this morning? A book about the cereal castle?"
	};
	player on_tick = null_script;
	player direction = south;
	show dialog {
		Jackob
		"The serial castle, yes. We did have a book about that."
		"But... it's gone."

		PLAYER "Gone? You lost the book?"
		Alfonso "No, not lost! It's just missing!"
		PLAYER "But you're in charge of the library! How did you lose a whole book?"
		
		Bert
		"We will keep looking for it. But first you go."
		"More important for you to get started right away."
		
		Alfonso "You have demonstrated incredible competence with what you've already done with Ring Zero, so we are confident you can work your way through the castle on your own."
	};
	wait 100ms;
	player on_tick = "look-left-and-right-fast";
	wait 200ms;
	show dialog {
		PLAYER "On my own? You aren't coming inside with me?"
	};
	player on_tick = null_script;
	player direction = south;
	show dialog {
		Jackob "We are not hackers and cannot pass the gate."
		Alfonso "Ring Zero should grant you access...."
		PLAYER "\"Should?\""
	};
	wait 150ms;
	entity "Alfonso" direction = south;
	wait 500ms;
	show dialog {
		Alfonso "If I remember correctly...."
		PLAYER "You don't know? What kind of wise old men are you?"
		Jackob "We'll certainly see, won't we?"
	};
	entity "Alfonso" direction = player;
	show dialog {
		PLAYER "That book... what did it say? Can you give me a summary at least?"
	};
	entity "Jackob" on_tick = "gossip-right-down";
	entity "Alfonso" on_tick = "gossip-left-and-right-down";
	entity "Bert" on_tick = "gossip-left-down";
	wait 200ms;
	show dialog {
		Jackob "I must have read it once...."
		Alfonso "Wasn't it something about...?"
		Bert "I don't know."
	};
	wait 500ms;
	entity "Alfonso" on_tick = null_script;
	entity "Jackob" on_tick = null_script;
	entity "Bert" on_tick = null_script;
	wait 100ms;
	entity "Alfonso" direction = north;
	wait 50ms;
	entity "Bert" direction = north;
	wait 40ms;
	entity "Jackob" direction = north;
	show dialog {
		Jackob "I can't remember."
		Alfonso "Something about rooms... and magic words...."
		PLAYER "Ho boy."
		
		Jackob
		"We had hoped that book would give you a leg up, but ultimately, I doubt it was that important, %PLAYER%."
		"Instead you will need to rely on XA, who will be able to provide you with a great deal of information."
		
		PLAYER "Exay?"
		Bert "XA sits just inside. Is robot."
		Alfonso "It literally has insider information about the place."
		
		Jackob
		"I realize it's disappointing that we cannot help you very much in this chapter of your journey as a mage."
		"But we promise you, %PLAYER%, that you will not be entirely on your own."
		"Trust us. You'll be just fine."

		Alfonso "You will not be in danger at any point!"
		Bert "Unless Big Bad comes back."
		Alfonso "Yes! And that is why you must hurry!"
		PLAYER "All right. Let's not waste any more time."
	};
	"ch2-escort-cutscene" = true;
	wait 160ms;
	entity "Jackob" on_tick = "birthday-march-on-own-path";
	wait 180ms;
	entity "Alfonso" on_tick = "birthday-march-on-own-path";
	wait 90ms;
	entity "Bert" on_tick = "birthday-march-on-own-path";
	player on_tick = "birthday-march-on-own-path-player";
}
"birthday-march-on-own-path" {
	self position -> geometry "%ENTITY_PATH%" length over 2400ms;
	goto "yeet";
}
"birthday-march-on-own-path-player" {
	wait 300ms;
	player position -> geometry "hesitation-point" origin over 300ms;
	wait 600ms;
	player position -> geometry "birthday-alfonso" origin over 400ms;
	player direction = west;
	wait 250ms;
	camera -> player position over 400ms;
	wait 300ms;
	show dialog {
		PLAYER "All right. Take a deep breath.\nYou can do it, %PLAYER%."
	};
	warp_state = "none";
	entity "Jackob" on_tick = yeet;
	entity "Alfonso" on_tick = yeet;
	entity "Bert" on_tick = yeet;
	player_control = on;
	goto null_script;
}
"cutscene-ch2-escort-exit" {
	warp_state = "walk_from-east";
	load map "ch2-castle-outside";
}

/* --- AND THE REST OF THE OWL --- */
