include "header.mgs";

/* ---------- LOOK SCRIPTS ---------- */

"look-ch2-lodge-rtfm" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked around at the <c>RTFM ROOM</>."
		"\tThe books in this room seem older than those in the rest of the library. More leather bindings, more handwritten pages. The air is mustier, but not in an unpleasant way."
		" "
	};
}

"look-ch2-hackbook" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the book called \"<m>%SELF%</>.\""
		"\tWorn, and apparently forgotten. It's splayed on the ground as if it had been knocked down unknowingly."
	};
}

"look-ch2-bitsbytes" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the book called \"<m>%SELF%</>.\""
		"\tThe cover is crisp and clean as if the book were brand new. The bright and simple geometric design on the cover is the epitome of a textbook choosing to not commit to its subject."
	};
}

"look-ch2-entities" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the book called \"<m>%SELF%</>.\""
		"\tThe page edges are misaligned as if the book were bound by hand. The gold leaf lettering on the cover certainly looks hand done. It's one of a set of four."
	};
}

"look-ch2-hexediting" {
	show serial_dialog spacer;
	show serial_dialog "books-extra" { // referred to elsewhere
		"You looked at the book called \"<m>%SELF%</>.\""
		"\tThis tome resembles the hand-made \"Entities\" books, but this one was more neatly constructed and is less worn. It's one of a set of two."
	};
}

"look-ch2-hexediting2" {
	show serial_dialog spacer;
	show serial_dialog "books-extra";
}

"look-ch2-redletters" {
	show serial_dialog spacer;
	show serial_dialog "red-letter-pamphlet" { // referred to elsewhere
		"You looked at the book called \"<m>%SELF%</>.\""
		"\tThis sheet -- and its nearby counterpart -- are ripped on the left side, as if torn from a bound book. The paper is yellowed, particularly on the edges, and the ink is smeared in places."
		"\tSome words are written in an ominous red."
	};
}

"look-ch2-bookorigins" {
	show serial_dialog spacer;
	show serial_dialog "red-letter-pamphlet";
}



/* ---------- ON_LOAD ---------- */

"on_load-ch2-lodge-rtfm" {
	// entrance text
	show serial_dialog spacer;
	show serial_dialog { "Entering <bold>RTFM ROOM</>..." };
	// do unique stuff first (but player_control = serial / off first, if you do)
	
	// Village elders hiding
	if ("ch2-storyflag-round" != 1) {
		entity "Bert" on_tick = yeet;
	}
	if ("ch2-storyflag-round" != 3) {
		entity "Jackob" on_tick = yeet;
		entity "Alfonso" on_tick = yeet;
	}
	// unique stuff done
	copy!("ch2-map-init")
}

/* ---------- ON_TICK ---------- */

"on_tick-ch2-lodge-rtfm" {
	if (player intersects geometry "south-hitbox") {
		warp_state = "walk_from-north";
		load map "ch2-lodge";
	}
}

/* ---------- EXIT STUFF ---------- */

"on_go-ch2-lodge-rtfm-south" {
	copy!("warping-out-fade-out")
	load map "ch2-lodge";
}

/* ---------- ENTITIES ---------- */

"interact-ch2-alfonso-rtfm" {
	// divert people trying to hack their way to see this convo early
	if ("ch2-storyflag-round" != 3) {
		goto null_script;
	}
	copy!("face-player")
	show dialog {
		SELF
		"Bert said he was going to check something, but whatever he's doing, he's sure taking his time."
		"I confess I'm getting worried, %PLAYER%. Something isn't right about all of this."
	};
	self direction = west;
}
"interact-ch2-jackob-rtfm" {
	// divert people trying to hack their way to see this convo early
	if ("ch2-storyflag-round" != 3) {
		goto null_script;
	}
	copy!("face-player")
	show dialog {
		SELF
		"We're doing just fine here, %PLAYER%, but don't you dawdle too long. Time is slipping away from us."
		"There's so little we can do to help you right now, yet all I feel is this increasing urgency, like a vise tightening around my chest."
		"Doom is coming, and only you can head it off. Please hurry."
	};
	self direction = south;
}
"interact-ch2-bert-rtfm" {
	// divert people trying to hack their way to see this convo early
	if ("ch2-storyflag-round" != 1) {
		goto null_script;
	}
	copy!("face-player")
	show dialog {
		SELF
		"This morning we could not find the book you need, but I want to find it still."
		"Still I am looking. I will bring it if I find it."
	};
	self direction = north;
}

