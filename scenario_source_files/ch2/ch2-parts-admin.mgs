include "header.mgs";
/* ---------- DEBUG: Storyflag rounds ---------- */

// debug: "win" that storyflag round

"debug-get-items-for-round1" {
	copy!(ch2_pickup_monitor)
	copy!(ch2_pickup_heatsink)
	copy!(ch2_pickup_powersupply)
}
"debug-get-items-for-round2" {
	copy!(ch2_pickup_keyboard)
	copy!(ch2_pickup_mouse)
	copy!(ch2_pickup_needle)
	copy!(ch2_pickup_plate)
	copy!("ch2-solder-harddrive")
}
"debug-get-items-for-round3" {
	copy!(ch2_pickup_ramchips)
	copy!(ch2_pickup_clock)
	copy!(ch2_pickup_goldfish)
	copy!(ch2_pickup_abacus)
	copy!("ch2-solder-cpu")
}
"debug-set-flags-to-round0" {
	serial_control = on;
	"ch2-has-artifact" = true;
	hex_control = on;
	"current-chapter" = 2;
	ch2_cutseen_birthday = true;
	// debug!("<y>debug-set-flags-to-round0")
}
"debug-jump-to-round1" {
	copy!("debug-set-flags-to-round1")
	copy!(ch2_load_castle_1_special)
}
"debug-jump-to-round2" {
	copy!("debug-set-flags-to-round2")
	copy!(ch2_load_castle_1_special)
}
"debug-jump-to-round3" {
	copy!("debug-set-flags-to-round3")
	copy!(ch2_load_castle_1_special)
}
"debug-jump-to-round4" {
	copy!("debug-set-flags-to-round4")
	copy!(ch2_load_castle_1_special)
}

"debug-set-flags-to-round1" {
	copy!("debug-set-flags-to-round0")
	copy!("debug-finish-toot")
	copy!("debug-map-flags-for-round1")
}
"debug-set-flags-to-round2" {
	copy!("debug-set-flags-to-round1")
	copy!("debug-get-items-for-round1")
	copy!("ch2-install-heatsink")
	copy!("ch2-install-powersupply")
	copy!("ch2-install-monitor")
	ch2_cutseen_bert_secret = true;
	"ch2-lambda-bert-backstory" = true;
	"ch2-parts-round-2-backstory" = true;
	copy!("debug-map-flags-for-round2")
}
"debug-set-flags-to-round3" {
	copy!("debug-set-flags-to-round2")
	copy!("debug-get-items-for-round2")
	copy!("ch2-install-harddrive")
	copy!("ch2-install-keyboard")
	copy!("ch2-install-mouse")
	ch2_cutseen_bert_message = true;
	"ch2-parts-round-3-backstory" = true;
	copy!("debug-map-flags-for-round3")
}
"debug-set-flags-to-round4" {
	copy!("debug-set-flags-to-round3")
	copy!("debug-get-items-for-round3")
	copy!("ch2-install-ramchips")
	copy!("ch2-install-clock")
	copy!("ch2-install-cpu")
	ch2_cutseen_last_parts_done = true;
	"ch2-warp-granted" = true;
	"ch2-parts-round-4-backstory" = true;
	copy!("debug-map-flags-for-round4")
}

/* ---------- PUT QUEST MARKERS ON MAP ---------- */

"ch2-put_on_map-round1" {
	"ch2-map-monitor" = true;
	"ch2-map-heatsink" = true;
	"ch2-map-powersupply" = true;
}
"ch2-put_on_map-round2" {
	"ch2-map-keyboard" = true;
	"ch2-map-mouse" = true;
	"ch2-map-plate" = true;
	"ch2-map-needle" = true;
}
"ch2-put_on_map-round3" {
	"ch2-map-clock" = true;
	"ch2-map-goldfish" = true;
	"ch2-map-abacus" = true;
	"ch2-map-ramchips" = true;
}
"ch2-put_on_map-round4" {
	"ch2-map-mainframeos" = true;
}
"ch2-take_off_map-round1" {
	"ch2-map-monitor" = false;
	"ch2-map-heatsink" = false;
	"ch2-map-powersupply" = false;
}
"ch2-take_off_map-round2" {
	"ch2-map-keyboard" = false;
	"ch2-map-mouse" = false;
	"ch2-map-plate" = false;
	"ch2-map-needle" = false;
}
"ch2-take_off_map-round3" {
	"ch2-map-clock" = false;
	"ch2-map-goldfish" = false;
	"ch2-map-abacus" = false;
	"ch2-map-ramchips" = false;
}
"ch2-take_off_map-round4" {
	"ch2-map-mainframeos" = false;
}

/* ---------- ACQUIRE ITEM ---------- */

// Also, remove quest marker from map

//ROUND 1

"ch2_pickup_monitor" {
	"ch2-map-monitor" = false;
	ch2_carrying_monitor = true;
	command "look" + "monitor" = "look-monitor";
}
"ch2_pickup_powersupply" {
	"ch2-map-powersupply" = false;
	ch2_carrying_powersupply = true;
	command "look" + "power supply" = "look-powersupply";
}
"ch2-handoff-heatsink-to-cactuscooler" {
	"ch2-map-heatsink" = false;
	"ch2-map-cactuscooler-castle" = true;
}
"ch2-handoff-cactuscooler-to-bobaustin" {
	"ch2-map-cactuscooler-castle" = false;
	"ch2-map-cactuscooler-bobaustin" = true;
}
"ch2_pickup_cactuscooler" {
	"ch2-map-cactuscooler-castle" = false; // just in case you went straight there
	"ch2-map-cactuscooler-bobaustin" = false;
	"ch2-map-heatsink" = true;
	ch2_carrying_cactuscooler = true;
	command "look" + "cactus cooler" = "look-cactuscooler";
}
"ch2_pickup_heatsink" {
	"ch2-cactuscooler-delivered" = true;
	"ch2-map-cactuscooler-castle" = false; // just in case you went straight there
	"ch2-map-heatsink" = false;
	ch2_carrying_heatsink = true;
	command "look" + "heat sink" = "look-heatsink";
}
"ch2-unpickup-cactuscooler" { // DEBUG
	// inverse of the above, lol, sorry for incorrect state
	"ch2-map-cactuscooler-bobaustin" = true;
	"ch2-map-heatsink" = false;
	ch2_carrying_cactuscooler = false;
	delete command "look"  + "cactus cooler";
}

// ROUND 2

"ch2_pickup_keyboard" {
	"ch2-map-keyboard" = false;
	ch2_carrying_keyboard = true;
	command "look" + "keyboard" = "look-keyboard";
}
"ch2_pickup_mouse" {
	"ch2-map-mouse" = false;
	ch2_carrying_mouse = true;
	command "look" + "mouse" = "look-mouse";
}
"ch2_pickup_plate" {
	"ch2-map-plate" = false;
	ch2_carrying_plate = true;
	if ("ch2_carrying_needle") {
		"ch2-map-solder" = true;
	}
	command "look" + "plate" = "look-plate";
}
"ch2_pickup_needle" {
	"ch2-map-needle" = false;
	ch2_carrying_needle = true;
	if ("ch2_carrying_plate") {
		"ch2-map-solder" = true;
	}
	command "look" + "needle" = "look-needle";
}
"ch2-solder-harddrive" {
	copy!("ch2-install-plate")
	copy!("ch2-install-needle")
	"ch2-map-solder" = false;
	ch2_carrying_harddrive = true;
	command "look" + "hard drive" = "look-harddrive";
}

// ROUND 3

"ch2_pickup_clock" {
	"ch2-map-clock" = false;
	ch2_carrying_clock = true;
	command "look" + "clock" = "look-clock";
}
"ch2_pickup_goldfish" {
	"ch2-map-goldfish" = false;
	ch2_carrying_goldfish = true;
	if ("ch2_carrying_abacus") {
		"ch2-map-solder" = true;
	}
	command "look" + "goldfish" = "look-goldfish";
}
"ch2_pickup_manual" {
	"ch2-map-manual" = false;
	"ch2-map-abacus" = true;
	ch2_carrying_manual = true;
	command "look" + "abacus" = "look-abacus";
}
"ch2-handoff-abacus-to-manual" {
	"ch2-map-abacus" = false;
	"ch2-map-manual" = true;
}
"ch2_pickup_abacus" {
	"ch2-map-abacus" = false;
	ch2_carrying_abacus = true;
	if ("ch2_carrying_goldfish") {
		"ch2-map-solder" = true;
	}
	command "look" + "abacus" = "look-abacus";
}
"ch2-solder-cpu" {
	copy!("ch2-install-goldfish")
	copy!("ch2-install-abacus")
	"ch2-map-solder" = false;
	ch2_carrying_cpu = true;
	command "look" + "cpu" = "look-cpu";
}
"ch2-handoff-ramchips-to-seamoss" {
	"ch2-map-ramchips" = false;
	"ch2-map-seamoss" = true;
}
"ch2_pickup_ramchips" {
	"ch2-map-ramchips" = false;
	"ch2-map-seamoss" = false;
	ch2_carrying_ramchips = true;
	command "look" + "ram chips" = "look-ramchips";
	command "look" + "ram" = "look-ramchips";
}

// ROUND 4

"ch2_pickup_mainframeos" {
	"ch2-map-mainframeos" = false;
	ch2_carrying_mainframeos = true;
	command "look" + "mainframeos" = "look-mainframeos";
}

/* ---------- ITEM "INSTALL" ---------- */

"ch2-install-monitor" {
	ch2_carrying_monitor = false;
	ch2_installed_monitor = true;
	delete command "look"  + monitor;
}
"ch2-turn_in-cactuscooler" {
	ch2_carrying_cactuscooler = false;
	delete command "look"  + "cactus cooler";
	// picking up heatsink is currently a separate step
}
"ch2-install-heatsink" {
	ch2_carrying_heatsink = false;
	ch2_installed_heatsink = true;
	delete command "look"  + "heat sink";
}
"ch2-install-powersupply" {
	ch2_carrying_powersupply = false;
	ch2_installed_powersupply = true;
	delete command "look"  + "power supply";
}
"ch2-install-keyboard" {
	ch2_carrying_keyboard = false;
	ch2_installed_keyboard = true;
	delete command "look"  + keyboard;
}
"ch2-install-mouse" {
	ch2_carrying_mouse = false;
	ch2_installed_mouse = true;
	delete command "look"  + mouse;
}
"ch2-install-harddrive" {
	ch2_carrying_harddrive = false;
	ch2_installed_harddrive = true;
	delete command "look"  + "hard drive";
}
"ch2-install-plate" {
	ch2_carrying_plate = false;
	ch2_installed_plate = true;
	delete command "look"  + plate;
}
"ch2-install-needle" {
	ch2_carrying_needle = false;
	ch2_installed_needle = true;
	delete command "look"  + needle;
}
"ch2-install-ramchips" {
	ch2_carrying_ramchips = false;
	ch2_installed_ramchips = true;
	delete command "look"  + "ram chips";
	delete command "look"  + ram;
}
"ch2-install-clock" {
	ch2_carrying_clock = false;
	ch2_installed_clock = true;
	delete command "look"  + clock;
}
"ch2-install-cpu" {
	ch2_carrying_cpu = false;
	ch2_installed_cpu = true;
	delete command "look"  + cpu;
}
"ch2-install-goldfish" {
	ch2_carrying_goldfish = false;
	ch2_installed_goldfish = true;
	delete command "look"  + goldfish;
}
"ch2-install-abacus" {
	ch2_carrying_abacus = false;
	ch2_installed_abacus = true;
	delete command "look"  + abacus;
}
"ch2-install-mainframeos" {
	ch2_carrying_mainframeos = false;
	ch2_installed_mainframeos = true;
	delete command "look"  + mainframeos;
}

//--------------------------- SOLDER ZONE ---------------------------\\

"interact_ch2_solderzone_harddrive" {
	show dialog {
		PLAYER "Got the plate and the needle. All right, let's solder!"
		name "" "(You created a hard drive!)"
	};
	copy!("ch2-solder-harddrive")
}
"interact_ch2_solderzone_cpu" {
	show dialog {
		PLAYER "Got the goldfish and the abacus. All right, let's solder!"
		name "" "(You created a cpu!)"
	};
	copy!("ch2-solder-cpu")
}
"interact_ch2_solderzone" {
	if (!"ch2-parts-round-2-backstory") {
		show dialog {
			PLAYER "It's a metal pen resting inside a metal coil. But why is the pen plugged in? What does it do?"
		};
		return;
	}
	if (!"ch2-solder-sight") {
		show dialog {
			PLAYER "So this is a solder station."
		};
		"ch2-solder-sight" = true;
	}
	// plate + needle = harddrive
	if ("ch2_storyflag_round" == 2) {
		if (
			!"ch2_installed_plate"
			|| !"ch2_installed_needle"
		) {
			if ("ch2_carrying_plate") {
				if (!ch2_carrying_needle) {
					show dialog {
						PLAYER "I have the plate. Now I just need the needle, and then I can solder them together here."
					};
				} else {
					copy!("interact_ch2_solderzone_harddrive")
				}
			} else if ("ch2_carrying_needle") {
				if (!ch2_carrying_plate) {
					show dialog {
						PLAYER "I have the needle. Now I just need the plate, and then I can solder them together here."
					};
				} else {
					copy!("interact_ch2_solderzone_harddrive")
				}
			} else {
				show dialog {
					PLAYER "When I have the plate and the needle, I can solder them together here."
				};
			}
		} else {
			show dialog { PLAYER "All done soldering for now." };
		}
	}
	// goldfish + abacus = cpu
	else if ("ch2_storyflag_round" == 3) {
		if (
			!"ch2_installed_goldfish"
			|| !"ch2_installed_abacus"
		) {
			if ("ch2_carrying_goldfish") {
				if (!ch2_carrying_abacus) {
					show dialog {
						PLAYER "I have the goldfish. Now I just need the abacus, and then I can solder them together here."
					};
				} else {
					copy!("interact_ch2_solderzone_cpu")
				}
			} else if ("ch2_carrying_abacus") {
				if (!ch2_carrying_goldfish) {
					show dialog {
						PLAYER "I have the abacus. Now I just need the goldfish, and then I can solder them together here."
					};
				} else {
					copy!("interact_ch2_solderzone_cpu")
				}
			} else {
				show dialog {
					PLAYER "When I have the goldfish and the abacus, I can solder them together here."
				};
			}
		} else {
			show dialog { PLAYER "All done soldering for now." };
		}
	} else {
		show dialog {
			PLAYER "Guess I'm all done soldering."
		};
	}
}