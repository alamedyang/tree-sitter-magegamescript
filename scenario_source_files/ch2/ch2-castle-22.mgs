include "header.mgs";

/* ---------- LOOK SCRIPTS ---------- */

"look-ch2-castle-22" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked around the <c>SERVER ROOM</>."
		"\tRacks upon racks of servers hum with a buzz so deep you can feel it through your bones. Cold air flows in from somewhere, presumably to keep the stacks cool, but it rustles your hat and clothes."
		" "
	};
}

"look-ch2-ckwatt" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tThe purple fur of this rabbit-like creature looks incredibly soft. You're tempted to pet it, but that may in fact be a rude thing to do to a sapient creature. Rude, or weird."
	};
}

"look-ch2-scuzzy" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tHis cool, slick hair looks like it would take a lot of time to care for. %SELF% might spend as much time on it as he does his work, even when catastrophe had struck the castle."
	};
}

/* ---------- ON_LOAD ---------- */

"on_load-ch2-castle-22-doorless" {
	// abbreviated copy-pasta of the below
	"ch2-in-room" = 22;
	// entrance text
	show serial_dialog spacer;
	show serial_dialog { "Entering <bold>SERVER ROOM</>..." };
	if (warp_state == "walk_from-south") {
		player position = geometry "walk_from-south";
	} else if (warp_state == "walk_from-north") {
		player position = geometry "walk_from-north";
	}
	copy!("ch2-map-init")
}
"on_load-ch2-castle-22" {
	if (warp_state == "ch2 ending") {
		goto "ch2-castle-22-ending";
	}
	// ch2 room state
	"ch2-in-room" = 22;

	// entrance text
	show serial_dialog spacer;
	if (!"ch2-seen-room-22") {
		show serial_dialog { "Discovered <bold><c>SERVER ROOM</>! Room added to <c>MAP</>!" };
		"ch2-seen-room-22" = true;
	} else {
		show serial_dialog { "Entering <bold>SERVER ROOM</>..." };
	}

	// room unique stuff
	if (
		"ch2-installed-powersupply"
		|| "ch2-carrying-powersupply"
	) {
		copy!("ch2-hide-powersupply")
	}
	
	copy!("ch2-map-init")
}

/* ---------- ON_TICK ---------- */

"on_tick-ch2-castle-22" {}
"on_tick-ch2-castle-22-doorless" {
	if (player intersects geometry "south-hitbox") {
		copy!("on_exit-ch2-castle-22")
		warp_state = "walk_from-north";
		copy!("ch2-load-castle-21-special")
	} else if (player intersects geometry "north-hitbox") {
		copy!("on_exit-ch2-castle-22")
		warp_state = "walk_from-south";
		copy!("ch2-load-castle-23-special")
	}
}

/* ---------- EXIT STUFF ---------- */

"on_exit-ch2-castle-22" { // sanitize ch2 room state
	"ch2-in-room" = 0;
}

// SERIAL: go

"on_go-castle-22-north" {
	copy!("on_exit-ch2-castle-22")
	copy!("warping-out-fade-out")
	copy!("ch2-load-castle-23-special")
}
"on_go-castle-22-south" {
	copy!("on_exit-ch2-castle-22")
	copy!("warping-out-fade-out")
	copy!("ch2-load-castle-21-special")
}

/* ---------- ENTITIES ---------- */

"interact-ch2-scuzzy" {
	player_control = off;
	copy!("interrupt-walk")
	if (!"ch2-scuzzy-backstory") {
		show dialog {
			SELF "The quake caused a lot of infrastructure damage around here. We're trying to reroute things and compensate with software."
		};
		"ch2-scuzzy-backstory" = true;
	} else {
		show dialog {
			SELF "In a metaphorical sense, the castle a computer."
			"Each room has its specialized function, and resources and information may flow between rooms when they are connected."
			"In isolation each room -- and each of us -- are capable of little. But when we can connect people to each other, we can collaborate and build amazing things."
		};
		copy!("resume-walk")
		wait 500ms;
		copy!("interrupt-walk")
		show dialog {
			SELF "I mean, the castle is also a computer in a LITERAL sense, too...."
		};
	}
	copy!("resume-walk")
	player_control = on;
}
"interact-ch2-ckwatt" {
	copy!("face-player")
	if (!"ch2-ckwatt-backstory") {
		show dialog {
			SELF "Huh? You need a power supply?"
			"Go ahead and take this one."
			PLAYER "You're sure?"
			SELF "Sure. My new rig needed something more beefy than this, so I bought a better one a while back. This one is extra."
			PLAYER "Neat. Thank you!"
			SELF "No prob."
		};
		"ch2-ckwatt-backstory" = true;
		"ch2-powersupply-permission" = true;
	} else {
		show dialog {
			SELF "When you upgrade computers regularly, you tend to accumulate extra stuff. After a while, it can start to take up a lot of space."
			"Glad that old power supply will see some use."
		};
	}
	self direction = south;
}
"interact-ch2-ckwatt-end" {
	copy!("face-player")
	show dialog { SELF "I wanna get down to the great hall, but we're not quite done yet. Don't wanna be sloppy."};
	self direction = south;
}
"interact-ch2-scuzzy-end" {
	copy!("face-player")
	show dialog { SELF "I'm glad the king and his staff were able to clear the debris blocking the doors. Not that we can leave yet -- still more to do."};
	self direction = south;
}

/* ---------- POWER SUPPLY (ADMIN) ---------- */

"ch2-hide-powersupply" {
	entity "Power Supply" position = geometry "hiding-spot";
	entity "Power Supply" name = " ";
}

"ch2-touch-powersupply" {
	if (!"ch2-powersupply-permission") {
		show dialog {
			PLAYER "This has a power cable coming out of it, so it must be the power supply."
			"But it probably belongs to someone. I better ask permission before taking it."
		};
		return;
	}
	if (!"interrupt") {
		show dialog {
			PLAYER "All right, power supply!"
		};
		"interrupt" = true;
	}
	if ("ch2-powersupply-permission") {
		show dialog {
			name "" "(Pick up the power supply?)"
			> "Yes" = "ch2-touch-powersupply-yes"
			> "No" = "ch2-touch-powersupply-no"
		};
	}
}

"ch2-touch-powersupply-yes" {
	show dialog {
		name "" "(You pick up the power supply!)"
	};
	copy!("ch2-hide-powersupply")
	copy!("ch2-pickup-powersupply")
	self on_interact = null_script;
}
"ch2-touch-powersupply-no" {
	show dialog {
		PLAYER "Maybe later, then."
	};
	self on_interact = "ch2-touch-powersupply";  // reset after script jump
}
