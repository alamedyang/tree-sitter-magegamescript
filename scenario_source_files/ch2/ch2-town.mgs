include "header.mgs";

/* ---------- LOOK SCRIPTS ---------- */

look_ch2_town {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked around the <c>TOWN</>."
		"\tThere's no place like home! You know practically every pebble on every dirt patch, and every cracked brick in every building."
		" "
	};
}

/* ---------- ON_LOAD ---------- */

ch2_setup_alfonso_verthandi {
	entity Alfonso position = geometry "alfonso-verthandi-spot";
	entity Alfonso direction = west;
	entity Verthandi direction = east;
	entity Alfonso on_interact = interact_ch2_alfonso_verthandi;
	entity Verthandi on_interact = interact_ch2_alfonso_verthandi;
}

on_load_ch2_town {
	// cutscene interrupt:
	if (warp_state == "cutscene-escort") {
		goto ch2_escort_cutscene;
	}

	// entrance text
	show serial_dialog spacer;
	show serial_dialog { "Entering <bold>TOWN</>..." };
	// sending entities to their places
	// elders
	entity Bert on_tick = yeet; // always hiding
	if (ch2_storyflag_round < 5) {
		entity Jackob on_tick = yeet; // always hiding before end
		if (ch2_storyflag_round == 2) {
			copy!(ch2_setup_alfonso_verthandi)
		} else {
			entity Alfonso on_tick = yeet;
		}
	}
	// shepherd
	if (
		ch2_storyflag_round == 1
		|| ch2_storyflag_round == 2
	) {
		entity Shepherd position = geometry shepherd2;
		entity Shepherd direction = east;
		entity Shepherd on_tick = null_script;
	} else if (
		ch2_storyflag_round == 3
		|| ch2_storyflag_round == 4
	) {
		entity Shepherd position = geometry shepherd1;
	}
	// verthandi
	if (ch2_storyflag_round == 0) {
		entity Verthandi position = geometry verthandi1;
		entity Verthandi direction = west;
	} else if (
		ch2_storyflag_round == 3
		|| ch2_storyflag_round == 4
	) {
		entity Verthandi position = geometry verthandi2;
		entity Verthandi direction = north;
	}
	// marta
	if (
		ch2_storyflag_round == 1
		|| ch2_storyflag_round == 2
	) {
		entity Marta position = geometry marta1;
		entity Marta direction = west;
	} else if (
		ch2_storyflag_round == 3
		|| ch2_storyflag_round == 4
	) {
		entity Marta position = geometry marta2;
		entity Marta direction = south;
	}
	// delmar
	if (ch2_delmar_in_house) {
		entity Delmar on_tick = yeet;
	}

	// walking out of doorways:
	player_control = off;
	if (warp_state == "raw_game_load-ch2") {
		copy!(restore_player_state)
	} else if (warp_state == "exit_from-credits") {
		player position = geometry "from-credits-spot";
		camera fade in -> #000000 over 800ms;
	} else if (warp_state == "walk_from-east") {
		player path = "walk_from-east"
		player on_tick = on_tick_ch2_town_walkfadein;
		camera fade in -> #000000 over 400ms;
	} else if (warp_state == "walk_from-west") {
		player path = "walk_from-west";
		player on_tick = on_tick_ch2_town_walkfadein;
		camera fade in -> #000000 over 400ms;
	} else if (warp_state == "walk_from-bobsclub") {
		player position -> geometry "walk_from-bobsclub" length over 200ms;
	} else if (warp_state == "walk_from-bakery") {
		player position -> geometry "walk_from-bakery" length over 200ms;
	} else if (warp_state == "walk_from-lodge") {
		player position -> geometry "walk_from-lodge" length over 200ms;
	} else if (warp_state == "walk_from-greenhouse") {
		player position -> geometry "walk_from-greenhouse" length over 200ms;
	} else if (warp_state == "walk_from-magehouse") {
		player position -> geometry "walk_from-magehouse" length over 200ms;
	} else if (warp_state == "walk_from-oldcouplehouse") {
		player position -> geometry "walk_from-oldcouplehouse" length over 200ms;
	} else if (warp_state == "walk_from-smithfamily") {
		player position -> geometry "walk_from-smithfamily" length over 200ms;
	} else if (warp_state == "walk_from-woprhouse") {
		player position -> geometry "walk_from-woprhouse" length over 200ms;
	} else if (warp_state == "walk_from-bobsclub") {
		player position -> geometry "walk_from-bobsclub" length over 200ms;
	}
	copy!(ch2_map_init)
	if (debug_mode) {
		command cutbug = cutbug;
		command cutbug + auto = cutbug_auto;
		show serial_dialog {
			" "
			"<y>DEBUG></> town</>"
			"<y>></> <c>CUTBUG</>: use player Y position for test"
			"<y>></> <c>CUTBUG AUTO</>: run cutbugs 0-1030"
		};
	}
	copy!(ch2_player_spawn_click_protection)
}

/* ---------- ON_TICK ---------- */

// map loading scripts

on_tick_ch2_town {
	if (player intersects geometry "door-bakery") {
		warp_state = "ch2-town";
		load map "ch2-bakery";
	}
	if (player intersects geometry "door-smithfamily") {
		warp_state = "ch2-town";
		load map "ch2-smithfamily";
	}
	if (player intersects geometry "door-greenhouse") {
		warp_state = "ch2-town";
		load map "ch2-greenhouse";
	}
	if (player intersects geometry "door-magehouse") {
		warp_state = "ch2-town";
		load map "ch2-magehouse";
	}
	if (player intersects geometry "door-lodge") {
		warp_state = "ch2-town";
		load map "ch2-lodge";
	}
	if (player intersects geometry "door-oldcouplehouse") {
		warp_state = "ch2-town";
		load map "ch2-oldcouplehouse";
	}
	if (player intersects geometry "door-bobsclub") {
		warp_state = "ch2-town";
		load map "ch2-bobsclub";
	}
	if (player intersects geometry "door-woprhouse") {
		warp_state = "ch2-town";
		load map woprhouse; // both chapters use the same map
	}
	if (player intersects geometry "east-hitbox") {
		player path = "east-point";
		warp_state = "walk_from-west";
		player on_tick = on_tick_ch2_town_walkfadeout;
		camera fade out -> #000000 over 500ms;
		load map "ch2-town";
	}
	if (player intersects geometry "west-hitbox") {
		player path = "west-point";
		warp_state = "walk_from-east";
		player on_tick = on_tick_ch2_town_walkfadeout;
		camera fade out -> #000000 over 500ms;
		load map "ch2-castle-outside";
	}
}

on_tick_ch2_town_walkfadeout {
	player_control = off;
	player position -> geometry "%ENTITY_PATH%" origin over 3500ms;
	goto null_script;
}
on_tick_ch2_town_walkfadein {
	player position -> geometry "%ENTITY_PATH%" length over 500ms;
	player_control = on;
	goto null_script;
}

/* ---------- EXIT STUFF ---------- */

on_exit_ch2_town {
	// universal behavior
}

on_go_ch2_town_west {
	copy!(on_exit_ch2_town)
	copy!(warping_out_fade_out)
	load map "ch2-castle-outside";
}
on_go_ch2_town_home {
	copy!(on_exit_ch2_town)
	copy!(warping_out_fade_out)
	load map "ch2-magehouse";
}
on_go_ch2_town_bakery {
	copy!(on_exit_ch2_town)
	copy!(warping_out_fade_out)
	load map "ch2-bakery";
}
on_go_ch2_town_library {
	copy!(on_exit_ch2_town)
	copy!(warping_out_fade_out)
	load map "ch2-lodge";
}
on_go_ch2_town_greenhouse {
	copy!(on_exit_ch2_town)
	copy!(warping_out_fade_out)
	load map "ch2-greenhouse";
}
on_go_ch2_town_wopr {
	copy!(on_exit_ch2_town)
	copy!(warping_out_fade_out)
	load map woprhouse; // both chapters use the same map
}
on_go_ch2_town_beatrice {
	copy!(on_exit_ch2_town)
	copy!(warping_out_fade_out)
	load map "ch2-oldcouplehouse";
}
on_go_ch2_town_smith {
	copy!(on_exit_ch2_town)
	copy!(warping_out_fade_out)
	load map "ch2-smithfamily";
}
on_go_ch2_town_bobsclub {
	copy!(on_exit_ch2_town)
	copy!(warping_out_fade_out)
	load map "ch2-bobsclub";
}

/* ---------- ENTITIES ---------- */

look_ch2_townsign {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the <m>%SELF%</>."
		"\tA worn wooden sign featuring the words \"to King Gibson's palace\" -- but \"palace\" is crossed out, and \"castle\" is scrawled above it."
	};
}
look_ch2_lakesign {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the <m>%SELF%</>."
		"\tA worn wooden sign that says \"LINQ PARKING.\""
	};
}
interact_ch2_townsign {
	show dialog {
		PLAYER "\"To King Gibson's castle.\" Oh, but it looks like it used to say \"To King Gibson's palace.\" I wonder why they changed it."
	};
}
interact_ch2_lakesign {
	show dialog {
		name "" "LINQ PARKING"
	};
}
interact_ch2_rake {
	entity Marta on_tick = make_rake_float;
}
interact_ch2_yabbomongo {
	copy!(face_player)
	show dialog {
		SELF "Oi, I'm bummed out! The circle of infinity is broken!"
		"Used to be you could go west and come back out east, and vice versa. But now the west road goes to King Gibson's castle. Bizzare."
	};
}

interact_ch2_marta {
	copy!(face_player)
	if (ch2_storyflag_round == 0) {
		show dialog {
			SELF "Oh! You're heading out west, to the castle in the mountain?"
			PLAYER "Yeah, the serial castle."
			SELF "That old castle always freaked me out a little bit."
			"It's so ancient, and full of such strange humming and clicking noises. I never really want to stay over there for long."
			"...."
			"Oh, I mean... I'm sure it's perfectly safe. Seriously, I hear King Gibson is actually really nice, and --"
			PLAYER "It's okay, I'll face a spooky old castle to protect the town, even if it is a little creepy."
			SELF "You'll do great. Good luck."
		};
	} else if (ch2_storyflag_round <= 2) {
		show dialog {
			SELF "How's it going over there? Learning anything about computers?"
			PLAYER "I guess so. I'm starting to build one, anyway."
			"I've been getting a lot of help, though."
			SELF "That's okay. I'm glad you found a mentor to help you learn and do these things. It's a great jump start to learning a new skill."
			PLAYER "I suppose so."
		};
	} else if (ch2_storyflag_round <= 4) {
		show dialog {
			PLAYER "Hey, %SELF%, I'm looking for Bert. Have you seen him?"
			SELF "Here and there, but not for a while. Do you need him for something?"
			PLAYER "No. Yeah. I don't know."
			"He just said he was going to investigate something, then come right back and tell me what he found out."
			"But I haven't heard anything yet. I'm getting worried."
			SELF "Well, he can't have gotten far. He'll turn up eventually, I'm sure."
			"I'll keep an eye out, though."
		};
	} else {
		show dialog {
			SELF "You've got two artifacts under your belt now. Save some for the rest of us, eh?"
			"In all seriousness, way to go, %PLAYER%!"
		};
	}
	self on_interact = interact_ch2_marta;
}

interact_ch2_shepherd {
	copy!(face_player)
	if (ch2_storyflag_round == 0) {
		show dialog {
			SELF "So, um, I don't really know what's so special about cereal."
			"Like, is the artifact a prize from inside a cereal box?"
			PLAYER "Apparently it's \"serial,\" not \"cereal.\""
			SELF "???" "That's what I said, isn't it? Cereal. Like, granola?"
			PLAYER "No, it's \"serial,\" like... well, I don't know what it's like, but it's not breakfast."
			SELF "Huh, well, I don't really get it, but good luck in the castle."
		};
	} else if (ch2_storyflag_round <= 2) {
		show dialog {
			SELF "You know, it's the weirdest thing, but I could've sworn there were only four sheep before."
		};
		self direction = east;
	} else if (ch2_storyflag_round <= 4) {
		show dialog {
			PLAYER "Hey, have you seen Bert recently?"
			SELF "I saw him coming in and out of the library a few times."
			PLAYER "When was that?"
			SELF "Let's see.... Not for kind of a while, I guess."
			"He sure looked like he was in a hurry though. Especially that last time."
			"He came racing out of the library like something was on fire."
			PLAYER "(Oh my goodness. Was that when he came to see me in the castle?)"
			"(But what happened after that?)"
		};
	} else {
		show dialog {
			SELF "Hey, I heard you built a computer or something. That's so cool! Way to go!"
		};
	}
	self on_interact = interact_ch2_shepherd;
}

interact_ch2_delmar_town {
	copy!(face_player)
	show dialog { SELF alignment TL "...." };
	self direction += 2;
}
interact_ch2_verthandi {
	copy!(interrupt_walk)
	if (ch2_storyflag_round == 0) {
		show dialog {
			SELF "Oh! You're doing the castle today, aren't you?"
			"I believe in you, %PLAYER%!"
		};
	} else if (ch2_storyflag_round <= 2) {
		show dialog {
			SELF "Hello, %PLAYER%. I hope you're making friends and learning all sorts of wonderful things."
			PLAYER "Thank you. I'm doing my best."
		};
	} else if (ch2_storyflag_round <= 4) {
		show dialog {
			PLAYER "Excuse me, Miss %Verthandi%, but have you seen Bert recently?"
			SELF "No, I haven't, I'm afraid. Though... oh."
			PLAYER "What is it?"
		};
		player_control = off;
		self direction += 1;
		wait 1500ms;
		self direction -= 1;
		show dialog {
			SELF "How odd. I thought for a moment I sensed him in distress, far, far away."
			PLAYER "You can sense where he is?"
			SELF "Not with any precision, nor any certainty. I do apologize, %PLAYER%."
			"I'll let him know you're looking for him if I see him today."
			PLAYER "Thank you."
		};
		player_control = on;

	} else {
		show dialog {
			SELF "Oh, how wonderful, %PLAYER%! I knew you could do it! Well done!"
		};
	}
	copy!(resume_walk)
}

interact_ch2_alfonso_verthandi {
	entity Alfonso direction = player;
	entity Verthandi direction = player;
	show dialog {
		Alfonso "You know, miss Verthandi here is very knowledgeable about the nature of reality and our relationship to our digital environment."
	};
	entity Verthandi direction = entity Alfonso;
	show dialog {
		Verthandi "Oh, you're too kind! I am only a hobbyist when it comes to such topics. There is always more to learn."
	};
	entity Verthandi direction = player;
	show dialog {
		Alfonso "In any case, we are brainstorming things we might do to help you on your quest."
		Verthandi "We will at least wish you luck!"
	};
	entity Alfonso direction = entity Verthandi;
	entity Verthandi direction = entity Alfonso;
}

interact_ch2_jackob_end {
	if (ch2_storyflag_round < 5) {
		goto null_script;
	}
	copy!(face_player)
	show dialog {
		SELF "Ah, %PLAYER%, good work today."
	};
	if (!ch1_finished) {
		if (!ch1_unfinished_backstory) {
			show dialog {
				SELF "But you know, it seems like you still haven't finished chapter 1 in this save file."
				"I'm not angry. Just disappointed."
				"Just kidding! If the game didn't want you to beat chapter 2 before chapter 1, it should have tried harder to stop you."
				"In all seriousness, do feel free to give chapter 1 a go."
			};
			ch1_unfinished_backstory = true;
		}
	}
	show dialog {
		Alfonso "Stop getting distracted! You must remember what you've forgotten!"
		Jackob "I can't remember any more \"old songs\", %Alfonso%. This isn't working, and I need a break."
		Alfonso "We don't have time for breaks! Sing \"Danny Boy\" again, and this time, try to really -- really -- remember where you learned it."
		Jackob "The more I sing \"Danny Boy,\" the more I remember the last time I sang it -- which is right here, right now."
	};
}

interact_ch2_alfonso_end {
	if (ch2_storyflag_round < 5) {
		goto null_script;
	}
	copy!(face_player)
	show dialog {
		SELF "I never congratulated you on acquiring the artifact and repairing its fundamental functions, %PLAYER%. Good job."
		"The next challenge will test you further, but you should still try to remember everything you've learned so far."
		"Remember... yes, remember everything you've learned. We all must remember...."
	};
}

interact_ch2_cleo {
	entity Cleo direction = player;
	entity Cat direction = player;
	show dialog ch2_catpuddle { // reused
		Cleo "Hey there, %PLAYER%."
		"Sorry, can't chat now. The cat had a really bad day."
		Cat "Meow."
		PLAYER "What happened?"
		Cleo "He accidentally rolled over into a puddle of water. Threw off his whole groove."
		PLAYER "Aw, I'm so sorry. That's rough."
		Cat "Meow!"
	};
	entity Cleo direction = entity Cat;
	entity Cat direction = entity Cleo;
}

interact_ch2_cat {
	copy!(face_player)
	show dialog ch2_catpuddle;
	self direction = entity Cleo;
}

interact_ch2_hamster {
	copy!(face_player)
	if (ch2_hamstertalk== 0) {
		show dialog {
			SELF "Ohhh... the wheel is still spinning, but the hamster is dead."
		};
		ch2_hamstertalk = 1;
	} else if (ch2_hamstertalk == 1) {
		show dialog {
			SELF "I demand more asbestos!"
		};
		ch2_hamstertalk = 2;
	} else if (ch2_hamstertalk == 2) {
		show dialog {
			SELF "Fungible."
		};
		ch2_hamstertalk = 0;
	}
}
