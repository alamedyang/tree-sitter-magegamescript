include "header.mgs";

add dialog settings {
	label Aurelius {
		entity Aurelius // if this doesn't work, change this to `name` instead of `entity`
		portrait "goldfish"
	}
	label GIBSON {
		entity "King Gibson"
	}
}

/* ---------- LOOK SCRIPTS ---------- */

"look-ch2-castle-13" {
	show serial_dialog "spacer";
	show serial_dialog {
		"You looked around the <c>CASTLE THRONE ROOM</>."
		"\tTall, imposing statues line the walls, regal and bovine. Banners bearing the castle crest hang beside them. The floors gleam in the torchlight. The whole place is fancy enough that it makes you feel like you should stand a little straighter."
		" "
	};
}

"look-ch2-sebastian" {
	show serial_dialog "spacer";
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tYou're not sure whether his glowing green eyes are evidence of powerful magic or whether this is typical for his species. You keep your distance, determined not to learn firsthand."
		"\tThe lizard wizard is frowning at you -- you think."
	};
}

"look-ch2-templeton" {
	show serial_dialog "spacer";
	show serial_dialog {
		"You looked at <m>%SELF%</>."
		"\tYou're not sure what aspect of his gelatinous body might be his face -- if he even has one. His jiggling seems more born from nervousness than aggression, at least."
	};
}

/* ---------- ON_LOAD ---------- */

"on_load-ch2-castle-13-doorless" {
	// abbreviated copy-pasta of the below
	"ch2-in-room" = 13;
	// entrance text
	show serial_dialog "spacer";
	show serial_dialog { "Entering <bold>CASTLE THRONE ROOM</>..." };
	if (warp_state == "walk_from-north") {
		player position = geometry "walk_from-north";
	} else if (warp_state == "walk_from-south") {
		player position = geometry "walk_from-south";
	}
	copy!("ch2-map-init")
}
"on_load-ch2-castle-13" {
	if (warp_state == "ch2 ending") {
		entity "door" on_tick = "yeet";
		entity "King Gibson" position = geometry "king-examine1";
		entity "Sebastian" position = geometry "end-sebastian";
		entity "Templeton" position = geometry "end-templeton1";
		goto "ch2-castle-13-ending";
	}
	// ch2 room state
	"ch2-in-room" = 13;

	// entrance text
	show serial_dialog "spacer";
	if (!"ch2-seen-room-13") {
		show serial_dialog { "Discovered <bold><c>CASTLE THRONE ROOM</>! Room added to <c>MAP</>!" };
		"ch2-seen-room-13" = true;
	} else {
		show serial_dialog { "Entering <bold>CASTLE THRONE ROOM</>..." };
	}

	// room unique stuff

	if (warp_state == "Ch2 King Returns") {
		entity "door" on_tick = "yeet";
		camera = geometry "camera-spot2";
	} else if ("ch2-king-rescued") {
		entity "door" on_tick = "yeet";
		entity "King Gibson" position = geometry "king-examine1";
		entity "Sebastian" position = geometry "end-sebastian";
		entity "Templeton" position = geometry "end-templeton1";
		entity "Aurelius" position = geometry "end-fish";
		entity "Templeton" on_tick = "templeton-catalog-search";
		entity "King Gibson" on_tick = "king-debris-ponder";
	} else {
		entity "King Gibson" on_tick = "yeet";
	}

	if (
		"ch2-installed-goldfish"
		|| "ch2-carrying-goldfish"
	) {
		entity "Aurelius" on_tick = "yeet";
	}

	copy!("ch2-map-init")
	
	// check if you do the cutscene or not
	if (warp_state == "Ch2 King Returns") {
		"ch2-cutseen-castle13" = true; // in case you didn't see the whole prev cutscene, set this flag so the reunion cutscene will play out correctly
		copy!("ch2-cutscene-kingreturns")
	} else {
		if (!"ch2-cutseen-castle13") {
			copy!("ch2-cutscene-castle12")
		}
	}
}

"ch2-skip-13" {
	"ch2-cutseen-castle13" = true;
	load map "ch2-castle-13";
}

"ch2-templeton-pace" {
	self position -> geometry "templeton1" origin over 400ms;
	self position -> geometry "templeton2" origin over 400ms;
	if (!"ch2-templeton-pace") {
		goto "null_script";
	}
}
"ch2-sebastian-paceonce" {
	self position -> geometry "lizard1" origin over 600ms;
	self position -> geometry "lizard2" origin over 600ms;
	goto "null_script";
}
"ch2-sebastian-stomp" {
	entity "Sebastian" animation -> 2 once; // magical slam on the ground
	goto "null_script";
}
"ch2-lizard-lurch" {
	self position -> geometry "lizard-lurch" origin over 300ms;
	goto "null_script";
}
"ch2-sebastian-approach" {
	wait 800;
	entity "Sebastian" position -> geometry "lizard-walk" origin over 800ms;
	entity "Sebastian" position -> geometry "lizard-walk" length over 600ms;
	goto "null_script";
}
"ch2-mage-approach" {
	player position -> geometry "mage-walk" length over 2500ms;
	goto "null_script";
}
"ch2-lizard-withdraw" {
	entity "Sebastian" position -> geometry "lizard-walk" origin over 800ms;
	goto "null_script";
}
$fadeTime = 600ms;
"ch2-cutscene-castle12" {
	if (debug_mode) {
		command "skip" = "ch2-skip-13";
		show serial_dialog {
			" "
			"<y>DEBUG></> <c>SKIP</>: skip this cutscene"
		};
	}
	player_control = off;
	wait 300ms;
	// some kind of arguing
	show dialog {
		Sebastian "...and I say we forget him!"
	};
	player direction = north;
	show dialog {
		PLAYER "...?"
	};
	camera fade out -> black over $fadeTime;
	camera = geometry "camera-spot";
	player position = geometry "mage-walk";
	camera fade in -> black over $fadeTime;
	entity "Templeton" on_tick = "spin_self_clockwise";
	show dialog {
		Templeton "Oh... oh dear."
	};
	entity "Sebastian" on_tick = "ch2-sebastian-paceonce";
	show dialog {
		Sebastian "How many hours has it been? It's clear to me he's not coming back."
		"As usual, he's left us to clean up his messes for him."
		Templeton "But what if he's injured? He could be stuck in there, bleeding out. Maybe he can't reach the intercom. Maybe he's waiting for us to --"
		Sebastian "He won't be \"bleeding out\" after a tiny shake like that."
	};
	"ch2-templeton-pace" = true;
	entity "Templeton" on_tick = "ch2-templeton-pace";
	show dialog {
		Templeton "But what if a piece of debris hit him on the head, and --"
		Sebastian "He has the constitution of an ox!"
	};
	"ch2-templeton-pace" = false;
	entity "Templeton" direction = east;
	show dialog {
		Templeton "Actually, he's a gnu."
	};
	entity "Sebastian" on_tick = "ch2-sebastian-stomp";
	wait 200ms;
	show dialog {
		Sebastian "Enough!"
		"In either case, we must take action before anything worse happens."
	};
	entity "Sebastian" position -> geometry "lizard1" origin over 400ms;
	show dialog {
		Sebastian "We must take administrative control of the castle. We must --"
		Aurelius "Sebastian, wait. Someone is here."
	};
	entity "Templeton" direction = south;
	show dialog {
		Templeton "!!"
	};
	camera fade out -> black over $fadeTime;
	camera = player position;
	player on_tick = "ch2-mage-approach";
	camera fade in -> black over $fadeTime;
	entity "Sebastian" on_tick = "ch2-sebastian-approach";
	wait 2600ms;
	show dialog {
		PLAYER "Sorry. I didn't mean to interrupt anything."
		Sebastian "And who do you think you are?"
		Templeton "Oh! How did you get in? Does this mean the door's been unblocked?"
		PLAYER "No, the doors are still broken. But I have this portable terminal, and --"
	};
	entity "Sebastian" on_tick = "ch2-lizard-lurch";
	wait 150ms;
	player strafe = 2;
	player position -> geometry "mage-backup" origin over 200ms;
	player strafe = 0;
	player direction = east;
	show dialog {
		Sebastian "You have the artifact?"
		"Give that to me. That belongs to --"
		Aurelius "Stop, %Sebastian%. It does not belong to you. Nor to any of us."
	};
	player direction = north;
	show dialog {
		Templeton "It's up to the king who gets to wield it!"
	};
	entity "Sebastian" on_tick = "ch2-lizard-withdraw";
	player direction = east;
	show dialog {
		Sebastian "But the king isn't here, is he? It's up to US --"
	};
	player direction = north;
	show dialog {
		Aurelius "It is up to me, if it is up to anyone. I am regent."
		"You, stranger. What is your name?"
		PLAYER "It's %PLAYER%."
		Aurelius "How is the rest of the castle?"
	};
	if (
		!"ch2-seen-room-21"
		|| !"ch2-seen-room-22"
		|| !"ch2-seen-room-23"
		|| !"ch2-seen-room-31"
		|| !"ch2-seen-room-32"
		|| !"ch2-seen-room-33"
		|| !"ch2-seen-room-34"
		|| !"ch2-seen-room-11"
		|| !"ch2-seen-room-12"
	) {
		show dialog {
			PLAYER "Um, the rooms are in bad shape, but everyone seems okay. The people I've met so far, anyway."
		};
	} else {
		show dialog {
			PLAYER "Um, the rooms are in bad shape, but I can report that everyone is okay."
		};
	}
	show dialog {
		Aurelius "Very good. And that device -- I was under the impression it was inoperative."
		PLAYER "Mostly. I'm trying to fix the castle mainframe, which should get it working a lot better."
		Aurelius "Then I'll let you have at it. But first, if I might ask something of you, %PLAYER%...."
		"We have not seen or heard from King Gibson since last night. You see, prior to that, there was --"
		Templeton "We all had an argument."
		Sebastian "Balderdash. He was overreacting, and I only --"
		Aurelius "Nevertheless, I fear he is trapped in his bedroom, just to the north of us. He's not answering the intercom."
		Templeton "He could be hurt!"
		Sebastian "Bah! He's ignoring us. He could've answered us if he wanted to talk to us. Or he'd've unlocked the door and come out."
		Aurelius "If you would be so kind as to check up on him, please?"
		PLAYER "Sure."
	};
	"ch2-cutseen-castle13" = true;
	player_control = on;
}

"walk-over-1500" {
	self position -> geometry "%ENTITY_PATH%" length over 1500ms;
	goto "null_script";
}
"ch2-cutscene-kingreturns" {
	camera fade out -> black over 1ms;
	player_control = off;
	player direction = entity "King Gibson";
	entity "Templeton" direction = entity "King Gibson";
	entity "Sebastian" direction = entity "King Gibson";
	entity "King Gibson" on_tick = "walk-over-1500";
	wait 200ms;
	player on_tick = "walk-over-1500";
	camera fade in -> black over 600ms;
	entity "Templeton" direction = entity "King Gibson";
	show dialog { Templeton "Your majesty!" };
	entity "Sebastian" direction = entity "King Gibson";
	show dialog { Sebastian "!!" };
	show dialog {
		Aurelius "You are well!"
	};
	entity "King Gibson" direction = entity "Aurelius";
	show dialog {
		GIBSON "Of course. You see, the intercom and doorlocks were broken, and the door failed shut."
		"I regret...."
	};
	entity "King Gibson" direction += 1;
	show dialog {
		GIBSON emote 3 "I regret the things I said yesterday. And... I regret I assumed you had locked me in there. I should have trusted you better."
		Templeton "Oh, Your Majesty! I'm just glad you --"
	};
	entity "King Gibson" direction = entity "Sebastian";
	// King perks up
	show dialog {
		Sebastian "Never mind any of that! This is an emergency!"
		"Can we finally begin some damn rescue operations, or do we need to form a committee to draft a proposal and bring it to the populace to a vote?"
		GIBSON "What have you tried so far?"
	};
	entity "King Gibson" direction = entity "Aurelius";
	show dialog {
		Aurelius "Neither Templeton nor I have the leverage to lift the debris. And Sebastian's telekinesis spell doesn't have enough power on its own."
	};
	entity "King Gibson" direction = entity "Sebastian";
	show dialog {
		Sebastian "I won't burn out my mana trying over and over again. It's obvious it won't work."
	};
	entity "King Gibson" direction = south;
	show dialog {
		GIBSON "Well, maybe I can shift it."
	};
	camera fade out -> black over $fadeTime; // cheaty :P
	player position = geometry "end-player";
	entity "King Gibson" position = geometry "end-king";
	entity "Sebastian" position = geometry "end-sebastian";
	entity "Templeton" position = geometry "end-templeton";
	entity "Aurelius" position = geometry "end-fish";
	player direction = entity "King Gibson";
	camera = geometry "camera-spot3";
	camera fade in -> black over $fadeTime;
	show dialog {
		GIBSON "I can bench press three hundred pounds. I might be able to move this."
		Sebastian "At the speed my mana was draining, it's heavier than that. You'll wear yourself out for no reason."
		"But at least we're focused on the real problem now. Finally!"
		Templeton "This debris wasn't going anywhere, and the king might have been lying in a pool of his own blood, gasping for air, with his every bone broken!"
		Aurelius "Fortunately, the king is all right, and what's done is done. Focus."
		"Neither the king nor Sebastian can move the debris alone, but what about together?"
		Templeton "Oh, yes! But what if you lighten the load with telekinesis, Sebastian?"
		Sebastian "Perhaps, but...."
	};
	entity "Sebastian" position -> geometry "end-sebastian2" origin over 400ms;
	show dialog {
		Sebastian "You there, stranger. The rest of the castle is damaged?"
		PLAYER "Yes. Most doors are crumpled exactly like this."
	};
	show dialog {
		Sebastian "Then I must pace myself. The mana required is the square of the weight being manipulated.... Ah, but if I don't have to introduce lateral motion myself --"
		Aurelius "Yes, a gravitation spell."
	};
	entity "Sebastian" position -> geometry "end-sebastian" origin over 400ms;
	show dialog {
		Sebastian "That's much cheaper. But... blast, I need to check a few numbers in my physics spellbook. Templeton, please tell me you've eaten a copy?"
	};
	entity "Templeton" position -> geometry "end-templeton1" origin over 200ms;
	show dialog {
		Templeton "I-I think so. Let me check. I've got it filed in here somewhere."
	};
	entity "Templeton" on_tick = "templeton-catalog-search";
	show dialog {
		GIBSON "Now we're getting somewhere."
	};
	entity "King Gibson" on_tick = "king-walk-to-debris";
	show dialog {
		GIBSON "I can at least see if there are any pieces of this I can extract on my own."
		Aurelius "%PLAYER%, we're making sufficient progress here. Don't let us keep you."
		"Once the mainframe is repaired, we will be much better equipped to clear all the debris at once."
		PLAYER "Right, of course."
	};
	copy!("interrupt-king-walk")
	entity "King Gibson" direction = player;
	show dialog {
		GIBSON "Good luck. And thank you. Truly."
	};
	copy!("resume-king-walk")
	warp_state = "nothing lol";
	"ch2-king-rescued" = true;
	camera -> player position over 700ms;
	player_control = on;
}
"king-walk-to-debris" {
	self position -> geometry "king-examine1" origin over 1200ms;
	goto "king-debris-ponder";
}

/* ---------- ON_TICK ---------- */

"on_tick-ch2-castle-13-doorless" {
	if (player intersects geometry "south-hitbox") {
		copy!("on_exit-ch2-castle-13")
		warp_state = "walk_from-north";
		copy!("ch2-load-castle-12-special")
	} else if (player intersects geometry "north-hitbox") {
		copy!("on_exit-ch2-castle-13")
		warp_state = "walk_from-south";
		copy!("ch2-load-castle-14-special")
	}
}
"on_tick-ch2-castle-13" {
	if ("ch2-king-rescued") {
		if (player intersects geometry "north-hitbox") {
			warp_state = "walk_from-south";
			copy!("ch2-load-castle-14-special")
		}
	}
}

/* ---------- EXIT STUFF ---------- */

"on_exit-ch2-castle-13" { // sanitize ch2 room state
	"ch2-in-room" = 0;
}

// SERIAL: go

"on_go-castle-13-north" {
	copy!("on_exit-ch2-castle-13")
	copy!("warping-out-fade-out")
	copy!("ch2-load-castle-14-special")
}
"on_go-castle-13-south" {
	copy!("on_exit-ch2-castle-13")
	copy!("warping-out-fade-out")
	copy!("ch2-load-castle-12-special")
}

/* ---------- GOLDFISH (ADMIN) ---------- */


"ch2-touch-goldfish" {
	if (!"ch2-cutseen-castle14") {
		show dialog { // don't use "self"; "Aurelius" is a label!
			Aurelius "Do please check on King Gibson, would you? He's in his bedroom, just to the north of us."
		};
	} else if (!"ch2-king-rescued") {
		copy!("ch2-king-unharmed")
		if ("ch2-king-report") {
			show dialog {
				Aurelius "Godspeed."
			};
		}
		"ch2-king-report" = true;
	} else {
		show dialog {
			Aurelius "Once the mainframe is repaired, we will be much better equipped to clear all the debris at once. Don't let us keep you."
		};
		if ("ch2-storyflag-round" >= 3) {
			show dialog {
				PLAYER "Actually, I could use your help to fix it."
				Aurelius "Of course. Anything."
				PLAYER "I sort of need to borrow... you."
				"We're short a CPU. And I need a volunteer to act as the \"thinking\" part --"
			};
			copy!("interrupt-king-walk")
			copy!("interrupt-templeton-walk")
			entity "King Gibson" current_animation = 2;
			show dialog {
				GIBSON emote 1 "What's this? What are you talking about?"
			};
			entity "King Gibson" current_animation = 0;
			show dialog {
				PLAYER "It'd be temporary. After the mainframe is fixed, we can mail order a real CPU, and we can put you back to normal when it arrives. But for this short little while --"
				Aurelius "I'll do it."
				Templeton "%Aurelius%!"
				Aurelius "The king has returned, so I'm of little use now."
				GIBSON "Don't say that."
				Aurelius "This is an emergency, and I can offer no physical assistance. You've valued me for my mind, haven't you? I've been your advisor. Ambassador."
				GIBSON "And friend!"
				Aurelius "It's only temporary. And it sounds like an interesting adventure. A new perspective... a novel experience. Wouldn't you say?"
				Sebastian "And what if it's not temporary? This doesn't sound safe!"
				Aurelius "I've read a monograph on this very subject. It's safe."
				GIBSON emote 3 "...."
				"If this is really what you want, I won't stop you."
				Aurelius "It will empower the rest of you."
				GIBSON "Very well. We'll continue to do our best here."
			};
			entity "Aurelius" position = geometry "hiding-spot"; // doing yeet now aborts the script early!
			copy!("ch2-pickup-goldfish")
			show dialog {
				name "" "(You acquired a goldfish!)"
				GIBSON "I'm counting on you both. Good luck."
			};
			copy!("resume-king-walk")
			copy!("resume-templeton-walk")
			entity "Aurelius" on_tick = "yeet";
		}
	}
}

/* ---------- ENTITIES ---------- */

"ch2-king-unharmed" {
	if (!"ch2-king-report") {
		show dialog {
			PLAYER "The king is okay, but he's trapped in there. Both the intercom and the keypad lock to his door seem to be broken."
		};
		player direction = entity "Templeton";
		show dialog {
			Templeton "Oh, thank goodness! He's all right!"
		};
		player direction = entity "Aurelius";
		show dialog {
			Aurelius "I see. Thank you."
		};
	}
	show dialog {
		PLAYER "I'll get the king out of there. Don't worry."
	};
}

"interact-ch2-sebastian" {
	copy!("face-player")
	if (!"ch2-cutseen-castle14") {
		show dialog {
			SELF "Mark my words: he's ignoring us. He's all but abdicated."
		};
	} else if (!"ch2-king-rescued") {
		copy!("ch2-king-unharmed")
		if ("ch2-king-report") {
			show dialog {
				SELF "Forget the king. He's on his own."
			};
		}
		"ch2-king-report" = true;
	} else {
		show dialog {
			SELF "%Templeton%, I promise not to yell at you for eating my spellbooks again if you can produce the right page in the next five minutes."
		};
		entity "Templeton" on_tick = "null_script";
		entity "Templeton" direction = entity "Sebastian";
		// pauses fussing
		show dialog {
			Templeton "Darn it, you made me forget the filing number! Now I'll have to go through the card catalog all over again!"
		};
		entity "Templeton" on_tick = "templeton-catalog-search";
	}
}

"interact-ch2-templeton" {
	copy!("face-player")
	if (!"ch2-cutseen-castle14") {
		show dialog {
			SELF "I know King Gibson doesn't always get along with us, but this silence is unlike him. I'm worried."
		};
	} else if (!"ch2-king-rescued") {
		copy!("ch2-king-unharmed")
		if ("ch2-king-report") {
			show dialog {
				SELF "Oh, goodness! Thank you, thank you!"
			};
		}
		"ch2-king-report" = true;
	} else {
		copy!("interrupt-walk")
		show dialog {
			SELF "Thank you for rescuing the king. We were all very worried about him."
			Sebastian "%Templeton%, please don't forget that you're trying to find my physics spellbook. The gravitation spell, please?"
		};
		copy!("resume-walk")
		show dialog {
			SELF "Yes, yes, of course."
		};
	}
}

"interrupt-king-walk" {
	// todo: functions plz :/
	"captured_animation2" = entity "King Gibson" current_animation;
	entity "King Gibson" current_animation = 0;
	pause entity "King Gibson" on_tick;
	if (entity "King Gibson" direction == north) {
		"tempvar2" = 0;
	} else if (entity "King Gibson" direction == east) {
		"tempvar2" = 1;
	} else if (entity "King Gibson" direction == south) {
		"tempvar2" = 2;
	} else if (entity "King Gibson" direction == west) {
		"tempvar2" = 3;
	}
	entity "King Gibson" direction = player;
}
"resume-king-walk" {
	entity "King Gibson" current_animation = "captured_animation2";
	unpause entity "King Gibson" on_tick;
	if ("tempvar2" == 0) {
		entity "King Gibson" direction = north;
	} else if ("tempvar2" == 1) {
		entity "King Gibson" direction = east;
	} else if ("tempvar2" == 2) {
		entity "King Gibson" direction = south;
	} else if ("tempvar2" == 3) {
		entity "King Gibson" direction = west;
	}
}
"interrupt-templeton-walk" {
	// todo: functions plz :/
	"captured_animation3" = entity "Templeton" current_animation;
	entity "Templeton" current_animation = 0;
	pause entity Templeton on_tick;
	if (entity "Templeton" direction == north) {
		"tempvar3" = 0;
	} else if (entity "Templeton" direction == east) {
		"tempvar3" = 1;
	} else if (entity "Templeton" direction == south) {
		"tempvar3" = 2;
	} else if (entity "Templeton" direction == west) {
		"tempvar3" = 3;
	}
	entity "Templeton" direction = player;
}
"resume-templeton-walk" {
	entity "Templeton" current_animation = "captured_animation3";
	unpause entity Templeton on_tick;
	if ("tempvar3" == 0) {
		entity "Templeton" direction = north;
	} else if ("tempvar3" == 1) {
		entity "Templeton" direction = east;
	} else if ("tempvar3" == 2) {
		entity "Templeton" direction = south;
	} else if ("tempvar3" == 3) {
		entity "Templeton" direction = west;
	}
}
"templeton-catalog-search" {
	copy!("tick-rand-mini") // from bob's club basement
	entity "Templeton" position -> geometry "end-templeton2" origin over 200ms;
	copy!("tick-rand-mini") // from bob's club basement
	entity "Templeton" position -> geometry "end-templeton1" origin over 200ms;
	copy!("tick-rand-mini") // from bob's club basement
}
"king-debris-ponder" {
	copy!("tick-rand-mini") // from bob's club basement
	self position -> geometry "king-examine2" origin over 700ms;
	self direction = south;
	copy!("tick-rand-broad") // from bob's club basement
	wait 400ms;
	self position -> geometry "king-examine1" origin over 700ms;
	self direction = south;
	copy!("tick-rand-broad") // from bob's club basement
}

"interact-ch2-gibson-throneroom" {
	copy!("interrupt-walk")
	if ("ch2-storyflag-round" < 3) {
		show dialog {
			GIBSON "My staff and I are reunited, thanks to you."
			"I am in your debt."
		};
	} else if (!"ch2-clock-permission") {
		show dialog {
			PLAYER "So, Your Majesty, about the clock in your bedroom...."
			GIBSON "What? What's this about my heirloom grandfather clock?"
			PLAYER "I sort of want to borrow it to --"
			GIBSON "Fine then. Have it, if you want it."
			PLAYER "Really? Just like that?"
			GIBSON "It's just an object. A thing. Replaceable."
			"You reunited me with my advisors. My friends. That's worth far more to me than a piece of furniture."
			"Take it, if you feel you can make good use of it."
			PLAYER "Thank you very much!"
		};
		"ch2-clock-permission" = true;
	} else {
		if (
			"ch2-installed-goldfish"
			|| "ch2-carrying-goldfish"
		) {
			show dialog {
				GIBSON "Take good care of Aurelius for me."
				PLAYER "I will."
			};
		} else {
			show dialog {
				GIBSON "My staff and I are reunited. You have my thanks."
			};
		}
	}
	copy!("resume-walk")
}

"interact-ch2-sebastian-end" {
	show dialog { SELF "*whew* All the doors are cleared now, so time for a break." };
}
"interact-ch2-templeton-end" {
	show dialog { SELF "Jeez Louise. I hope nothing else goes wrong today." };
}
"interact-ch2-gibson-throneroom-end" {
	copy!("face-player")
	show dialog {
		SELF "Do let me know when we can get Aurelius back."
		PLAYER "Of course."
};
	self direction = south;
}
"interact-ch2-statue" {
	show dialog {
		PLAYER "Neat detail work in the beard. I wonder what kind of stone this is."
	};
}

