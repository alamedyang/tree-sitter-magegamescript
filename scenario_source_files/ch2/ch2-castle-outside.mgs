include "header.mgs";

/* ---------- LOOK SCRIPTS ---------- */

"look_ch2_castle_outside" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked around the <c>CASTLE EXTERIOR</>."
		"\tThe building itself is undeniably ominous with its stark, charcoal-colored stone. You can just make out that the green orbs of light embedded in its walls are flickering slightly. A subtle humming emanates from the nearby pillars. It is no surprise that the townsfolk avoid this place."
		" "
	};
}
"look_ch2_entrancesign" {
	show serial_dialog spacer;
	show serial_dialog {
		"You looked at the <m>%SELF%</>."
		"\tA worn wooden sign inscribed with the words \"King Gibson's castle.\""
	};
}

/* ---------- ON_LOAD ---------- */

"on_load-ch2-castle-outside" {
	player_control = off;
	if (!"ch2-full-escort-cutseen") {
		// todo: cutscene: leading you inside
	}
	// entrance text
	show serial_dialog spacer;
	show serial_dialog { "Entering <bold>CASTLE EXTERIOR</>..." };
	// Village elders hiding
	if ("ch2_storyflag_round" != 0) {
		entity "Bert" on_tick = yeet;
	}
	if (
		"ch2_storyflag_round" == 1
		|| "ch2_storyflag_round" == 2
		|| "ch2_storyflag_round" == 3
		|| "ch2_storyflag_round" >= 5
	) {
		entity "Jackob" on_tick = yeet;
		entity "Alfonso" on_tick = yeet;
	}
	// unique stuff done
	copy!(ch2_map_init) // (player control comes back here)
	// animations
	if (warp_state == "walk_from-north") {
		player position -> geometry "walk_from-north" length over 200ms;
	} else if (warp_state == "walk_from-east") {
		player path = "walk_from-east";
		player on_tick = "on_tick-ch2-town-walkfadein";
			// borrowing this from town scripts
		camera fade in -> #000000 over 400ms;
	} else if (warp_state == "ch2-ending") {
		goto "ch2-post-credits";
	}
}

/* ---------- ON_TICK ---------- */

"on_tick-ch2-castle-outside" {
	if (player intersects geometry "north-hitbox") {
		copy!("on_exit-ch2-castle-outside")
		warp_state = "walk_from-south";
		copy!(ch2_load_castle_1_special)
	}
	if (player intersects geometry "east-hitbox") {
		copy!("on_exit-ch2-castle-outside")
		warp_state = "walk_from-west";
		player path = "east-point";
		player on_tick = "on_tick-ch2-town-walkfadeout";
			// borrowing this from town scripts
		camera fade out -> #000000 over 500ms;
		load map "ch2-town";
	}
}

/* ---------- EXIT STUFF ---------- */

"on_exit-ch2-castle-outside" {
	// universal behavior
}

// SERIAL: go

"on_go-castle-outside-north" {
	copy!("on_exit-ch2-castle-outside")
	copy!(warping_out_fade_out)
	copy!(ch2_load_castle_1_special)
}
"on_go-castle-outside-east" {
	copy!("on_exit-ch2-castle-outside")
	copy!(warping_out_fade_out)
	load map "ch2-town";
}

/* ---------- ENTITIES ---------- */

"interact_ch2_entrancesign" {
	show dialog {
		name "" "King Gibson's castle"
	};
}

"interact_ch2_alfonso_outside" {
	if ("ch2_storyflag_round" == 4) {
		if (!"ch2-alfonso-almostdone") {
			show dialog {
				SELF "Word on the street is that you're almost done repairing the artifact." "Very well done so far, but keep going!"
			};
			"ch2-alfonso-almostdone" = true;
		} else {
			show dialog {
				SELF "Keep going! We need that artifact working!"
			};
		}
	} else {
		show dialog {
			SELF "Go on in! You'll do fine!"
		};
	}
}

"interact_ch2_bert_outside" {
	show dialog {
		SELF "Only hackers may cross door into castle."
	};
}

"interact_ch2_jackob_outside" {
	if ("ch2_storyflag_round" == 4) {
		if (!"ch2-jackob-almostdone") {
			show dialog {
				SELF "There's a buzz around town that you're nearly finished. Very impressive."
			};
			"ch2-jackob-almostdone" = true;
		}
		show dialog {
			SELF "Don't stop now. Get that software installed quick!"
		};
	} else {
		show dialog {
			SELF "XA will be able to help you on the inside. Good luck."
		};
	}
}

"interact_ch2_pedestal" {
	show dialog {
		PLAYER "What a strange symbol. Like a trident, but with different shapes on the tips of the tines: a square, triangle, and circle."
		"But what does it mean?" // Frontier psychiatry
	};
}

"interact_ch2_pillar" {
	show dialog {
		PLAYER "What do these things do?"
	};
}
